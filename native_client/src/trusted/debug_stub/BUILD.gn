# Copyright (c) 2014 The Native ClientAuthors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config/sanitizers/sanitizers.gni")

static_library("debug_stub") {
  sources = [
    "abi.cc",
    "debug_stub.cc",
    "nacl_debug.cc",
    "packet.cc",
    "session.cc",
    "target.cc",
    "thread_common.cc",
    "transport_common.cc",
    "transport_ipc.cc",
    "util.cc",
  ]
  if (current_os != "win") {
    sources += [
      "posix/debug_stub_posix.cc",
      "posix/platform_impl.cc",
      "posix/thread_impl.cc",
    ]
  }
  if (current_os == "win") {
    sources += [
      "win/debug_stub_win.cc",
      "win/platform_impl.cc",
      "win/thread_impl.cc",
    ]
  }
  deps = [
    "//build/config/nacl:nacl_base",
    "//native_client/src/shared/platform:platform",
  ]
}

executable("gdb_rsp_unittest") {
  sources = [
    "abi_test.cc",
    "packet_test.cc",
    "session_test.cc",
    "test.cc",
    "util_test.cc",
  ]

  deps = [
    ":debug_stub",
    "//build/config/nacl:nacl_base",

    # All executables must have the sanitizer deps to properly link in
    # asan mode (this target will be empty in other cases).
    "//build/config:exe_and_shlib_deps",
    "//build/win:default_exe_manifest",
  ]
}

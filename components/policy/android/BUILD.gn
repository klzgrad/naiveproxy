# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

_jni_sources = [
  "java/src/org/chromium/policy/PolicyConverter.java",
  "java/src/org/chromium/policy/CombinedPolicyProvider.java",
]

android_library("policy_java") {
  deps = [
    "//base:base_java",
  ]
  java_files =
      _jni_sources + [
        "java/src/org/chromium/policy/AbstractAppRestrictionsProvider.java",
        "java/src/org/chromium/policy/AppRestrictionsProvider.java",
        "java/src/org/chromium/policy/PolicyProvider.java",
      ]
}

android_library("policy_java_test_support") {
  testonly = true
  deps = [
    ":policy_java",
    "//base:base_java",
    "//base:base_java_test_support",
  ]
  java_files = [
    "javatests/src/org/chromium/policy/test/annotations/Policies.java",
    "javatests/src/org/chromium/policy/test/PolicyData.java",
  ]
}

generate_jni("jni_headers") {
  visibility = [ "//components/policy/*" ]
  sources = _jni_sources
  jni_package = "policy"
}

junit_binary("components_policy_junit_tests") {
  java_files = [
    "junit/src/org/chromium/policy/AbstractAppRestrictionsProviderTest.java",
    "junit/src/org/chromium/policy/CombinedPolicyProviderTest.java",
    "junit/src/org/chromium/policy/PolicyConverterTest.java",
    "junit/src/org/chromium/policy/test/annotations/PoliciesTest.java",
  ]
  deps = [
    ":policy_java",
    ":policy_java_test_support",
    "//base:base_java",
    "//third_party/hamcrest:hamcrest_java",
  ]
}

# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//build/config/features.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//services/service_manager/public/tools/test/service_test.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "font_service_app.cc",
    "font_service_app.h",
  ]

  deps = [
    "//base",
    "//components/services/font/public/interfaces",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//ppapi/buildflags:buildflags",
    "//services/service_manager/public/cpp",
    "//ui/gfx",
  ]

  public_deps = [
    "//skia",
  ]

  if (is_linux && enable_plugins) {
    deps += [ ":ppapi_fontconfig_matching" ]
  }

  if (is_linux) {
    deps += [ "//base/test:fontconfig_util_linux" ]
  }
}

if (is_linux && enable_plugins) {
  source_set("ppapi_fontconfig_matching") {
    sources = [
      "ppapi_fontconfig_matching.cc",
      "ppapi_fontconfig_matching.h",
    ]

    deps = [
      "//base:base",
      "//ppapi/buildflags:buildflags",
      "//ppapi/c",
      "//third_party/fontconfig",
    ]
  }
}

service("font_service") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//base",
    "//mojo/public/c/system",
    "//services/service_manager/public/cpp",
  ]
}

service_manifest("manifest") {
  name = "font_service"
  source = "manifest.json"
}

service_test("font_service_unittests") {
  sources = [
    "font_loader_test.cc",
    "font_loader_test.h",
  ]

  catalog = ":font_service_unittests_catalog"

  deps = [
    "//base",
    "//components/services/font/public/cpp",
    "//components/services/font/public/interfaces",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//ppapi/buildflags:buildflags",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp:service_test_support",
    "//skia",
  ]

  if (enable_plugins) {
    deps += [
      "//ppapi/c",
      "//third_party:freetype_harfbuzz",
    ]
  }

  data_deps = [
    ":font_service",
  ]
}

service_manifest("test_manifest") {
  name = "font_service_unittests"
  source = "test_manifest.json"
}

catalog("font_service_unittests_catalog") {
  embedded_services = [ ":test_manifest" ]
  standalone_services = [ ":manifest" ]
}

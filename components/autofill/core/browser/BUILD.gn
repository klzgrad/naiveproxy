# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")

static_library("browser") {
  sources = [
    "address.cc",
    "address.h",
    "address_combobox_model.cc",
    "address_combobox_model.h",
    "address_field.cc",
    "address_field.h",
    "address_i18n.cc",
    "address_i18n.h",
    "address_normalization_manager.cc",
    "address_normalization_manager.h",
    "address_normalizer.h",
    "address_normalizer_impl.cc",
    "address_normalizer_impl.h",
    "address_rewriter.cc",
    "address_rewriter.h",
    "address_rewriter_rules.cc",
    "address_validation_util.cc",
    "address_validation_util.h",
    "autocomplete_history_manager.cc",
    "autocomplete_history_manager.h",
    "autofill-inl.h",
    "autofill_address_util.cc",
    "autofill_address_util.h",
    "autofill_client.h",
    "autofill_country.cc",
    "autofill_country.h",
    "autofill_data_model.cc",
    "autofill_data_model.h",
    "autofill_data_util.cc",
    "autofill_data_util.h",
    "autofill_download_manager.cc",
    "autofill_download_manager.h",
    "autofill_driver.h",
    "autofill_driver_factory.cc",
    "autofill_driver_factory.h",
    "autofill_experiments.cc",
    "autofill_experiments.h",
    "autofill_external_delegate.cc",
    "autofill_external_delegate.h",
    "autofill_field.cc",
    "autofill_field.h",
    "autofill_handler.cc",
    "autofill_handler.h",
    "autofill_handler_proxy.cc",
    "autofill_handler_proxy.h",
    "autofill_ie_toolbar_import_win.cc",
    "autofill_ie_toolbar_import_win.h",
    "autofill_manager.cc",
    "autofill_manager.h",
    "autofill_manager_test_delegate.h",
    "autofill_metrics.cc",
    "autofill_metrics.h",
    "autofill_popup_delegate.h",
    "autofill_profile.cc",
    "autofill_profile.h",
    "autofill_profile_comparator.cc",
    "autofill_profile_comparator.h",
    "autofill_profile_validator.cc",
    "autofill_profile_validator.h",
    "autofill_provider.cc",
    "autofill_provider.h",
    "autofill_scanner.cc",
    "autofill_scanner.h",
    "autofill_type.cc",
    "autofill_type.h",
    "autofill_wallet_data_type_controller.cc",
    "autofill_wallet_data_type_controller.h",
    "card_unmask_delegate.cc",
    "card_unmask_delegate.h",
    "contact_info.cc",
    "contact_info.h",
    "country_combobox_model.cc",
    "country_combobox_model.h",
    "country_data.cc",
    "country_data.h",
    "country_names.cc",
    "country_names.h",
    "credit_card.cc",
    "credit_card.h",
    "credit_card_field.cc",
    "credit_card_field.h",
    "email_field.cc",
    "email_field.h",
    "field_candidates.cc",
    "field_candidates.h",
    "field_types.h",
    "form_field.cc",
    "form_field.h",
    "form_group.cc",
    "form_group.h",
    "form_structure.cc",
    "form_structure.h",
    "form_types.cc",
    "form_types.h",
    "legal_message_line.cc",
    "legal_message_line.h",
    "name_field.cc",
    "name_field.h",
    "password_generator.cc",
    "password_generator.h",
    "payments/full_card_request.cc",
    "payments/full_card_request.h",
    "payments/payments_client.cc",
    "payments/payments_client.h",
    "payments/payments_request.h",
    "payments/payments_service_url.cc",
    "payments/payments_service_url.h",
    "personal_data_manager.cc",
    "personal_data_manager.h",
    "personal_data_manager_observer.h",
    "phone_email_validation_util.cc",
    "phone_email_validation_util.h",
    "phone_field.cc",
    "phone_field.h",
    "phone_number.cc",
    "phone_number.h",
    "phone_number_i18n.cc",
    "phone_number_i18n.h",
    "popup_item_ids.h",
    "region_combobox_model.cc",
    "region_combobox_model.h",
    "region_data_loader.h",
    "region_data_loader_impl.cc",
    "region_data_loader_impl.h",
    "risk_data_loader.h",
    "state_names.cc",
    "state_names.h",
    "subkey_requester.cc",
    "subkey_requester.h",
    "suggestion.cc",
    "suggestion.h",
    "ui/card_unmask_prompt_controller.h",
    "ui/card_unmask_prompt_controller_impl.cc",
    "ui/card_unmask_prompt_controller_impl.h",
    "ui/card_unmask_prompt_view.h",
    "validation.cc",
    "validation.h",
    "webdata/autocomplete_sync_bridge.cc",
    "webdata/autocomplete_sync_bridge.h",
    "webdata/autocomplete_syncable_service.cc",
    "webdata/autocomplete_syncable_service.h",
    "webdata/autofill_change.cc",
    "webdata/autofill_change.h",
    "webdata/autofill_data_type_controller.cc",
    "webdata/autofill_data_type_controller.h",
    "webdata/autofill_entry.cc",
    "webdata/autofill_entry.h",
    "webdata/autofill_profile_data_type_controller.cc",
    "webdata/autofill_profile_data_type_controller.h",
    "webdata/autofill_profile_syncable_service.cc",
    "webdata/autofill_profile_syncable_service.h",
    "webdata/autofill_table.cc",
    "webdata/autofill_table.h",
    "webdata/autofill_table_encryptor.h",
    "webdata/autofill_table_encryptor_factory.cc",
    "webdata/autofill_table_encryptor_factory.h",
    "webdata/autofill_wallet_metadata_syncable_service.cc",
    "webdata/autofill_wallet_metadata_syncable_service.h",
    "webdata/autofill_wallet_syncable_service.cc",
    "webdata/autofill_wallet_syncable_service.h",
    "webdata/autofill_webdata.h",
    "webdata/autofill_webdata_backend.h",
    "webdata/autofill_webdata_backend_impl.cc",
    "webdata/autofill_webdata_backend_impl.h",
    "webdata/autofill_webdata_service.cc",
    "webdata/autofill_webdata_service.h",
    "webdata/autofill_webdata_service_observer.h",
    "webdata/system_encryptor.cc",
    "webdata/system_encryptor.h",
    "webdata/web_data_model_type_controller.cc",
    "webdata/web_data_model_type_controller.h",
  ]

  if (is_ios) {
    sources += [
      "keyboard_accessory_metrics_logger.h",
      "keyboard_accessory_metrics_logger.mm",
    ]
  }

  if (is_ios || is_android) {
    sources += [
      "autofill_assistant.cc",
      "autofill_assistant.h",
      "autofill_credit_card_filling_infobar_delegate_mobile.cc",
      "autofill_credit_card_filling_infobar_delegate_mobile.h",
      "autofill_save_card_infobar_delegate_mobile.cc",
      "autofill_save_card_infobar_delegate_mobile.h",
      "autofill_save_card_infobar_mobile.h",
    ]
  }

  if (!is_android) {
    sources += [ "ui/save_card_bubble_controller.h" ]
  }

  configs += [ "//build/config:precompiled_headers" ]

  public_deps = [
    "//components/autofill/core/browser/proto",
    "//components/autofill/core/common",
    "//components/resources",
    "//skia",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//components/data_use_measurement/core",
    "//components/infobars/core",
    "//components/keyed_service/core",
    "//components/os_crypt",
    "//components/pref_registry",
    "//components/prefs",
    "//components/security_state/core",
    "//components/signin/core/browser",
    "//components/signin/core/common",
    "//components/strings",
    "//components/sync",
    "//components/variations/net",
    "//components/version_info",
    "//components/webdata/common",
    "//google_apis",
    "//net",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//sql",
    "//third_party/fips181",
    "//third_party/icu",
    "//third_party/libaddressinput",
    "//third_party/libphonenumber",
    "//third_party/re2",
    "//ui/base",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/range",
    "//url",
  ]

  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  if (is_mac) {
    libs = [ "AddressBook.framework" ]
  }
}

static_library("test_support") {
  testonly = true
  sources = [
    "autofill_test_utils.cc",
    "autofill_test_utils.h",
    "data_driven_test.cc",
    "data_driven_test.h",
    "suggestion_test_helpers.h",
    "test_address_normalizer.cc",
    "test_address_normalizer.h",
    "test_autofill_client.cc",
    "test_autofill_client.h",
    "test_autofill_clock.cc",
    "test_autofill_clock.h",
    "test_autofill_driver.cc",
    "test_autofill_driver.h",
    "test_autofill_external_delegate.cc",
    "test_autofill_external_delegate.h",
    "test_personal_data_manager.cc",
    "test_personal_data_manager.h",
    "test_region_data_loader.cc",
    "test_region_data_loader.h",
  ]

  if (!is_android) {
    sources += [
      "ui/mock_save_card_bubble_controller.cc",
      "ui/mock_save_card_bubble_controller.h",
    ]
  }

  public_deps = [
    ":browser",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/os_crypt",
    "//components/os_crypt:test_support",
    "//components/pref_registry",
    "//components/prefs:test_support",
    "//components/signin/core/browser",
    "//components/signin/core/common",
    "//components/ukm",
    "//components/ukm:test_support",
    "//google_apis:test_support",
    "//skia",
    "//testing/gtest",
    "//third_party/libaddressinput:util",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
  ]
}

bundle_data("unit_tests_bundle_data") {
  sources = [
    "//components/test/data/autofill/merge/input/ambiguous.in",
    "//components/test/data/autofill/merge/input/case.in",
    "//components/test/data/autofill/merge/input/email.in",
    "//components/test/data/autofill/merge/input/identical.in",
    "//components/test/data/autofill/merge/input/primarycase.in",
    "//components/test/data/autofill/merge/input/singlemerge.in",
    "//components/test/data/autofill/merge/input/validation.in",
    "//components/test/data/autofill/merge/output/ambiguous.out",
    "//components/test/data/autofill/merge/output/case.out",
    "//components/test/data/autofill/merge/output/email.out",
    "//components/test/data/autofill/merge/output/identical.out",
    "//components/test/data/autofill/merge/output/primarycase.out",
    "//components/test/data/autofill/merge/output/singlemerge.out",
    "//components/test/data/autofill/merge/output/validation.out",
  ]
  outputs = [
    "{{bundle_resources_dir}}/" +
        "{{source_root_relative_dir}}/{{source_file_part}}",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "address_combobox_model_unittest.cc",
    "address_field_unittest.cc",
    "address_i18n_unittest.cc",
    "address_normalization_manager_unittest.cc",
    "address_normalizer_impl_unittest.cc",
    "address_rewriter_unittest.cc",
    "address_unittest.cc",
    "address_validation_util_unittest.cc",
    "autocomplete_history_manager_unittest.cc",
    "autofill_country_unittest.cc",
    "autofill_data_model_unittest.cc",
    "autofill_data_util_unittest.cc",
    "autofill_download_manager_unittest.cc",
    "autofill_driver_factory_unittest.cc",
    "autofill_experiments_unittest.cc",
    "autofill_external_delegate_unittest.cc",
    "autofill_field_unittest.cc",
    "autofill_ie_toolbar_import_win_unittest.cc",
    "autofill_manager_unittest.cc",
    "autofill_merge_unittest.cc",
    "autofill_metrics_unittest.cc",
    "autofill_profile_comparator_unittest.cc",
    "autofill_profile_unittest.cc",
    "autofill_profile_validator_unittest.cc",
    "autofill_type_unittest.cc",
    "contact_info_unittest.cc",
    "country_combobox_model_unittest.cc",
    "country_names_unittest.cc",
    "credit_card_field_unittest.cc",
    "credit_card_unittest.cc",
    "field_candidates_unittest.cc",
    "form_field_unittest.cc",
    "form_structure_unittest.cc",
    "legal_message_line_unittest.cc",
    "name_field_unittest.cc",
    "password_generator_unittest.cc",
    "payments/full_card_request_unittest.cc",
    "payments/payments_client_unittest.cc",
    "payments/payments_service_url_unittest.cc",
    "personal_data_manager_unittest.cc",
    "phone_email_validation_util_unittest.cc",
    "phone_field_unittest.cc",
    "phone_number_i18n_unittest.cc",
    "phone_number_unittest.cc",
    "region_combobox_model_unittest.cc",
    "subkey_requester_unittest.cc",
    "ui/card_unmask_prompt_controller_impl_unittest.cc",
    "validation_unittest.cc",
    "webdata/autocomplete_sync_bridge_unittest.cc",
    "webdata/autofill_data_type_controller_unittest.cc",
    "webdata/autofill_profile_syncable_service_unittest.cc",
    "webdata/autofill_table_unittest.cc",
    "webdata/autofill_wallet_metadata_syncable_service_unittest.cc",
    "webdata/autofill_wallet_syncable_service_unittest.cc",
    "webdata/web_data_service_unittest.cc",
  ]

  if (is_ios || is_android) {
    sources += [ "autofill_assistant_unittest.cc" ]
  }

  deps = [
    ":browser",
    ":test_support",
    ":unit_tests_bundle_data",
    "//base",
    "//base/test:test_support",
    "//components/autofill/core/common",
    "//components/metrics/proto",
    "//components/os_crypt",
    "//components/os_crypt:test_support",
    "//components/prefs:test_support",
    "//components/resources",
    "//components/security_state/core",
    "//components/signin/core/browser",
    "//components/signin/core/browser:test_support",
    "//components/signin/core/common",
    "//components/strings",
    "//components/sync",
    "//components/sync:test_support_driver",
    "//components/sync:test_support_model",
    "//components/ukm",
    "//components/ukm:test_support",
    "//components/variations",
    "//components/variations:test_support",
    "//components/webdata/common",
    "//components/webdata_services:test_support",
    "//google_apis",
    "//google_apis:test_support",
    "//net:test_support",
    "//services/metrics/public/cpp:ukm_builders",
    "//sql",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/libaddressinput:util",
    "//third_party/libphonenumber",
    "//ui/base",
    "//url",
  ]
}

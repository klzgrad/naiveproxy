# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

action("field_trial_testing_config_action") {
  visibility = [ ":field_trial_config" ]
  script = "//tools/variations/fieldtrial_to_struct.py"

  source = "//testing/variations/fieldtrial_testing_config.json"
  inputs = [
    "//tools/json_to_struct/element_generator.py",
    "//tools/json_to_struct/json_to_struct.py",
    "//tools/json_to_struct/struct_generator.py",
    source,
  ]
  out_name = "fieldtrial_testing_config"
  outputs = [
    "$target_gen_dir/$out_name.cc",
    "$target_gen_dir/$out_name.h",
  ]

  args = [
    rebase_path(source, root_build_dir),
    "--destbase=" + rebase_path(target_gen_dir, root_build_dir),
    "--namespace=variations",
    "--schema=" +
        rebase_path("field_trial_testing_config_schema.json", root_build_dir),
    "--platform=" + current_os,
    "--output=$out_name",
  ]
}

static_library("field_trial_config") {
  sources = [
    "field_trial_util.cc",
    "field_trial_util.h",
  ]

  sources += get_target_outputs(":field_trial_testing_config_action")

  deps = [
    ":field_trial_testing_config_action",
    "//base",
    "//components/variations",
    "//net",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "field_trial_util_unittest.cc",
  ]
  deps = [
    ":field_trial_config",
    "//base",
    "//base/test:test_support",
    "//components/variations",
    "//testing/gtest",
  ]
}

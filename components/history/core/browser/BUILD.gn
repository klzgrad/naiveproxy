# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("browser") {
  sources = [
    "browsing_history_driver.h",
    "browsing_history_service.cc",
    "browsing_history_service.h",
    "delete_directive_handler.cc",
    "delete_directive_handler.h",
    "download_constants.h",
    "download_database.cc",
    "download_database.h",
    "download_row.cc",
    "download_row.h",
    "download_slice_info.cc",
    "download_slice_info.h",
    "download_types.cc",
    "download_types.h",
    "expire_history_backend.cc",
    "expire_history_backend.h",
    "history_backend.cc",
    "history_backend.h",
    "history_backend_client.h",
    "history_backend_notifier.h",
    "history_backend_observer.h",
    "history_client.h",
    "history_constants.cc",
    "history_constants.h",
    "history_context.h",
    "history_database.cc",
    "history_database.h",
    "history_database_params.cc",
    "history_database_params.h",
    "history_db_task.h",
    "history_delete_directives_data_type_controller.cc",
    "history_delete_directives_data_type_controller.h",
    "history_model_worker.cc",
    "history_model_worker.h",
    "history_service.cc",
    "history_service.h",
    "history_service_observer.h",
    "history_types.cc",
    "history_types.h",
    "in_memory_database.cc",
    "in_memory_database.h",
    "in_memory_history_backend.cc",
    "in_memory_history_backend.h",
    "keyword_id.h",
    "keyword_search_term.cc",
    "keyword_search_term.h",
    "page_usage_data.cc",
    "page_usage_data.h",
    "thumbnail_database.cc",
    "thumbnail_database.h",
    "top_sites.cc",
    "top_sites.h",
    "top_sites_backend.cc",
    "top_sites_backend.h",
    "top_sites_cache.cc",
    "top_sites_cache.h",
    "top_sites_database.cc",
    "top_sites_database.h",
    "top_sites_impl.cc",
    "top_sites_impl.h",
    "top_sites_observer.h",
    "typed_url_data_type_controller.cc",
    "typed_url_data_type_controller.h",
    "typed_url_model_type_controller.cc",
    "typed_url_model_type_controller.h",
    "typed_url_sync_bridge.cc",
    "typed_url_sync_bridge.h",
    "typed_url_sync_metadata_database.cc",
    "typed_url_sync_metadata_database.h",
    "typed_url_syncable_service.cc",
    "typed_url_syncable_service.h",
    "url_database.cc",
    "url_database.h",
    "url_row.cc",
    "url_row.h",
    "url_utils.cc",
    "url_utils.h",
    "visit_database.cc",
    "visit_database.h",
    "visit_delegate.cc",
    "visit_delegate.h",
    "visit_tracker.cc",
    "visit_tracker.h",
    "visitsegment_database.cc",
    "visitsegment_database.h",
    "web_history_service.cc",
    "web_history_service.h",
    "web_history_service_observer.h",
  ]

  public_deps = [
    "//components/sync/protocol",
    "//skia",
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//components/data_use_measurement/core",
    "//components/favicon_base",
    "//components/history/core/common",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/query_parser",
    "//components/signin/core/browser",
    "//components/sync",
    "//components/url_formatter",
    "//components/variations",
    "//components/version_info",
    "//google_apis",
    "//net",
    "//sql",
    "//third_party/sqlite",
    "//ui/base",
    "//ui/gfx",
    "//url",
  ]

  if (is_android) {
    sources += [
      "android/android_cache_database.cc",
      "android/android_cache_database.h",
      "android/android_history_types.cc",
      "android/android_history_types.h",
      "android/android_time.h",
      "android/android_urls_database.cc",
      "android/android_urls_database.h",
      "android/android_urls_sql_handler.cc",
      "android/android_urls_sql_handler.h",
      "android/favicon_sql_handler.cc",
      "android/favicon_sql_handler.h",
      "android/sql_handler.cc",
      "android/sql_handler.h",
      "android/urls_sql_handler.cc",
      "android/urls_sql_handler.h",
      "android/visit_sql_handler.cc",
      "android/visit_sql_handler.h",
    ]
  }

  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]
}

bundle_data("unit_tests_bundle_data") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [
    "//components/test/data/history/Favicons.v3.sql",
    "//components/test/data/history/Favicons.v4.sql",
    "//components/test/data/history/Favicons.v5.sql",
    "//components/test/data/history/Favicons.v6.sql",
    "//components/test/data/history/Favicons.v7.sql",
    "//components/test/data/history/Favicons.v8.sql",
    "//components/test/data/history/HistoryNoDuration",
    "//components/test/data/history/HistoryNoSource",
    "//components/test/data/history/History_with_starred",
    "//components/test/data/history/TopSites.v1.sql",
    "//components/test/data/history/TopSites.v2.sql",
    "//components/test/data/history/TopSites.v3.sql",
    "//components/test/data/history/history.22.sql",
    "//components/test/data/history/history.26.sql",
    "//components/test/data/history/history.27.sql",
    "//components/test/data/history/history.28.sql",
    "//components/test/data/history/history.29.sql",
    "//components/test/data/history/history.30.sql",
    "//components/test/data/history/history.31.sql",
    "//components/test/data/history/history.32.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.corrupt_meta.disable",
    "//components/test/data/history/thumbnail_wild/Favicons.v2.init.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v3.init.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v4.init.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v5.icon_type.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v5.icon_type2.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v5.init.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v5.sizes.sql",
    "//components/test/data/history/thumbnail_wild/Favicons.v6.init.sql",
  ]
  outputs = [
    "{{bundle_resources_dir}}/" +
        "{{source_root_relative_dir}}/{{source_file_part}}",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "browsing_history_service_unittest.cc",
    "download_slice_info_unittest.cc",
    "expire_history_backend_unittest.cc",
    "history_backend_db_unittest.cc",
    "history_backend_unittest.cc",
    "history_database_unittest.cc",
    "history_model_worker_unittest.cc",
    "history_querying_unittest.cc",
    "history_service_unittest.cc",
    "history_types_unittest.cc",
    "thumbnail_database_unittest.cc",
    "top_sites_cache_unittest.cc",
    "top_sites_database_unittest.cc",
    "top_sites_impl_unittest.cc",
    "typed_url_sync_bridge_unittest.cc",
    "typed_url_sync_metadata_database_unittest.cc",
    "typed_url_syncable_service_unittest.cc",
    "url_database_unittest.cc",
    "url_utils_unittest.cc",
    "visit_database_unittest.cc",
    "visit_tracker_unittest.cc",
    "web_history_service_unittest.cc",
  ]
  deps = [
    ":browser",
    ":unit_tests_bundle_data",
    "//base",
    "//base/test:test_support",
    "//components/favicon_base",
    "//components/history/core/common",
    "//components/history/core/test",
    "//components/prefs:test_support",
    "//components/signin/core/browser",
    "//components/signin/core/browser:test_support",
    "//components/sync",
    "//components/sync:test_support_driver",
    "//components/sync:test_support_model",
    "//sql",
    "//sql:test_support",
    "//testing/gtest",
    "//ui/gfx",
    "//ui/gfx:test_support",
    "//url",
  ]
  if (is_android) {
    sources += [ "android/android_history_types_unittest.cc" ]
  }
}

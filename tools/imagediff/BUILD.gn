# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/symlink.gni")

if (current_toolchain == host_toolchain) {
  executable("imagediff") {
    output_name = "image_diff"  # Different than dir nam for historical reasons.
    sources = [
      "image_diff.cc",
      "image_diff_png.cc",
      "image_diff_png.h",
    ]

    configs += [ "//build/config/compiler:wexit_time_destructors" ]

    deps = [
      "//base",
      "//build/config:exe_and_shlib_deps",
      "//build/win:default_exe_manifest",
      "//third_party/libpng",
      "//third_party/zlib",
    ]
  }
} else {
  # Aliases for compatibility with GYP.
  binary_symlink("imagediff") {
    binary_label = ":$target_name($host_toolchain)"
    binary_output_name = "image_diff"
  }
}

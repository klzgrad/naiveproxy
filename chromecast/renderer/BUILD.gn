# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")

source_set("renderer") {
  sources = [
    "cast_content_renderer_client.cc",
    "cast_content_renderer_client.h",
    "cast_render_frame_action_deferrer.cc",
    "cast_render_frame_action_deferrer.h",
  ]

  public_deps = [
    "//chromecast/renderer/media",
  ]

  if (chromecast_branding == "public") {
    sources += [ "cast_content_renderer_client_simple.cc" ]
  }

  if (use_playready) {
    configs += [ "//chromecast:playready_config" ]
  }

  deps = [
    "//base",
    "//chromecast/base",
    "//chromecast/common/media",
    "//chromecast/media",
    "//components/network_hints/renderer",
    "//content/public/common",
    "//content/public/renderer",
    "//crypto",
    "//ipc",
    "//media",
    "//services/service_manager/public/cpp",
  ]

  if (!is_fuchsia) {
    # TODO(crbug.com/753619): Enable crash reporting on Fuchsia.
    deps += [ "//chromecast/crash" ]
  }

  if (!is_android) {
    sources += [
      "memory_pressure_observer_impl.cc",
      "memory_pressure_observer_impl.h",
    ]

    deps += [
      "//chromecast/common:interfaces",
      "//mojo/public/cpp/bindings",
    ]
  }
}

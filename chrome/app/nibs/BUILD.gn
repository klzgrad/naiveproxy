# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/mac/rules.gni")

assert(is_mac)

translated_xibs = [
  "AppMenu.xib",
  "BookmarkAllTabs.xib",
  "BookmarkBubble.xib",
  "BookmarkEditor.xib",
  "BookmarkNameFolder.xib",
  "CollectedCookies.xib",
  "ContentBlockedCookies.xib",
  "ContentBlockedDownloads.xib",
  "ContentBlockedGeolocation.xib",
  "ContentBlockedMIDISysEx.xib",
  "ContentBlockedMedia.xib",
  "ContentBlockedMixedScript.xib",
  "ContentBlockedPopups.xib",
  "ContentBlockedSimple.xib",
  "ContentProtocolHandlers.xib",
  "CookieDetailsView.xib",
  "DownloadItem.xib",
  "DownloadShelf.xib",
  "ExtensionInstallPrompt.xib",
  "ExtensionInstallPromptNoWarnings.xib",
  "ExtensionInstallPromptWebstoreData.xib",
  "ExtensionInstalledBubble.xib",
  "FirstRunBubble.xib",
  "HttpAuthLoginSheet.xib",
  "HungRendererDialog.xib",
  "MainMenu.xib",
  "OneClickSigninBubble.xib",
  "OneClickSigninDialog.xib",
  "SaveAccessoryView.xib",
  "TaskManager.xib",
  "Toolbar.xib",
]

untranslated_xibs = [
  "BookmarkBarFolderWindow.xib",
  "FindBar.xib",
  "GlobalErrorBubble.xib",
  "InfoBar.xib",
]

mac_xib_bundle_data("chrome_xibs") {
  sources = translated_xibs + untranslated_xibs
}

action("localizer_table") {
  script = "generate_localizer.py"

  sources = translated_xibs
  table_path = "$target_gen_dir/localizer_table.h"
  outputs = [
    table_path,
  ]
  args = [
    "--output_path",
    rebase_path(table_path, root_build_dir),
  ]
  if (!use_system_xcode) {
    args += [
      "--developer_dir",
      hermetic_xcode_path,
    ]
  }
  args += rebase_path(translated_xibs, root_build_dir)
}

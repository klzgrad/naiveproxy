# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generate the binary proto form of "ssl_error_assistant" from the ascii proto.
action("make_ssl_error_assistant_protobuf") {
  script = "gen_ssl_error_assistant_proto.py"

  # The output goes in $target_gen_dir since that's where
  # chrome/browser/browser_resources.grd will look for it.

  input_filename = "ssl_error_assistant.asciipb"
  output_dir = target_gen_dir
  output_basename = "ssl_error_assistant.pb"
  python_path_root = "$root_out_dir/pyproto"
  python_path_ssl = "$python_path_root/chrome/browser/ssl/"

  inputs = [
    input_filename,
  ]

  deps = [
    "//chrome/browser/ssl:proto",
    "//third_party/protobuf:py_proto",
  ]

  outputs = [
    "$output_dir/$output_basename",
  ]

  args = [
    "-w",
    "-i",
    rebase_path(input_filename, root_build_dir),
    "-d",
    rebase_path(output_dir, root_build_dir),
    "-o",
    output_basename,
    "-p",
    rebase_path(python_path_root, root_build_dir),
    "-p",
    rebase_path(python_path_ssl, root_build_dir),
  ]
}

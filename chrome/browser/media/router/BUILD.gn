# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("//extensions/features/features.gni")
import("//testing/test.gni")

static_library("router") {
  deps = [
    "//base",
    "//chrome/common:constants",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//content/public/common",
    "//net",
    "//third_party/icu",
    "//url",
  ]
  public_deps = [
    "//chrome/common/media_router:router",
  ]
  sources = [
    "browser_presentation_connection_proxy.cc",
    "browser_presentation_connection_proxy.h",
    "issue_manager.cc",
    "issue_manager.h",
    "issues_observer.cc",
    "issues_observer.h",
    "media_router.h",
    "media_router_base.cc",
    "media_router_base.h",
    "media_router_dialog_controller.cc",
    "media_router_dialog_controller.h",
    "media_router_factory.cc",
    "media_router_factory.h",
    "media_router_metrics.cc",
    "media_router_metrics.h",
    "media_routes_observer.cc",
    "media_routes_observer.h",
    "media_sinks_observer.cc",
    "media_sinks_observer.h",
    "offscreen_presentation_manager.cc",
    "offscreen_presentation_manager.h",
    "offscreen_presentation_manager_factory.cc",
    "offscreen_presentation_manager_factory.h",
    "presentation_media_sinks_observer.cc",
    "presentation_media_sinks_observer.h",
    "presentation_service_delegate_impl.cc",
    "presentation_service_delegate_impl.h",
    "presentation_service_delegate_observers.cc",
    "presentation_service_delegate_observers.h",
    "receiver_presentation_service_delegate_impl.cc",
    "receiver_presentation_service_delegate_impl.h",
    "render_frame_host_id.h",
    "route_message_observer.cc",
    "route_message_observer.h",
  ]

  if (enable_extensions) {
    deps += [
      "discovery",
      "//extensions/browser",
      "//mojo/public/cpp/bindings",
    ]
    sources += [
      "event_page_request_manager.cc",
      "event_page_request_manager.h",
      "event_page_request_manager_factory.cc",
      "event_page_request_manager_factory.h",

      # TODO(crbug.com/646627): Move media_router_ui_service* to
      # chrome/browser/ui/.
      "media_router_ui_service.cc",
      "media_router_ui_service.h",
      "media_router_ui_service_factory.cc",
      "media_router_ui_service_factory.h",
      "mojo/extension_media_route_provider_proxy.cc",
      "mojo/extension_media_route_provider_proxy.h",
      "mojo/media_route_controller.cc",
      "mojo/media_route_controller.h",
      "mojo/media_route_provider_util_win.cc",
      "mojo/media_route_provider_util_win.h",
      "mojo/media_router_desktop.cc",
      "mojo/media_router_desktop.h",
      "mojo/media_router_mojo_impl.cc",
      "mojo/media_router_mojo_impl.h",
      "mojo/media_router_mojo_metrics.cc",
      "mojo/media_router_mojo_metrics.h",
    ]
  }
}

static_library("test_support") {
  testonly = true
  deps = [
    "//chrome/test:test_support",
    "//testing/gmock",
  ]
  public_deps = [
    ":router",
  ]
  sources = [
    "mock_media_router.cc",
    "mock_media_router.h",
    "mock_screen_availability_listener.cc",
    "mock_screen_availability_listener.h",
    "test_helper.cc",
    "test_helper.h",
  ]

  if (enable_extensions) {
    deps += [
      "discovery",
      "//chrome/common/media_router/mojo:media_router",
      "//chrome/common/media_router/mojo:media_router_test_interfaces",
      "//extensions/browser",
      "//extensions/common",
    ]
    sources += [
      "discovery/mdns/mock_dns_sd_registry.cc",
      "discovery/mdns/mock_dns_sd_registry.h",
      "mojo/media_router_mojo_test.cc",
      "mojo/media_router_mojo_test.h",
    ]
  }
}

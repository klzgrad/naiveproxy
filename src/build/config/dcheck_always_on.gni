# Copyright (c) 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# TODO(crbug.com/1233050): Until the bug is resolved we need to include
# gclient_args for the definition of build_with_chromium and build_overrides
# for client overrides of that flag. The latter should go away.
import("//build/config/gclient_args.gni")
import("//build_overrides/build.gni")
declare_args() {
  # Enables DCHECKs to be built-in, but to default to being non-fatal/log-only.
  # DCHECKS can then be set as fatal/non-fatal via the DCheckIsFatal feature.
  # See https://bit.ly/dcheck-albatross for details on how this is used.
  dcheck_is_configurable = false
}

declare_args() {
  # Set to false to disable DCHECK in Release builds. This is enabled by default
  # for non-official builds on the below platforms.
  # This default only affects Chromium as indicated by build_with_chromium.
  # Other clients typically set this to false. If another client wants to use
  # the same default value as Chromium, we'd need to add a separate gclient
  # variable to replace build_with_chromium here.
  # Note: If you are here to revert because DCHECKs are failing on a specific OS
  # please prefer excluding OSes rather than reverting . I.e. if Mac builds
  # break badly but other platforms are reasonably stable, add "&& !is_mac"
  # instead of reverting.
  dcheck_always_on =
      (build_with_chromium && !is_official_build) || dcheck_is_configurable
}

declare_args() {
  # Set to false to disable EXPENSIVE_DCHECK()s.
  # TODO(crbug.com/1225701): Hash out whether expensive DCHECKs need to be
  # disabled for developers by default. There's concern that disabling these
  # globally by default effectively reduces them to zero coverage. This is
  # in place so that you can disable expensive DCHECKs while retaining some
  # DCHECK coverage, which is especially important in user-facing builds.
  enable_expensive_dchecks = is_debug || dcheck_always_on
}

# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/c++/c++.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/sanitizers/sanitizers.gni")

if (current_toolchain == default_toolchain) {
  import("//build/toolchain/concurrent_links.gni")
}

assert(current_os == "openwrt")

declare_args() {
  build_static = false

  no_madvise_syscall = false
}

# This is included by reference in the //build/config/compiler config that
# is applied to all targets. It is here to separate out the logic that is
# Android-only.
config("compiler") {
  cflags = []
  ldflags = []

  defines = [
    "_LIBCPP_HAS_MUSL_LIBC=1",
    #"__UCLIBC__",
    "__MUSL__",
  ]

  if (build_static) {
    # Musl static objects are not built with -fPIC, cannot use -static-pie.
    ldflags += [ "-static" ]
    defines += [ "OPENWRT_BUILD_STATIC" ]
  } else if (current_cpu == "x86") {
    ldflags += [ "-Wl,--dynamic-linker=/lib/ld-musl-i386.so.1" ]
  } else if (current_cpu == "mipsel") {
    ldflags += [ "-Wl,--dynamic-linker=/lib/ld-musl-mipsel-sf.so.1" ]
  }

  if (no_madvise_syscall) {
    defines += [ "NO_MADVISE_SYSCALL" ]
  }

  abi = "musl"
  if (current_cpu == "arm") {
    abi = "musleabi"
    arch = "arm"
  } else if (current_cpu == "arm64") {
    arch = "aarch64"
  } else if (current_cpu == "x86") {
    arch = "i486"
  } else if (current_cpu == "x64") {
    arch = "x86_64"
  } else if (current_cpu == "mipsel") {
    arch = "mipsel"
  } else if (current_cpu == "mips64el") {
    arch = "mips64el"
  } else if (current_cpu == "riscv64") {
    arch = "riscv64"
  } else {
    assert(false, "Architecture not supported")
  }
  cflags += [
    "--target=$arch-openwrt-linux-$abi",

    # musl malloc() alignment is 4*sizeof(size_t), i.e.
    # 16 bytes on 32-bit CPUs, and 32 bytes on 64-bit CPUs. See
    # https://git.musl-libc.org/cgit/musl/tree/src/malloc/oldmalloc/malloc_impl.h
    # And https://www.openwall.com/lists/musl/2019/07/07/2 for its rationale.
    # But Clang is unaware of this information and produces
    #   #define __STDCPP_DEFAULT_NEW_ALIGNMENT__ 4U
    # for the i486-openwrt-linux-musl target.
    # This further causes static_asserts in
    # src/third_party/protobuf/src/google/protobuf/arenastring.cc to fail.
    # However, PartitionAlloc does not support larger than 16 alignment.
    "-fnew-alignment=16",
  ]
  ldflags += [ "--target=$arch-openwrt-linux-$abi" ]

  # Assign any flags set for the C compiler to asmflags so that they are sent
  # to the assembler.
  asmflags = cflags
}

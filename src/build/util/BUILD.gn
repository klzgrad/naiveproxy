# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/util/lastchange.gni")

action("chromium_git_revision") {
  script = "version.py"

  template_file = "chromium_git_revision.h.in"
  inputs = [
    lastchange_file,
    template_file,
  ]

  output_file = "$target_gen_dir/chromium_git_revision.h"
  outputs = [ output_file ]

  args = [
    # LASTCHANGE contains "<build hash>-<ref>".  The user agent only wants the
    # "<build hash>" bit, so chop off everything after it.
    "-e",
    "LASTCHANGE=LASTCHANGE[:LASTCHANGE.find('-')]",
    "-f",
    rebase_path(lastchange_file, root_build_dir),
    rebase_path(template_file, root_build_dir),
    rebase_path(output_file, root_build_dir),
  ]
}

group("test_results") {
  data = [
    "//build/util/lib/__init__.py",
    "//build/util/lib/results/",
  ]
}

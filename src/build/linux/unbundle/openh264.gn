# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/linux/pkg_config.gni")
import("//build/shim_headers.gni")

config("encoder_cfi_flags") {
  cflags = []
  if (current_toolchain == default_toolchain && is_clang) {
    openh264_cfi_ignorelist_path =
        rebase_path("//build/linux/unbundle/openh264_encoder_cfi_ignores.txt",
                    root_build_dir)
    cflags += [ "-fsanitize-ignorelist=$openh264_cfi_ignorelist_path" ]
  }
}

pkg_config("config") {
  packages = [ "openh264" ]
}

shim_headers("openh264_shim") {
  prefix = "wels/"
  root_path = "src/codec/api/wels"
  headers = [
    "codec_api.h",
    "codec_app_def.h",
    "codec_def.h",
    "codec_ver.h",
  ]
}

source_set("common") {
  deps = [ ":openh264_shim" ]
  public_configs = [ ":config" ]
}

source_set("processing") {
  deps = [ ":openh264_shim" ]
  public_configs = [ ":config" ]
}

source_set("encoder") {
  deps = [ ":openh264_shim" ]
  public_configs = [
    ":config",
    ":encoder_cfi_flags",
  ]
}

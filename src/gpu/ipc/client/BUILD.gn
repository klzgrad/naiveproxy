# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

group("client") {
  if (is_component_build) {
    public_deps = [
      "//gpu",
    ]
  } else {
    public_deps = [
      ":ipc_client_sources",
    ]
  }
}

source_set("ipc_client_sources") {
  sources = [
    "command_buffer_proxy_impl.cc",
    "command_buffer_proxy_impl.h",
    "gpu_channel_host.cc",
    "gpu_channel_host.h",
    "image_decode_accelerator_proxy.cc",
    "image_decode_accelerator_proxy.h",
    "shared_image_interface_proxy.cc",
    "shared_image_interface_proxy.h",
  ]
  configs += [
    "//build/config/compiler:no_size_t_to_int_warning",
    "//gpu:gpu_implementation",
  ]
  deps = [
    "//base",
    "//cc/paint",
    "//gpu/command_buffer/client:client_sources",
    "//gpu/command_buffer/common:common_sources",
    "//gpu/config:config_sources",
    "//gpu/ipc/common:ipc_common_sources",
    "//mojo/public/cpp/system",
    "//ui/gfx:color_space",
    "//ui/gfx/geometry",
    "//ui/gl",
  ]
  public_deps = [
    "//ipc",
  ]
}

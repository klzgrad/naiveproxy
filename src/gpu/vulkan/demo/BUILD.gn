# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//gpu/vulkan/features.gni")

assert(enable_vulkan)

# TODO(cblume): These tests should run on each platform -- crbug.com/858614

group("demo") {
  if (use_x11) {
    deps = [
      ":vulkan_demo",
    ]
  }
}

if (use_x11) {
  executable("vulkan_demo") {
    sources = [
      "main.cc",
      "vulkan_demo.cc",
      "vulkan_demo.h",
    ]

    deps = [
      "//base",
      "//components/tracing:startup_tracing",
      "//components/viz/common",
      "//gpu/vulkan/init",
      "//ui/display/types",
      "//ui/events",
      "//ui/events/platform",
      "//ui/events/platform/x11",
      "//ui/gfx:native_widget_types",
      "//ui/gfx/geometry",
      "//ui/platform_window:platform_window",
      "//ui/platform_window/x11",
    ]

    deps += [ "//ui/gfx/x" ]
    configs += [ "//build/config/linux:x11" ]
  }
}

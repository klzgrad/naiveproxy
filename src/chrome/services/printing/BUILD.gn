# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

source_set("lib") {
  sources = [
    "pdf_nup_converter.cc",
    "pdf_nup_converter.h",
    "pdf_to_pwg_raster_converter.cc",
    "pdf_to_pwg_raster_converter.h",
    "printing_service.cc",
    "printing_service.h",
  ]

  deps = [
    "//components/crash/core/common:crash_key",
    "//components/pwg_encoder",
    "//pdf",
    "//printing",
    "//printing/mojom",
    "//ui/gfx",
  ]

  public_deps = [
    "//base",
    "//chrome/services/printing/public/mojom",
    "//mojo/public/cpp/bindings",
  ]

  if (is_chromeos) {
    sources += [
      "pdf_flattener.cc",
      "pdf_flattener.h",
      "pdf_thumbnailer.cc",
      "pdf_thumbnailer.h",
    ]

    deps += [
      "//pdf",
      "//ui/gfx/codec",
    ]
  }

  if (is_win) {
    sources += [
      "pdf_to_emf_converter.cc",
      "pdf_to_emf_converter.h",
      "pdf_to_emf_converter_factory.cc",
      "pdf_to_emf_converter_factory.h",
    ]

    deps += [ "//skia" ]
  }
}

if (is_chromeos) {
  source_set("pdf_thumbnailer_test") {
    testonly = true
    sources = [ "pdf_thumbnailer_unittest.cc" ]

    deps = [
      ":lib",
      "//base/test:test_support",
      "//skia",
      "//testing/gtest",
    ]
  }
}

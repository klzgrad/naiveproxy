# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//chrome/services/speech/buildflags.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_SODA=$enable_soda" ]
}

source_set("lib") {
  sources = [
    "cloud_speech_recognition_client.cc",
    "cloud_speech_recognition_client.h",
    "speech_recognition_recognizer_impl.cc",
    "speech_recognition_recognizer_impl.h",
    "speech_recognition_service_impl.cc",
    "speech_recognition_service_impl.h",
  ]

  if (is_chromeos_ash) {
    sources += [
      "cros_speech_recognition_recognizer_impl.cc",
      "cros_speech_recognition_recognizer_impl.h",
    ]
  }

  public_deps = [
    "//media/mojo/mojom",
    "//mojo/public/cpp/bindings",
    "//mojo/public/mojom/base",
  ]

  deps = [
    ":buildflags",
    "//base",
    "//chrome/services/speech/soda",
    "//chrome/services/speech/soda:soda_api_proto",
    "//components/speech",
    "//content/public/browser:proto",
    "//google_apis",
    "//media/mojo/common",
    "//mojo/public/cpp/bindings",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "cloud_speech_recognition_client_unittest.cc" ]

  deps = [
    ":lib",
    "//base",
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//components/speech",
    "//content/public/browser:proto",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

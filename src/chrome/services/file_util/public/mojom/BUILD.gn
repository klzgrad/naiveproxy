# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")
import("//components/safe_browsing/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "file_util_service.mojom" ]

  if (safe_browsing_mode == 1) {
    sources += [ "safe_archive_analyzer.mojom" ]
    enabled_features = [ "full_safe_browsing" ]
  }

  public_deps = [ "//mojo/public/mojom/base" ]

  if (is_chromeos_ash) {
    sources += [ "zip_file_creator.mojom" ]
    public_deps += [ "//components/services/filesystem/public/mojom" ]
  }

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chrome.mojom.SafeArchiveAnalyzerResults"
          cpp = "::safe_browsing::ArchiveAnalyzerResults"
        },
      ]
      traits_headers = [ "//chrome/common/safe_browsing/zip_analyzer.h" ]
      traits_private_headers = [ "safe_archive_analyzer_param_traits.h" ]
      traits_public_deps = [
        "//chrome/common/safe_browsing:proto",
        "//components/safe_browsing:buildflags",
        "//components/safe_browsing/core:csd_proto",
      ]
    },
  ]
}

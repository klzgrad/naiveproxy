# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")

source_set("cpp") {
  sources = [
    "media_parser_provider.cc",
    "media_parser_provider.h",
    "safe_audio_video_checker.cc",
    "safe_audio_video_checker.h",
    "safe_media_metadata_parser.cc",
    "safe_media_metadata_parser.h",
  ]

  deps = [
    "//chrome/app:generated_resources",
    "//chrome/common",
    "//content/public/browser",
    "//media:media_buildflags",
    "//net:buildflags",
    "//third_party/boringssl",
    "//third_party/libyuv",
  ]

  public_deps = [ "//chrome/services/media_gallery_util/public/mojom" ]

  if (media_use_ffmpeg) {
    deps += [
      "//third_party/ffmpeg",
      "//third_party/ffmpeg:ffmpeg_features",
    ]
  }
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true

    sources = [ "media_gallery_util_browsertest.cc" ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    deps = [
      ":cpp",
      "//base",
      "//chrome/services/media_gallery_util/public/mojom",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//testing/gtest",
      "//third_party/libyuv",
    ]

    if (media_use_ffmpeg) {
      deps += [
        "//third_party/ffmpeg",
        "//third_party/ffmpeg:ffmpeg_features",
      ]
    }
  }
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//chrome/common/apps/platform_apps/api/api_sources.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")

assert(enable_extensions)

################################################################################
# Public Targets

group("api") {
  public_deps = [
    ":generated_api_json_strings",
    ":generated_api_types",
    "//extensions/buildflags",
  ]
}

group("apps_features") {
  public_deps = [
    ":api_features",
    ":permission_features",
  ]
}

################################################################################
# Private Targets

generated_json_strings("generated_api_json_strings") {
  sources = chrome_apps_api_schema_files + chrome_apps_uncompiled_schema_files
  bundle_name = "ChromeApps"
  root_namespace = apps_api_root_namespace
  deps = [
    "//base",
    "//extensions/buildflags",
  ]
  visibility = [ ":api" ]
}

generated_types("generated_api_types") {
  sources = chrome_apps_api_schema_files
  root_namespace = apps_api_root_namespace
  deps = [
    "//base",
    "//extensions/buildflags",
  ]
  visibility = [ ":api" ]
}

json_features("api_features") {
  feature_type = "APIFeature"
  method_name = "AddChromeAppsAPIFeatures"
  sources = [
    "_api_features.json",
  ]
  visibility = [ ":apps_features" ]
}

json_features("permission_features") {
  feature_type = "PermissionFeature"
  method_name = "AddChromeAppsPermissionFeatures"
  sources = [
    "_permission_features.json",
  ]
  visibility = [ ":apps_features" ]
}

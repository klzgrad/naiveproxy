# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//base/android/linker/config.gni")
import("//build/config/android/rules.gni")
import("//device/vr/buildflags/buildflags.gni")

assert(enable_vr)
assert(modularize_vr)

template("vr_module_tmpl") {
  assert(defined(invoker.manifest_package))
  assert(defined(invoker.module_name))
  assert(defined(invoker.base_module_target))

  _manifest = "$target_gen_dir/$target_name/AndroidManifest.xml"
  _manifest_target = "${target_name}__manifest"
  jinja_template(_manifest_target) {
    input = "//chrome/android/modules/vr/AndroidManifest.xml"
    output = _manifest
    variables = [
      "target_sdk_version=$android_sdk_version",
      "manifest_package=${invoker.manifest_package}",
    ]
  }

  android_app_bundle_module(target_name) {
    forward_variables_from(invoker,
                           [
                             "base_module_target",
                             "module_name",
                             "version_code",
                           ])
    android_manifest = _manifest
    android_manifest_dep = ":${_manifest_target}"
    deps = [
      "//chrome/browser/android/vr:java",
    ]
    proguard_enabled = !is_java_debug
    uncompress_shared_libraries = chromium_linker_supported
  }
}

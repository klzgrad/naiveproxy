# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/clang/clang.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/dcheck_always_on.gni")
import("//build/nocompile.gni")
import("//chrome/process_version_rc_template.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")
import("//tools/grit/grit_rule.gni")

process_version_rc_template("version") {
  template_file = "gcp_setup.rc.version"
  output = "$root_out_dir/gcp_setup_version.rc"
}

source_set("common") {
  sources = [
    "gcpw_files.cc",
    "gcpw_files.h",
    "setup_lib.cc",
    "setup_lib.h",
    "setup_utils.cc",
    "setup_utils.h",
  ]
  public_deps = [ "//base" ]
  deps = [
    "../extension:common",
    "../gaiacp:common",
    "../gaiacp:util",
    "//chrome/common:version_header",
    "//chrome/credential_provider/gaiacp:string_resources",
    "//chrome/installer/util:with_no_strings",
  ]
}

grit("setup_resources") {
  source = "gcp_setup_resources.grd"
  inputs = [ "gcp_icon.ico" ]
  outputs = [
    "grit/gcp_setup_resources.h",
    "grit/gcp_setup_resources.rc",
  ]
  resource_ids = ""
}

# Builds a self extracting 7-zip binary using the 7-zip SFX sources but with
# the credential provider's version resources.
executable("gcp_sfx") {
  deps = [
    ":setup_resources",
    ":version",
    "//third_party/lzma_sdk/Util/SfxSetup:7zS2_source",
  ]
  configs += [ "//build/config/win:windowed" ]
}

executable("gcp_setup") {
  sources = [
    "gcp_installer_crash_reporting.cc",
    "gcp_installer_crash_reporting.h",
    "setup.cc",
  ]
  deps = [
    ":common",
    ":setup_resources",
    ":version",
    "../eventlog:gcp_eventlog_messages",
    "../extension:common",
    "../gaiacp:common",
    "../gaiacp:util",
    "//build:branding_buildflags",
    "//chrome/common:version_header",
    "//components/crash/core/app:app",
    "//components/crash/core/app:crash_export_thunks",
    "//components/crash/core/app:run_as_crashpad_handler",
    "//components/crash/core/common",
    "//content/public/common:static_switches",
  ]
  configs += [ "//build/config/win:windowed" ]
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

# Nothing outside //chrome/chrome_cleaner can depend on these targets.
visibility = [ "//chrome/chrome_cleaner/*" ]

template("chrome_cleaner_mojom") {
  mojom(target_name) {
    # chrome_cleaner does not interface with blink or any language except C++,
    # so build only the basic (cpp) interface variant.
    disable_variants = true

    # Forward all other variables unchanged.
    forward_variables_from(invoker, "*", [ "disable_variants" ])
  }
}

chrome_cleaner_mojom("chrome_prompt_interface") {
  sources = [ "chrome_prompt.mojom" ]

  deps = [ ":footprints_interface" ]

  # NOTE: We avoid scrambling message IDs here because these messages cross an
  # IPC boundary to an external program built from a different source tree.
  scramble_message_ids = false
}

chrome_cleaner_mojom("footprints_interface") {
  sources = [ "footprints.mojom" ]
  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chrome_cleaner.mojom.FilePath"
          cpp = "::base::FilePath"
        },
        {
          mojom = "chrome_cleaner.mojom.RegistryKey"
          cpp = "::std::wstring"
        },
        {
          mojom = "chrome_cleaner.mojom.ExtensionId"
          cpp = "::std::wstring"
        },
      ]
      traits_headers = [ "typemaps/footprints_mojom_traits.h" ]
      traits_sources = [ "typemaps/footprints_mojom_traits.cc" ]
    },
  ]
}

chrome_cleaner_mojom("wstring") {
  sources = [ "wstring.mojom" ]
  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chrome_cleaner.mojom.WString"
          cpp = "::std::wstring"
        },
      ]
      traits_headers = [ "typemaps/wstring_mojom_traits.h" ]
      traits_sources = [ "typemaps/wstring_mojom_traits.cc" ]
    },
  ]
}

chrome_cleaner_mojom("engine_sandbox_interface") {
  sources = [
    "cleaner_engine_requests.mojom",
    "engine_file_requests.mojom",
    "engine_requests.mojom",
    "engine_sandbox.mojom",
    "pup.mojom",
    "windows_handle.mojom",
    "wstring_embedded_nulls.mojom",
  ]
  deps = [
    ":footprints_interface",
    ":wstring",
    "//mojo/public/mojom/base",
  ]
  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chrome_cleaner.mojom.TraceLocation"
          cpp = "::chrome_cleaner::UwS::TraceLocation"
        },
        {
          mojom = "chrome_cleaner.mojom.PUP"
          cpp = "::chrome_cleaner::PUPData::PUP"
        },
      ]
      traits_headers = [ "typemaps/pup_mojom_traits.h" ]
      traits_sources = [ "typemaps/pup_mojom_traits.cc" ]
      traits_public_deps = [
        "//chrome/chrome_cleaner/os:common_os",
        "//chrome/chrome_cleaner/proto:shared_pup_enums_proto",
        "//chrome/chrome_cleaner/pup_data:pup_data_base",
        "//chrome/chrome_cleaner/settings:settings",
      ]
    },
    {
      types = [
        {
          mojom = "chrome_cleaner.mojom.WStringEmbeddedNulls"
          cpp = "::chrome_cleaner::WStringEmbeddedNulls"
        },
      ]
      traits_headers = [ "typemaps/wstring_embedded_nulls_mojom_traits.h" ]
      traits_sources = [ "typemaps/wstring_embedded_nulls_mojom_traits.cc" ]
    },
    {
      types = [
        {
          mojom = "chrome_cleaner.mojom.PredefinedHandle"
          cpp = "::HANDLE"
          copyable_pass_by_value = true
        },
        {
          mojom = "chrome_cleaner.mojom.WindowsHandle"
          cpp = "::HANDLE"
          copyable_pass_by_value = true
        },
      ]
      traits_headers = [ "typemaps/windows_handle_mojom_traits.h" ]
      traits_sources = [ "typemaps/windows_handle_mojom_traits.cc" ]
    },
  ]
}

chrome_cleaner_mojom("parser_interface") {
  sources = [ "parser_interface.mojom" ]
  deps = [
    ":wstring",
    "//mojo/public/mojom/base",
  ]
}

chrome_cleaner_mojom("zip_archiver_interface") {
  sources = [ "zip_archiver.mojom" ]
}

chrome_cleaner_mojom("engine_sandbox_test_interface") {
  testonly = true

  sources = [
    "test_pup_typemap.mojom",
    "test_wstring_embedded_nulls.mojom",
  ]

  deps = [ ":engine_sandbox_interface" ]
}

chrome_cleaner_mojom("mojo_sandbox_hooks_test_interface") {
  testonly = true

  sources = [ "test_mojo_sandbox_hooks.mojom" ]
}

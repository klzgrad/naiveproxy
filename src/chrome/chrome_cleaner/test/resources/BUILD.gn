# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/win/manifest.gni")
import("//tools/grit/grit_rule.gni")

# Nothing outside //chrome/chrome_cleaner can depend on these targets.
visibility = [ "//chrome/chrome_cleaner/*" ]

sample_dll_file = "$root_out_dir/empty_dll.dll"

sample_dll_digest_pb_file = "$target_gen_dir/sample_dll_digest.pb"

action("compute_sample_dll_digest") {
  script = "//chrome/chrome_cleaner/tools/compute_digests.py"

  deps = [
    "//chrome/chrome_cleaner/proto:file_digest_proto",
    "//chrome/chrome_cleaner/test:empty_dll",
    "//third_party/protobuf:py_proto",
  ]

  inputs = [
    sample_dll_file,
  ]

  outputs = [
    sample_dll_digest_pb_file,
  ]

  args = [
    "--output",
    rebase_path(sample_dll_digest_pb_file, root_build_dir),
    rebase_path(sample_dll_file, root_build_dir),
  ]
}

grit("test_resources") {
  source = "test_resources.grd"
  resource_ids = "resource_ids"

  # The .grd contains references to generated files.
  source_is_generated = true

  outputs = [
    "grit/test_resources.h",
    "test_resources.rc",
  ]

  grit_flags = [
    "-E",
    "generated_resources_dir=" + rebase_path(target_gen_dir, root_build_dir),
  ]

  deps = [
    ":compute_sample_dll_digest",
  ]
}

copy("signed_empty_dll") {
  sources = [
    "signed_dll/signed_empty_dll.dll",
  ]

  outputs = [
    "$root_out_dir/signed_empty_dll.dll",
  ]
}

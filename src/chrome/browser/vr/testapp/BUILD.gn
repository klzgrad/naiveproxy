# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/util/process_version.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

executable("vr_testapp") {
  testonly = true

  sources = [
    "gl_renderer.cc",
    "gl_renderer.h",
    "test_keyboard_delegate.cc",
    "test_keyboard_delegate.h",
    "test_keyboard_renderer.cc",
    "test_keyboard_renderer.h",
    "vr_test_context.cc",
    "vr_test_context.h",
    "vr_testapp.cc",
  ]

  deps = [
    ":assets_component_version_header",
    ":vr_testapp_pak",
    ":vr_testapp_resources",
    "//chrome/browser/vr:vr_common",
    "//chrome/browser/vr:vr_test_support",
    "//chrome/browser/vr:vr_ui",
    "//components:components_tests_pak",
    "//components/omnibox/browser:vector_icons",
    "//components/security_state/core",
    "//components/tracing:startup_tracing",
    "//components/vector_icons:vector_icons",
    "//ui/display/types",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//ui/events/ozone/layout",
    "//ui/gl/init",
    "//ui/ozone",
    "//ui/platform_window",
  ]
}

process_version("assets_component_version_header") {
  sources = [
    "../../resources/vr/assets/VERSION",
  ]
  template_file = "assets_component_version.h.in"
  output = "$target_gen_dir/assets_component_version.h"
}

grit("vr_testapp_resources") {
  source = "vr_testapp_resources.grd"
  defines = [ "is_chrome_branded=$is_chrome_branded" ]
  outputs = [
    "grit/vr_testapp_resources.h",
    "vr_testapp_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

repack("vr_testapp_pak") {
  sources = [
    "$root_gen_dir/chrome/vr_testapp_resources.pak",
    "$root_out_dir/vr_test.pak",
  ]

  output = "$root_out_dir/vr_testapp.pak"

  deps = [
    ":vr_testapp_resources",
    "//chrome/browser/vr:vr_test_pak",
  ]
}

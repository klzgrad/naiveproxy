# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

group("scheduler") {
  deps = [
    ":factory",
  ]

  public_deps = [
    ":public",
  ]
}

source_set("public") {
  sources = [
    "notification_background_task_scheduler.h",
    "notification_data.cc",
    "notification_data.h",
    "notification_params.cc",
    "notification_params.h",
    "notification_schedule_service.h",
    "notification_scheduler_client.h",
    "notification_scheduler_client_registrar.cc",
    "notification_scheduler_client_registrar.h",
    "notification_scheduler_types.h",
    "schedule_params.cc",
    "schedule_params.h",
    "user_action_handler.h",
  ]

  deps = [
    "//base",
    "//components/keyed_service/core",
    "//skia",
  ]
}

# The embedder can depend on this target to get the internal library.
source_set("factory") {
  sources = [
    "schedule_service_factory_helper.cc",
    "schedule_service_factory_helper.h",
  ]

  # This target should not depend on anything in //chrome/* except the proto library.
  deps = [
    ":lib",
    ":public",
    "//base",
    "//chrome/browser/notifications/proto",
    "//components/keyed_service/core",
    "//components/leveldb_proto",
  ]
}

# Internal library that embedders should not directly depend on.
source_set("lib") {
  visibility = [
    ":factory",
    ":scheduler",
    ":unit_tests",
    "//chrome/browser/notifications/scheduler/test:test_lib",
  ]

  sources = [
    "background_task_coordinator.cc",
    "background_task_coordinator.h",
    "collection_store.h",
    "display_decider.cc",
    "display_decider.h",
    "distribution_policy.cc",
    "distribution_policy.h",
    "icon_entry.cc",
    "icon_entry.h",
    "icon_store.cc",
    "icon_store.h",
    "impression_history_tracker.cc",
    "impression_history_tracker.h",
    "impression_store.cc",
    "impression_store.h",
    "impression_types.cc",
    "impression_types.h",
    "init_aware_scheduler.cc",
    "init_aware_scheduler.h",
    "notification_entry.cc",
    "notification_entry.h",
    "notification_schedule_service_impl.cc",
    "notification_schedule_service_impl.h",
    "notification_scheduler.cc",
    "notification_scheduler.h",
    "notification_scheduler_context.cc",
    "notification_scheduler_context.h",
    "notification_store.cc",
    "notification_store.h",
    "proto_conversion.cc",
    "proto_conversion.h",
    "scheduled_notification_manager.cc",
    "scheduled_notification_manager.h",
    "scheduler_config.cc",
    "scheduler_config.h",
    "scheduler_utils.cc",
    "scheduler_utils.h",
  ]

  # This target should not depend on anything in //chrome/* except the proto library.
  deps = [
    ":public",
    "//base",
    "//chrome/browser/notifications/proto",
    "//components/keyed_service/core",
    "//components/leveldb_proto",
    "//skia",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "background_task_coordinator_unittest.cc",
    "display_decider_unittest.cc",
    "distribution_policy_unittest.cc",
    "icon_store_unittest.cc",
    "impression_history_tracker_unittest.cc",
    "impression_store_unittest.cc",
    "init_aware_scheduler_unittest.cc",
    "notification_store_unittests.cc",
    "proto_conversion_unittest.cc",
    "scheduled_notification_manager_unittest.cc",
    "scheduler_utils_unittest.cc",
  ]

  deps = [
    ":lib",
    ":public",
    "//chrome/browser/notifications/proto",
    "//chrome/browser/notifications/scheduler/test:test_lib",
    "//components/leveldb_proto:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

if (is_android) {
  java_cpp_enum("jni_enums") {
    sources = [
      "notification_scheduler_types.h",
    ]
  }
}

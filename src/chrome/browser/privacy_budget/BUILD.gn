# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("headers") {
  sources = [
    "identifiability_study_state.h",
    "privacy_budget_prefs.h",
    "privacy_budget_ukm_entry_filter.h",
    "sampled_surface_tracker.h",
  ]

  deps = [
    "//base",
    "//chrome/common/privacy_budget",
    "//components/prefs",
    "//components/ukm",
    "//third_party/blink/public/common/privacy_budget",
  ]
}

source_set("privacy_budget") {
  sources = [
    "identifiability_study_state.cc",
    "privacy_budget_prefs.cc",
    "privacy_budget_ukm_entry_filter.cc",
    "sampled_surface_tracker.cc",
  ]

  public_deps = [ ":headers" ]

  deps = [
    "//base",
    "//chrome/common/privacy_budget",
    "//components/prefs",
    "//components/ukm",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//services/metrics/public/mojom",
    "//third_party/blink/public/common/privacy_budget",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "identifiability_study_state_unittest.cc",
    "privacy_budget_ukm_entry_filter_unittest.cc",
    "sampled_surface_tracker_unittest.cc",
  ]

  deps = [
    ":headers",
    "//chrome/browser",
    "//chrome/common/privacy_budget:test_support",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]
}

source_set("browser_tests") {
  testonly = true

  sources = [ "privacy_budget_browsertest.cc" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "//chrome/browser",
    "//chrome/common/privacy_budget:test_support",
    "//chrome/test:test_support",
    "//components/ukm:test_support",
    "//components/ukm:ukm_test_helper",
    "//components/variations/service:buildflags",
    "//third_party/blink/public/common/privacy_budget",
  ]

  if (is_android) {
    deps += [ "//chrome/test:test_support_ui_android" ]
  } else {
    deps += [ "//chrome/test:test_support_ui" ]
  }

  data = [ "//chrome/test/data/privacy_budget/" ]
}

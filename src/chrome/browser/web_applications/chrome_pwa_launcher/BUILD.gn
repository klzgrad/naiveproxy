# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win)

import("//chrome/process_version_rc_template.gni")

process_version_rc_template("chrome_pwa_launcher_version") {
  sources = [ "chrome_pwa_launcher.ver" ]
  output = "$target_gen_dir/chrome_pwa_launcher_version.rc"
}

executable("chrome_pwa_launcher") {
  sources = [
    "chrome_pwa_launcher_exe.rc",
    "chrome_pwa_launcher_main.cc",
  ]

  deps = [
    ":chrome_pwa_launcher_version",
    ":util",
    "//base",
    "//chrome/common:non_code_constants",
    "//chrome/install_static:install_static_util",
    "//components/version_info:generate_version_info",
  ]

  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]
}

source_set("util") {
  friend = [
    ":chrome_pwa_launcher",
    ":unit_tests",
  ]

  public = [
    "chrome_pwa_launcher_util.h",
    "last_browser_file_util.h",
    "launcher_update.h",
  ]

  sources = [
    "chrome_pwa_launcher_util.cc",
    "last_browser_file_util.cc",
    "launcher_log.cc",
    "launcher_log.h",
    "launcher_update.cc",
  ]

  public_deps = [ "//base" ]

  deps = [
    "//chrome/common:non_code_constants",
    "//chrome/install_static:install_static_util",
    "//chrome/installer/util:with_no_strings",
    "//components/version_info:generate_version_info",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "last_browser_file_util_unittest.cc",
    "launcher_log_unittest.cc",
    "launcher_update_unittest.cc",
  ]

  deps = [
    ":util",
    "//base/test:test_support",
    "//chrome/browser/web_applications/components",
    "//chrome/test:test_support",
    "//components/version_info:generate_version_info",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}

# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/browser/buildflags.gni")

source_set("preinstalled_web_apps") {
  sources = [
    "preinstalled_web_app_utils.cc",
    "preinstalled_web_app_utils.h",
    "preinstalled_web_apps.cc",
    "preinstalled_web_apps.h",
  ]

  deps = [
    "//base",
    "//build:branding_buildflags",
    "//build:chromeos_buildflags",
    "//chrome/browser:browser_process",
    "//chrome/browser/web_applications/components",
    "//chrome/common:non_code_constants",
    "//skia",
    "//ui/base",
  ]

  if (is_chrome_branded) {
    sources += [
      "gmail.cc",
      "gmail.h",
      "google_docs.cc",
      "google_docs.h",
      "google_drive.cc",
      "google_drive.h",
      "google_sheets.cc",
      "google_sheets.h",
      "google_slides.cc",
      "google_slides.h",
      "youtube.cc",
      "youtube.h",
    ]

    if (is_chromeos_ash) {
      sources += [
        "google_calendar.cc",
        "google_calendar.h",
      ]
    }

    deps += [ "//chrome/browser/resources:preinstalled_web_apps_resources" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "preinstalled_web_app_utils_unittest.cc" ]

  deps = [
    "//chrome/browser:browser_process",
    "//chrome/browser/web_applications/preinstalled_web_apps",
    "//skia",
    "//testing/gtest",
  ]
}

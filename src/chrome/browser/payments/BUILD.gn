# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("browsertests") {
  testonly = true

  sources = [
    "empty_parameters_browsertest.cc",
    "has_enrolled_instrument_browsertest.cc",
    "has_enrolled_instrument_query_quota_browsertest.cc",
    "iframe_csp_browsertest.cc",
    "ignore_payment_method_browsertest.cc",
    "journey_logger_browsertest.cc",
    "payment_handler_change_shipping_address_option_browsertest.cc",
    "payment_handler_enable_delegations_browsertest.cc",
    "payment_handler_exploit_browsertest.cc",
    "payment_handler_just_in_time_installation_browsertest.cc",
    "payment_request_can_make_payment_browsertest.cc",
    "payment_request_can_make_payment_event_browsertest.cc",
    "payment_request_minimal_ui_browsertest.cc",
    "sec_fetch_site_browsertest.cc",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//chrome/test/payments:test_support",
    "//components/payments/content",
    "//components/payments/content:utils",
    "//components/payments/core:test_support",
    "//components/ukm:test_support",
  ]

  if (is_android) {
    sources += [
      "expandable_payment_handler_browsertest.cc",
      "hybrid_request_skip_ui_browsertest.cc",
    ]
  }

  if (!is_android) {
    sources += [
      "manifest_verifier_browsertest.cc",
      "payment_manifest_parser_browsertest.cc",
      "service_worker_payment_app_finder_browsertest.cc",
      "site_per_process_payments_browsertest.cc",
    ]

    deps += [
      "//chrome/test:test_support_ui",
      "//components/payments/content:utils",
      "//components/payments/content/utility",
      "//components/permissions:permissions",
    ]
  }
}

source_set("unittests") {
  testonly = true

  sources = [ "payment_handler_permission_context_unittest.cc" ]

  deps = [
    "//chrome/test:test_support",
    "//components/content_settings/core/test:test_support",
    "//components/permissions:permissions",
  ]

  if (is_android) {
    sources += [
      "android/android_payment_app_finder_unittest.cc",
      "android/android_payment_app_unittest.cc",
      "android/payment_manifest_verifier_unittest.cc",
    ]

    deps += [ "//chrome/android:native_j_unittests_jni_headers" ]
  }
}

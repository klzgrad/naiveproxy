# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")

js_type_check("closure_compile") {
  deps = [
    ":bluetooth_internals",
  ]
}

js_library("bluetooth_internals") {
  sources = [
    "adapter_broker.js",
    "adapter_page.js",
    "bluetooth_internals.js",
    "characteristic_list.js",
    "debug_log_page.js",
    "descriptor_list.js",
    "device_broker.js",
    "device_collection.js",
    "device_details_page.js",
    "device_table.js",
    "devices_page.js",
    "expandable_list.js",
    "object_fieldset.js",
    "service_list.js",
    "sidebar.js",
    "snackbar.js",
    "value_control.js",
  ]

  deps = [
    ":node_utils",
    ":page",
    ":page_manager",
    "//chrome/browser/ui/webui/bluetooth_internals:mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr/ui:array_data_model",
    "//ui/webui/resources/js/cr/ui:list",
    "//ui/webui/resources/js/cr/ui:list_item",
  ]
}

js_library("node_utils") {
  deps = [
    "//ui/webui/resources/js:cr",
  ]
}

js_library("page") {
  deps = [
    ":node_utils",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr:event_target",
    "//ui/webui/resources/js/cr/ui:bubble",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager",
    "//ui/webui/resources/js/cr/ui:overlay",
  ]
}

js_library("page_manager") {
  deps = [
    ":page",
    "//ui/webui/resources/js:cr",
  ]
}

grit("resources") {
  source = "resources.grd"

  # The .grd contains references to generated files.
  source_is_generated = true
  outputs = [
    "grit/bluetooth_internals_resources.h",
    "grit/bluetooth_internals_resources_map.h",
    "grit/bluetooth_internals_resources_map.cc",
    "bluetooth_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
  depfile_dir = target_gen_dir
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]
  deps = [
    "//chrome/browser/ui/webui/bluetooth_internals:mojo_bindings_js",
  ]
}

# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

js_library("module_descriptor") {
  deps = [ "..:browser_proxy" ]
}

js_library("modules") {
  deps = [
    ":module_registry",
    "cart:module",
    "drive:module",
    "task_module:module",
  ]
  if (!is_official_build) {
    deps += [ "dummy:module" ]
  }
}

js_library("module_wrapper") {
  deps = [
    ":module_descriptor",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  externs_list = [ "//third_party/closure_compiler/externs/metrics_private.js" ]
}

js_library("module_header") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("module_registry") {
  deps = [
    ":module_descriptor",
    "//ui/webui/resources/js:cr.m",
  ]
}

html_to_js("web_components_local") {
  js_files = [
    "module_header.js",
    "module_wrapper.js",
  ]
}

group("web_components") {
  public_deps = [
    ":web_components_local",
    "cart:web_components",
    "drive:web_components",
    "dummy:web_components",
    "task_module:web_components",
  ]
}

# Up one folder to get to the root NTP gen dir, where files need to live
# for bundling.
preprocess_folder = "../preprocessed/modules"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

generate_grd("build_grdp") {
  grd_prefix = "new_tab_page"
  out_grd = "$target_gen_dir/resources.grdp"
  deps = [
    ":preprocess",
    ":preprocess_gen",
  ]
  manifest_files = [
    "$target_gen_dir/preprocessed_manifest.json",
    "$target_gen_dir/preprocessed_gen_manifest.json",
  ]
  resource_path_prefix = "modules"
}

preprocess_if_expr("preprocess") {
  defines = [ "is_official_build=$is_official_build" ]
  in_folder = "./"

  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "module_descriptor.js",
    "modules.js",
    "module_registry.js",
    "task_module/task_module_handler_proxy.js",
    "cart/chrome_cart_proxy.js",
    "drive/drive_module_proxy.js",
  ]
  if (!is_official_build) {
    in_files += [ "dummy/foo_proxy.js" ]
  }
}

preprocess_if_expr("preprocess_gen") {
  deps = [ ":web_components" ]
  defines = [ "is_official_build=$is_official_build" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "module_header.js",
    "module_wrapper.js",
    "task_module/module.js",
    "cart/module.js",
    "drive/module.js",
  ]
  if (!is_official_build) {
    in_files += [ "dummy/module.js" ]
  }
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/polymer/polymer.gni")
import("../optimize_webui.gni")

if (optimize_webui) {
  print_preview_pak_file = "print_preview_resources.pak"
  unpak_folder = "print_preview_resources.unpak"

  optimize_webui("build") {
    host = "print"
    input = rebase_path("$target_gen_dir/$unpak_folder", root_build_dir)
    js_out_files = [ "print_preview.rollup.js" ]
    js_module_in_files = [ "print_preview.js" ]

    deps = [
      ":unpak",
      "../../../../ui/webui/resources:modulize",
    ]
    excludes = [
      "chrome://resources/js/cr.m.js",
      "pdf/pdf_scripting_api.js",
    ]
  }

  unpak("unpak") {
    pak_file = print_preview_pak_file
    out_folder = unpak_folder

    deps = [
      ":flattened_resources",
    ]
  }

  grit("flattened_resources") {
    source = "print_preview_resources.grd"

    # The .grd contains references to generated files.
    source_is_generated = true
    grit_flags = [
      "-E",
      "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
    ]

    deps = [
      "ui:polymer3_elements",
    ]
    defines = chrome_grit_defines
    outputs = [
      "grit/print_preview_resources.h",
      "grit/print_preview_resources_map.cc",
      "grit/print_preview_resources_map.h",
      print_preview_pak_file,
    ]
    output_dir = "$root_gen_dir/chrome/browser/resources/print_preview"
  }
}

group("closure_compile") {
  deps = [
    ":print_preview_module_resources",
    "data:closure_compile_module",
    "ui:closure_compile_module",
  ]
}

js_type_check("print_preview_module_resources") {
  is_polymer3 = true
  deps = [
    ":cloud_print_interface",
    ":cloud_print_interface_js",
    ":cloud_print_interface_manager",
    ":cloud_print_interface_native",
    ":dark_mode_behavior",
    ":metrics",
    ":native_layer",
    ":print_preview_utils",
  ]
}

js_library("print_preview_utils") {
  deps = [
    ":dark_mode_behavior",
    "//third_party/polymer/v3_0/components-chromium/iron-iconset-svg:iron-iconset-svg",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("metrics") {
  deps = [
    ":native_layer",
  ]
}

js_library("cloud_print_interface") {
  deps = [
    "data:destination",
    "data:invitation",
    "//ui/webui/resources/js/cr:event_target.m",
  ]
}

js_library("cloud_print_interface_manager") {
  deps = [
    ":cloud_print_interface",
    ":cloud_print_interface_js",
    ":cloud_print_interface_native",
    ":native_layer",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("cloud_print_interface_js") {
  deps = [
    ":cloud_print_interface",
    ":native_layer",
    "data:cloud_parsers",
    "data:destination",
    "data:invitation",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js/cr:event_target.m",
  ]
}

js_library("cloud_print_interface_native") {
  deps = [
    ":cloud_print_interface",
  ]
}

js_library("native_layer") {
  deps = [
    "data:destination",
    "data:destination_match",
    "data:destination_policies",
    "data:measurement_system",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("dark_mode_behavior") {
  deps = [
    "//ui/webui/resources/js:assert.m",
  ]
}

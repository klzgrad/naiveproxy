# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":main_page_behavior",
    ":settings_animated_pages",
    ":settings_section",
    ":settings_subpage",
  ]
}

js_library("main_page_behavior") {
  deps = [
    ":settings_section",
    "..:router",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:util",
  ]
  externs_list = [ "$externs_path/pending.js" ]
}

js_library("settings_animated_pages") {
  deps = [
    ":settings_subpage",
    "..:router",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js/cr/ui:focus_without_ink",
  ]
  externs_list = [ "$externs_path/pending_polymer.js" ]
}

js_library("settings_section") {
}

js_library("settings_subpage") {
  deps = [
    "..:router",
    "//third_party/polymer/v1_0/components-chromium/iron-resizable-behavior:iron-resizable-behavior-extracted",
    "//ui/webui/resources/cr_elements/cr_search_field:cr_search_field",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:find_shortcut_behavior",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js/cr/ui:focus_without_ink",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":main_page_behavior.m",
    ":settings_animated_pages.m",
    ":settings_section.m",
    ":settings_subpage.m",
  ]
}

js_library("main_page_behavior.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/settings_page/main_page_behavior.m.js" ]
  deps = [
    ":settings_section.m",
    "..:router.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("settings_animated_pages.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/settings_page/settings_animated_pages.m.js" ]
  deps = [
    ":settings_subpage.m",
    "..:router.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js/cr/ui:focus_without_ink.m",
  ]
  extra_deps = [ ":settings_animated_pages_module" ]
}

js_library("settings_section.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/settings_page/settings_section.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":settings_section_module" ]
}

js_library("settings_subpage.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/settings_page/settings_subpage.m.js" ]
  deps = [
    "..:router.m",
    "//third_party/polymer/v3_0/components-chromium/iron-resizable-behavior:iron-resizable-behavior",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_search_field:cr_search_field.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:find_shortcut_behavior.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_without_ink.m",
  ]
  extra_deps = [ ":settings_subpage_module" ]
}

import("//tools/polymer/polymer.gni")

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":settings_animated_pages_module",
    ":settings_section_module",
    ":settings_subpage_module",
  ]
}

polymer_modulizer("settings_animated_pages") {
  js_file = "settings_animated_pages.js"
  html_file = "settings_animated_pages.html"
  html_type = "dom-module"
  auto_imports = [
    "chrome/browser/resources/settings/router.html|Route, Router, RouteObserverBehavior",
    "ui/webui/resources/html/assert.html|assert",
    "ui/webui/resources/html/polymer.html|afterNextRender, dom, DomIf, html, Polymer",
  ]
  namespace_rewrites = settings_namespace_rewrites + [
                         "Polymer.dom|dom",
                         "Polymer.DomIf|DomIf",
                       ]
}

polymer_modulizer("settings_section") {
  js_file = "settings_section.js"
  html_file = "settings_section.html"
  html_type = "dom-module"
}

polymer_modulizer("settings_subpage") {
  js_file = "settings_subpage.js"
  html_file = "settings_subpage.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/router.html|RouteObserverBehavior,Router",
                   "third_party/polymer/v1_0/components-chromium/iron-resizable-behavior/iron-resizable-behavior.html|IronResizableBehavior",
                   "ui/webui/resources/html/assert.html|assert",
                   "ui/webui/resources/html/polymer.html|html, Polymer, afterNextRender",
                 ]
  namespace_rewrites = settings_namespace_rewrites +
                       [ "Polymer.IronResizableBehavior|IronResizableBehavior" ]
}

import("//ui/webui/resources/tools/js_modulizer.gni")

js_modulizer("modulize") {
  input_files = [ "main_page_behavior.js" ]
  namespace_rewrites = settings_namespace_rewrites
}

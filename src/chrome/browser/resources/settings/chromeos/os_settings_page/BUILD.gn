# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../os_settings.gni")

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":main_page_behavior.m",
    ":os_settings_page.m",
    ":settings_idle_load.m",
  ]
}

js_library("os_settings_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_page/os_settings_page.m.js" ]
  deps = [
    ":main_page_behavior.m",
    ":settings_idle_load.m",
    "..:os_page_visibility.m",
    "..:os_route.m",
    "../..:router.m",
    "../..:search_settings.m",
    "../../settings_page:settings_section.m",
    "../bluetooth_page:bluetooth_page.m",
    "../crostini_page:crostini_page.m",
    "../device_page:device_page.m",
    "../kerberos_page:kerberos_page.m",
    "../multidevice_page:multidevice_page.m",
    "../on_startup_page:on_startup_page.m",
    "../os_a11y_page:os_a11y_page.m",
    "../os_apps_page:android_apps_browser_proxy.m",
    "../os_apps_page:android_apps_browser_proxy.m",
    "../os_apps_page:os_apps_page.m",
    "../os_people_page:os_people_page.m",
    "../os_printing_page:os_printing_page.m",
    "../os_privacy_page:os_privacy_page.m",
    "../os_search_page:os_search_page.m",
    "../personalization_page:personalization_page.m",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":os_settings_page_module" ]
}

js_library("main_page_behavior.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_page/main_page_behavior.m.js" ]
  deps = [
    "../..:router.m",
    "../../settings_page:settings_section.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("settings_idle_load.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_page/settings_idle_load.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
  ]
  extra_deps = [ ":settings_idle_load_module" ]
}

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":os_settings_page_module",
    ":settings_idle_load_module",
  ]
}

polymer_modulizer("os_settings_page") {
  js_file = "os_settings_page.js"
  html_file = "os_settings_page.html"
  html_type = "dom-module"
  auto_imports =
      os_settings_auto_imports + [
        "ui/webui/resources/html/polymer.html|Polymer,html,beforeNextRender",
        "ui/webui/resources/html/assert.html|assert",
      ]
  namespace_rewrites = os_settings_namespace_rewrites
}

polymer_modulizer("settings_idle_load") {
  js_file = "settings_idle_load.js"
  html_file = "settings_idle_load.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  auto_imports = [
    "chrome/browser/resources/settings/chromeos/ensure_lazy_loaded.html|ensureLazyLoaded",
    "ui/webui/resources/html/assert.html|assert",
    "ui/webui/resources/html/polymer.html|Polymer,html,templatize,TemplateInstanceBase",
  ]
  namespace_rewrites = os_settings_namespace_rewrites +
                       [ "Polymer.Templatize.templatize|templatize" ]
}

js_modulizer("modulize") {
  input_files = [ "main_page_behavior.js" ]
  namespace_rewrites = os_settings_namespace_rewrites
}

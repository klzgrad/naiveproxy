# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("../optimize_webui.gni")

if (optimize_webui) {
  history_pak_file = "history_resources.pak"
  unpak_folder = "history_resources.unpak"

  optimize_webui("build") {
    host = "history"
    html_in_files = [
      "app.html",
      "lazy_load.html",
    ]
    html_out_files = [
      "app.vulcanized.html",
      "lazy_load.vulcanized.html",
    ]
    input = rebase_path("$target_gen_dir/$unpak_folder", root_build_dir)
    js_out_files = [
      "app.crisper.js",
      "lazy_load.crisper.js",
    ]

    deps = [
      ":unpak",
    ]
    excludes = [
      "chrome://resources/html/util.html",
      "constants.html",
    ]
  }

  unpak("unpak") {
    pak_file = history_pak_file
    out_folder = unpak_folder

    deps = [
      ":flattened_resources",
    ]
  }

  grit("flattened_resources") {
    source = "history_resources.grd"

    defines = chrome_grit_defines
    outputs = [
      "grit/history_resources.h",
      "grit/history_resources_map.cc",
      "grit/history_resources_map.h",
      history_pak_file,
    ]
    output_dir = "$root_gen_dir/chrome/browser/resources/history"
  }
}

js_type_check("closure_compile") {
  deps = [
    ":app",
    ":browser_service",
    ":constants",
    ":externs",
    ":history",
    ":history_item",
    ":history_list",
    ":history_toolbar",
    ":query_manager",
    ":router",
    ":side_bar",
    ":synced_device_card",
    ":synced_device_manager",
  ]
}

js_library("constants") {
}

js_library("browser_service") {
  deps = [
    ":externs",
    "//ui/webui/resources/js:cr",
  ]
}

js_library("externs") {
  deps = [
    ":constants",
  ]
}

js_library("history_item") {
  deps = [
    ":browser_service",
    ":constants",
    ":externs",
    "//ui/webui/resources/js:icon",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr/ui:focus_row_behavior",
  ]
}

js_library("history_list") {
  deps = [
    ":browser_service",
    ":constants",
    ":externs",
    ":history_item",
    "//third_party/polymer/v1_0/components-chromium/iron-a11y-announcer:iron-a11y-announcer-extracted",
    "//third_party/polymer/v1_0/components-chromium/iron-list:iron-list-extracted",
    "//third_party/polymer/v1_0/components-chromium/iron-scroll-threshold:iron-scroll-threshold-extracted",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render",
    "//ui/webui/resources/js:i18n_behavior",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("history_toolbar") {
  deps = [
    ":constants",
    ":externs",
    "//ui/webui/resources/cr_elements/cr_toolbar:cr_toolbar",
    "//ui/webui/resources/cr_elements/cr_toolbar:cr_toolbar_selection_overlay",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:util",
  ]
}

js_library("history") {
  deps = [
    ":app",
    ":constants",
    ":externs",
    "//ui/webui/resources/js:icon",
    "//ui/webui/resources/js:load_time_data",
  ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}

js_library("app") {
  deps = [
    ":constants",
    ":externs",
    ":history_list",
    ":history_toolbar",
    ":side_bar",
    ":synced_device_card",
    ":synced_device_manager",
    "//ui/webui/resources/cr_elements/cr_drawer:cr_drawer",
    "//ui/webui/resources/js:find_shortcut_behavior",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("query_manager") {
  deps = [
    ":browser_service",
    ":externs",
    ":router",
  ]
}

js_library("router") {
  deps = [
    ":constants",
    ":externs",
  ]
}

js_library("side_bar") {
  deps = [
    ":browser_service",
    "//third_party/polymer/v1_0/components-chromium/iron-a11y-keys-behavior:iron-a11y-keys-behavior-extracted",
    "//third_party/polymer/v1_0/components-chromium/iron-selector:iron-selector-extracted",
    "//third_party/polymer/v1_0/components-chromium/paper-ripple:paper-ripple-extracted",
    "//ui/webui/resources/js:load_time_data",
  ]
}

js_library("synced_device_card") {
  deps = [
    ":browser_service",
    ":constants",
    ":externs",
    "//ui/webui/resources/js:icon",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js/cr/ui:focus_row",
  ]
}

js_library("synced_device_manager") {
  deps = [
    ":browser_service",
    ":synced_device_card",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render",
    "//ui/webui/resources/js/cr/ui:focus_grid",
  ]
}

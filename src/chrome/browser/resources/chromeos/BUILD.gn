# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")

assert(is_chromeos, "Only Chrome OS resources in //c/b/resources//chromeos.")

grit("cellular_setup_resources") {
  source = "cellular_setup/cellular_setup_resources.grd"

  defines = chrome_grit_defines
  outputs = [
    "grit/cellular_setup_resources.h",
    "grit/cellular_setup_resources_map.cc",
    "grit/cellular_setup_resources_map.h",
    "cellular_setup_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

grit("multidevice_setup_resources") {
  source = "multidevice_setup/multidevice_setup_resources.grd"

  defines = chrome_grit_defines
  outputs = [
    "grit/multidevice_setup_resources.h",
    "grit/multidevice_setup_resources_map.cc",
    "grit/multidevice_setup_resources_map.h",
    "multidevice_setup_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

grit("camera_resources") {
  source = "camera/camera_resources.grd"

  defines = chrome_grit_defines
  outputs = [
    "grit/camera_resources.h",
    "grit/camera_resources_map.cc",
    "grit/camera_resources_map.h",
    "camera_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"

  deps = [
    "//media/capture/video/chromeos/mojom:cros_camera_js",
  ]

  # The .grd contains references to generated files.
  source_is_generated = true

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]
}

group("closure_compile") {
  deps = [
    "accessibility/braille_ime:closure_compile",
    "accessibility/select_to_speak:closure_compile",
    "accessibility/switch_access:closure_compile",
    "add_supervision:closure_compile",
    "bluetooth_pairing_dialog:closure_compile",
    "camera/src/js:closure_compile",
    "crostini_installer:closure_compile",
    "emulator:closure_compile",
    "internet_config_dialog:closure_compile",
    "internet_detail_dialog:closure_compile",
    "login:closure_compile",
    "machine_learning:closure_compile",
    "multidevice_setup:closure_compile",
    "network_ui:closure_compile",
    "set_time_dialog:closure_compile",
    "smb_shares:closure_compile",
    "sys_internals:closure_compile",
  ]
}

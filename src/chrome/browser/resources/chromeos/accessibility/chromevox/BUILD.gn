# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//chrome/common/features.gni")
import("//chrome/test/base/js2gtest.gni")
import("//testing/test.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("run_jsbundler.gni")

declare_args() {
  # Whether to compress the main Chromevox javascript files or load the
  # modules individually from source files.
  chromevox_compress_js = true
}

closure_library_dir =
    "//third_party/chromevox/third_party/closure-library/closure/goog"

# List of all modules that are included in one or more of the production
# chromevox scripts.
chromevox_modules = [
  "background/annotation/node_identifier.js",
  "background/annotation/user_annotation_handler.js",
  "background/automation_object_constructor_installer.js",
  "../common/automation_predicate.js",
  "../common/automation_util.js",
  "background/background.js",
  "background/base_automation_handler.js",
  "background/braille_background.js",
  "background/braille_captions_background.js",
  "background/braille_command_data.js",
  "background/braille_command_handler.js",
  "background/chromevox_state.js",
  "background/classic_background.js",
  "background/color.js",
  "background/command_handler.js",
  "../common/constants.js",
  "background/cursors.js",
  "background/custom_automation_event.js",
  "background/desktop_automation_handler.js",
  "background/download_handler.js",
  "background/earcon_engine.js",
  "background/editing.js",
  "background/event_source.js",
  "background/find_handler.js",
  "background/focus_automation_handler.js",
  "background/gesture_command_data.js",
  "background/gesture_command_handler.js",
  "background/injected_script_loader.js",
  "background/keyboard_handler.js",
  "background/keymaps/key_map.js",
  "background/locale_output_helper.js",
  "learn_mode/kbexplorer.js",
  "background/live_regions.js",
  "background/logging/event_stream_logger.js",
  "background/logging/log.js",
  "background/logging/log_store.js",
  "background/logging/log_types.js",
  "background/logging/output_logger.js",
  "background/logging/tree_dumper.js",
  "background/math_handler.js",
  "background/media_automation_handler.js",
  "background/mouse_handler.js",
  "background/next_earcons.js",
  "background/notifications.js",
  "background/output.js",
  "background/panel_command.js",
  "background/phonetic_data.js",
  "background/prefs.js",
  "background/range_automation_handler.js",
  "background/recovery_strategy.js",
  "background/tabs_api_handler.js",
  "../common/tree_walker.js",
  "braille/bluetooth_braille_display_manager.js",
  "braille/bluetooth_braille_display_ui.js",
  "braille/braille_display_manager.js",
  "braille/braille_input_handler.js",
  "braille/braille_key_event_rewriter.js",
  "braille/braille_key_types.js",
  "braille/braille_table.js",
  "braille/braille_translator_manager.js",
  "braille/expanding_braille_translator.js",
  "braille/liblouis.js",
  "braille/nav_braille.js",
  "braille/pan_strategy.js",
  "braille/spans.js",
  "common/abstract_earcons.js",
  "common/abstract_tts.js",
  "common/braille_interface.js",
  "common/chromevox.js",
  "common/command_store.js",
  "common/composite_tts.js",
  "common/console_tts.js",
  "common/editable_text_base.js",
  "common/extension_bridge.js",
  "common/key_sequence.js",
  "common/key_util.js",
  "common/keyboard_handler.js",
  "common/msgs.js",
  "common/nav_description.js",
  "common/spannable.js",
  "common/string_util.js",
  "common/tts_background.js",
  "common/tts_base.js",
  "common/tts_interface.js",
  "injected/api_implementation.js",
  "injected/loader.js",
  "injected/script_installer.js",
  "options/options.js",
  "panel/annotations_ui.js",
  "panel/i_search.js",
  "panel/panel.js",
  "panel/panel_menu.js",
  "panel/panel_menu_item.js",
  "panel/tutorial.js",
  "third_party/tamachiyomi/ja_phonetic_data.js",
]

# Closure library modules neede by chromevox.
relative_closure_library_modules = [
  "i18n/pluralrules.js",
  "i18n/ordinalrules.js",
  "i18n/messageformat.js",
  "string/string.js",
  "dom/nodetype.js",
  "base.js",
  "asserts/asserts.js",
  "debug/error.js",
  "object/object.js",
]

closure_library_modules =
    rebase_path(relative_closure_library_modules, ".", closure_library_dir)

chromevox_out_dir = "$root_out_dir/resources/chromeos/accessibility/chromevox"

group("build") {
  deps = [
    ":chromevox_copied_files",
    "//chrome/browser/resources/chromeos/accessibility/braille_ime:braille_ime_manifest",
    "//third_party/chromevox:chromevox_third_party_resources",
    "//third_party/liblouis",
  ]
  if (chromevox_compress_js) {
    deps += [
      ":chromevox_background_script",
      ":chromevox_learn_mode_script",
      ":chromevox_log_script",
      ":chromevox_min_content_script",
      ":chromevox_options_script",
      ":chromevox_panel_script",
    ]
  } else {
    deps += [ ":chromevox_deps_js" ]
  }
}

chromevox_background_script_loader_file = "background/loader.js"
chromevox_learn_mode_loader_file = "learn_mode/kbexplorer_loader.js"
chromevox_log_loader_file = "background/logging/log_loader.js"
chromevox_min_content_script_loader_file = "injected/loader.js"
chromevox_options_script_loader_file = "options/options_loader.js"
chromevox_panel_script_loader_file = "panel/panel_loader.js"

# Instead of setting up one copy target for each subdirectory, use a script
# to copy all files.
run_jsbundler("chromevox_copied_files") {
  mode = "copy"
  dest_dir = chromevox_out_dir
  sources = [
    "background/background.html",
    "background/earcons/control.wav",
    "background/earcons/selection.wav",
    "background/earcons/selection_reverse.wav",
    "background/earcons/skim.wav",
    "background/earcons/small_room_2.wav",
    "background/earcons/static.wav",
    "background/keymaps/default_keymap.json",
    "background/logging/log.css",
    "background/logging/log.html",
    "images/chromevox-128.png",
    "images/chromevox-16.png",
    "images/chromevox-19.png",
    "images/chromevox-48.png",
    "images/chromevox.svg",
    "images/close-19.png",
    "images/close-hover-19.png",
    "images/options-19.png",
    "images/options-hover-19.png",
    "images/triangle-6.png",
    "injected/api.js",
    "learn_mode/kbexplorer.html",
    "options/checked.png",
    "options/options.css",
    "options/options.html",
    "options/unchecked.png",
    "panel/panel.css",
    "panel/panel.html",
  ]
  if (!chromevox_compress_js) {
    sources += chromevox_modules
    sources += [
      "closure/closure_preinit.js",
      chromevox_background_script_loader_file,
      chromevox_learn_mode_loader_file,
      chromevox_log_loader_file,
      chromevox_min_content_script_loader_file,
      chromevox_options_script_loader_file,
      chromevox_panel_script_loader_file,
    ]
  }
  if (!chromevox_compress_js) {
    sources += closure_library_modules
  }
  rewrite_rules = [
    rebase_path(".", root_build_dir) + ":",
    rebase_path(closure_library_dir, root_build_dir) + ":closure",
  ]
}

template("generate_deps_js") {
  if (defined(invoker.rewrite_rules)) {
    rewrite_rules = invoker.rewrite_rules
  } else {
    rewrite_rules = []
  }
  action(target_name) {
    if (defined(invoker.testonly)) {
      testonly = invoker.testonly
    }
    script = "tools/generate_deps.py"
    inputs = jsbundler_modules
    sources = invoker.sources
    outputs = [ invoker.output_file ]
    args = [
      "-o",
      rebase_path(invoker.output_file, root_build_dir),
    ]
    foreach(rule, rewrite_rules) {
      args += [
        "-w",
        rule,
      ]
    }
    args += rebase_path(sources, root_build_dir)
    if (defined(invoker.deps)) {
      deps = invoker.deps
    }
  }
}

if (chromevox_compress_js) {
  template("compress_js") {
    run_jsbundler(target_name) {
      assert(defined(invoker.output_file))
      mode = "compressed_bundle"
      modules = chromevox_modules + closure_library_modules
      sources = invoker.sources
      output_file = invoker.output_file
    }
  }

  compress_js("chromevox_learn_mode_script") {
    sources = [ chromevox_learn_mode_loader_file ]
    output_file = "$chromevox_out_dir/chromeVoxKbExplorerScript.js"
  }

  compress_js("chromevox_log_script") {
    sources = [ chromevox_log_loader_file ]
    output_file = "$chromevox_out_dir/chromeVoxLogScript.js"
  }

  compress_js("chromevox_options_script") {
    sources = [ chromevox_options_script_loader_file ]
    output_file = "$chromevox_out_dir/chromeVoxChromeOptionsScript.js"
  }

  compress_js("chromevox_background_script") {
    sources = [ chromevox_background_script_loader_file ]
    output_file = "$chromevox_out_dir/chromeVoxChromeBackgroundScript.js"
  }

  compress_js("chromevox_min_content_script") {
    sources = [ chromevox_min_content_script_loader_file ]
    output_file = "$chromevox_out_dir/chromeVoxChromePageScript.js"
  }

  compress_js("chromevox_panel_script") {
    sources = [ chromevox_panel_script_loader_file ]
    output_file = "$chromevox_out_dir/chromeVoxPanelScript.js"
  }
} else {
  generate_deps_js("chromevox_deps_js") {
    sources = chromevox_modules + closure_library_modules
    output_file = "$chromevox_out_dir/deps.js"
    rewrite_rules = [
      rebase_path(".", root_build_dir) + ":..",
      rebase_path(closure_library_dir, root_build_dir) + ":../closure",
    ]
  }
}

if (is_chromeos) {
  source_set("browser_tests") {
    testonly = true
    assert(enable_extensions)

    deps = [
      ":chromevox_extjs_tests",
      ":chromevox_unitjs_tests",
    ]

    # TODO(jamescook): Figure out which of these are really necessary.
    data = [
      "$root_out_dir/chrome_100_percent.pak",
      "$root_out_dir/chrome_200_percent.pak",
      "$root_out_dir/locales/en-US.pak",
      "$root_out_dir/locales/fr.pak",
      "$root_out_dir/resources.pak",
      "$root_out_dir/resources/chromeos/accessibility/chromevox/",
      "$root_out_dir/test_data/chrome/browser/resources/chromeos/accessibility/chromevox/",

      "//chrome/browser/resources/chromeos/accessibility/common",
      "//chrome/browser/resources/chromeos/accessibility/chromevox/",
      "//third_party/chromevox/",
    ]
    data += js2gtest_js_libraries
  }

  action("chromevox_test_messages_js") {
    script = "tools/generate_test_messages.py"
    sources = [ "$root_out_dir/resources/chromeos/accessibility/_locales/en/messages.json.gz" ]
    output_file = "$root_out_dir/test_data/chrome/browser/resources/chromeos/accessibility/chromevox/host/testing/test_messages.js"
    outputs = [ output_file ]
    deps = [
      "//chrome/browser/resources/chromeos/accessibility:accessibility_strings",
    ]
    args = [
             "-o",
             rebase_path(output_file, root_build_dir),
           ] + rebase_path(sources, root_build_dir)
  }

  chromevox_test_modules = [ "testing/test_msgs.js" ] +
                           get_target_outputs(":chromevox_test_messages_js")

  generate_deps_js("chromevox_test_deps_js") {
    sources =
        closure_library_modules + chromevox_modules + chromevox_test_modules
    output_file = "$target_gen_dir/test_deps.js"
    rewrite_rules = [
      rebase_path("//", root_build_dir) + ":",
      rebase_path("$root_out_dir/test_data", root_build_dir) + ":",
    ]
    deps = [ ":chromevox_test_messages_js" ]
  }

  js2gtest("chromevox_unitjs_tests") {
    test_type = "webui"
    sources = [
      "braille/bluetooth_braille_display_manager_test.js",
      "braille/bluetooth_braille_display_ui_test.js",
      "braille/braille_display_manager_test.js",
      "braille/braille_input_handler_test.js",
      "braille/expanding_braille_translator_test.js",
      "braille/pan_strategy_test.js",
      "common/key_sequence_test.js",
      "common/spannable_test.js",
      "testing/mock_feedback_test.js",
    ]
    gen_include_files = [
      "testing/assert_additions.js",
      "testing/callback_helper.js",
      "testing/chromevox_unittest_base.js",
      "testing/mock_feedback.js",
    ]
    test_deps_js_outputs = get_target_outputs(":chromevox_test_deps_js")
    deps_js = test_deps_js_outputs[0]
    deps = [ ":chromevox_test_deps_js" ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }

  js2gtest("chromevox_extjs_tests") {
    test_type = "extension"
    sources = [
      "background/annotation/node_identifier_test.js",
      "background/annotation/user_annotation_handler_test.js",
      "background/background_test.js",
      "background/braille_command_data_test.js",
      "background/color_test.js",
      "background/cursors_test.js",
      "background/download_handler_test.js",
      "background/editing_test.js",
      "background/keyboard_handler_test.js",
      "background/live_regions_test.js",
      "background/locale_output_helper_test.js",
      "background/logging/log_store_test.js",
      "background/output_test.js",
      "background/recovery_strategy_test.js",
      "braille/braille_table_test.js",
      "braille/braille_translator_manager_test.js",
      "braille/liblouis_test.js",
      "common/tts_background_test.js",
      "options/options_test.js",
      "panel/i_search_test.js",
      "panel/panel_test.js",
    ]
    gen_include_files = [
      "testing/assert_additions.js",
      "testing/callback_helper.js",
      "testing/chromevox_e2e_test_base.js",
      "testing/chromevox_next_e2e_test_base.js",
      "testing/mock_feedback.js",
    ]

    # The test base classes generate C++ code with these deps.
    deps = [
      "//ash",
      "//base",
      "//chrome/browser/chromeos",
      "//chrome/common",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}

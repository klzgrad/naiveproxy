# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("external_service") {
  sources = [
    "external_connector.h",
    "external_connector_impl.cc",
    "external_connector_impl.h",
    "external_service.cc",
    "external_service.h",
  ]
  deps = [
    "//base",
    "//chromecast/external_mojo/public/mojom",
    "//mojo/core/embedder",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/platform",
    "//mojo/public/cpp/system",
  ]
}

source_set("process_setup") {
  sources = [
    "process_setup.cc",
    "process_setup.h",
  ]
  deps = [
    "//base",
    "//base:base_static",
    "//chromecast:chromecast_buildflags",
  ]

  if (!is_fuchsia) {
    sources += [
      "crash_reporter_client.cc",
      "crash_reporter_client.h",
    ]
    deps += [
      "//chromecast/crash",
      "//components/crash/core/app",
    ]
  }
}

source_set("service_process") {
  sources = [ "service_process.h" ]
}

source_set("standalone_service_main") {
  sources = [ "standalone_service_main.cc" ]
  deps = [
    ":external_service",
    ":process_setup",
    "//base",
    "//chromecast/external_mojo/public/cpp:common",
    "//mojo/core/embedder",
  ]
  public_deps = [ ":service_process" ]
}

source_set("chromium_service") {
  sources = [
    "chromium_service.cc",
    "chromium_service.h",
  ]
  deps = [
    ":external_service",
    "//base",
    "//chromecast/external_mojo/public/mojom",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
  ]
}

executable("standalone_mojo_broker") {
  sources = [ "standalone_mojo_broker.cc" ]
  deps = [
    ":process_setup",
    "//base",
    "//chromecast/external_mojo/public/cpp:common",
    "//chromecast/external_mojo/public/cpp:external_mojo_broker",
    "//mojo/core/embedder",
  ]
}

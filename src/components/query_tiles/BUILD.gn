# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

if (is_android) {
  java_group("java") {
    deps = [ ":public_java" ]
  }
}

group("query_tiles") {
  public_deps = [
    ":factory",
    ":public",
  ]

  deps = [ "internal" ]
}

source_set("public") {
  sources = [
    "logger.h",
    "switches.cc",
    "switches.h",
    "tile.cc",
    "tile.h",
    "tile_service.h",
    "tile_service_prefs.cc",
    "tile_service_prefs.h",
  ]

  deps = [ "//ui/gfx" ]

  public_deps = [
    "//base",
    "//components/keyed_service/core",
    "//components/prefs",
    "//url:url",
  ]

  if (is_android) {
    sources += [
      "android/tile_conversion_bridge.cc",
      "android/tile_conversion_bridge.h",
      "android/tile_provider_bridge.cc",
      "android/tile_provider_bridge.h",
    ]

    deps += [ ":jni_headers" ]
  }
}

source_set("factory") {
  sources = [
    "tile_service_factory_helper.cc",
    "tile_service_factory_helper.h",
  ]

  deps = [
    ":public",
    "internal:internal",
    "//components/background_task_scheduler:public",
    "//components/leveldb_proto",
    "//components/query_tiles/proto",
  ]
}

if (is_android) {
  android_library("public_java") {
    sources = [
      "android/java/src/org/chromium/components/query_tiles/QueryTile.java",
      "android/java/src/org/chromium/components/query_tiles/QueryTileConstants.java",
      "android/java/src/org/chromium/components/query_tiles/TileProvider.java",
      "android/java/src/org/chromium/components/query_tiles/TileUmaLogger.java",
      "android/java/src/org/chromium/components/query_tiles/bridges/TileConversionBridge.java",
      "android/java/src/org/chromium/components/query_tiles/bridges/TileProviderBridge.java",
    ]

    deps = [
      "//base:base_java",
      "//base:jni_java",
      "//components/browser_ui/widget/android:java",
      "//third_party/android_deps:androidx_annotation_annotation_java",
      "//third_party/android_deps:androidx_recyclerview_recyclerview_java",
      "//ui/android:ui_java",
    ]

    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  }

  generate_jni("jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/components/query_tiles/bridges/TileConversionBridge.java",
      "android/java/src/org/chromium/components/query_tiles/bridges/TileProviderBridge.java",
    ]
  }

  android_library("test_support_java") {
    sources = [ "android/java/src/org/chromium/components/query_tiles/TestTileProvider.java" ]

    deps = [
      ":public_java",
      "//base:base_java",
    ]
  }

  android_library("query_tiles_junit_tests") {
    bypass_platform_checks = true
    testonly = true
    sources = [ "android/java/src/org/chromium/components/query_tiles/TileUmaLoggerTest.java" ]

    deps = [
      ":public_java",
      ":test_support_java",
      "//base:base_java",
      "//base:base_junit_test_support",
      "//third_party/android_deps:robolectric_all_java",
      "//third_party/junit",
    ]
  }
}

group("unit_tests") {
  testonly = true

  deps = [
    ":query_tiles_unit_tests",
    "internal:unit_tests",
  ]

  data_deps = [ ":components_unittests_gtest_filter" ]
}

source_set("components_unittests_gtest_filter") {
  testonly = true

  data = [ "components_unittests.filter" ]
}

source_set("query_tiles_unit_tests") {
  testonly = true
  sources = [ "tile_unittest.cc" ]
  deps = [
    ":public",
    "test:test_lib",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}

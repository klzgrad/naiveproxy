# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
if (is_chromeos) {
  import("//printing/backend/tools/code_generator.gni")
}

declare_args() {
  # Enable print media localization only on the platforms that support CUPS IPP
  # (ChromeOS and macOS for now). The localization expects media vendor IDs
  # uniquely generated by CUPS IPP.
  enable_print_media_l10n = is_chromeos || is_mac
}

buildflag_header("printing_buildflags") {
  header = "printing_buildflags.h"

  flags = [ "PRINT_MEDIA_L10N_ENABLED=$enable_print_media_l10n" ]
}

if (is_chromeos) {
  localization_map_path = "$target_gen_dir/ipp_l10n.cc"

  ipp_code_generate("ipp_l10n_generate") {
    outputs = [ localization_map_path ]
    args = [ "--localization-map=" +
             rebase_path(localization_map_path, root_build_dir) ]
  }
}

static_library("browser") {
  sources = [
    "features.cc",
    "features.h",
    "print_composite_client.cc",
    "print_composite_client.h",
    "print_manager.cc",
    "print_manager.h",
    "print_manager_utils.cc",
    "print_manager_utils.h",
    "printer_capabilities.cc",
    "printer_capabilities.h",
  ]

  public_deps = [
    ":printing_buildflags",
    "//content/public/browser",
  ]

  deps = [
    "//base",
    "//components/crash/core/common",
    "//components/discardable_memory/service",
    "//components/printing/common",
    "//components/printing/common:mojo_interfaces",
    "//components/services/print_compositor/public/cpp",
    "//components/services/print_compositor/public/mojom",
    "//components/strings:components_strings_grit",
    "//printing",
    "//printing/common:common",
    "//ui/base",
    "//ui/gfx/geometry",
  ]

  if (is_mac) {
    sources += [
      "printer_capabilities_mac.h",
      "printer_capabilities_mac.mm",
    ]
  }

  if (is_chromeos) {
    deps += [ ":ipp_l10n_generate" ]

    sources += [
      "ipp_l10n.h",
      localization_map_path,
    ]
  }

  if (enable_print_media_l10n) {
    sources += [
      "print_media_l10n.cc",
      "print_media_l10n.h",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "printer_capabilities_unittest.cc" ]

  deps = [
    "//base",
    "//components/printing/browser",
    "//content/test:test_support",
    "//printing:test_support",
    "//testing/gtest",
  ]

  if (is_mac) {
    sources += [ "printer_capabilities_mac_unittest.mm" ]
  }

  if (enable_print_media_l10n) {
    sources += [ "print_media_l10n_unittest.cc" ]
  }
}

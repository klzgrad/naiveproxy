# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")
import("//mojo/public/tools/bindings/mojom.gni")
if (is_android) {
  import("//build/config/android/rules.gni")
}

jumbo_static_library("common") {
  sources = [
    "content_settings.cc",
    "content_settings.h",
    "content_settings_param_traits.cc",
    "content_settings_param_traits.h",
    "content_settings_pattern.cc",
    "content_settings_pattern.h",
    "content_settings_pattern_parser.cc",
    "content_settings_pattern_parser.h",
    "content_settings_types.h",
    "content_settings_utils.cc",
    "content_settings_utils.h",
    "cookie_settings_base.cc",
    "cookie_settings_base.h",
    "pref_names.cc",
    "pref_names.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [ ":features" ]

  deps = [
    "//base",
    "//ipc",
    "//mojo/public/cpp/base",
    "//mojo/public/cpp/bindings:struct_traits",
    "//net",
    "//url",
  ]
}

jumbo_source_set("unit_tests") {
  testonly = true
  sources = [
    "content_settings_pattern_parser_unittest.cc",
    "content_settings_pattern_unittest.cc",
    "cookie_settings_base_unittest.cc",
  ]

  deps = [
    ":common",
    "//base",
    "//net",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}

mojom("mojo_bindings") {
  generate_java = true
  sources = [ "content_settings.mojom" ]

  public_deps = [ "//mojo/public/mojom/base" ]
}

component("features") {
  output_name = "content_settings_features"

  defines = [ "IS_CONTENT_SETTINGS_FEATURES_IMPL" ]

  sources = [
    "features.cc",
    "features.h",
  ]

  deps = [ "//base" ]
}

if (is_android) {
  java_cpp_enum("content_settings_type_javagen") {
    sources = [ "content_settings_types.h" ]
  }
}

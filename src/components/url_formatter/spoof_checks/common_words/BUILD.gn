# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

action_foreach("common_words_dafsa") {
  script = "//net/tools/dafsa/make_dafsa.py"
  sources = [
    "data/common_words.gperf",
    "data/common_words_test.gperf",
  ]
  outputs = [ "${target_gen_dir}/{{source_name_part}}-inc.cc" ]
  args = [
    "{{source}}",
    rebase_path("${target_gen_dir}/{{source_name_part}}-inc.cc",
                root_build_dir),
  ]
}

source_set("common") {
  sources = [
    "common_words_util.cc",
    "common_words_util.h",
  ]

  deps = [
    ":common_words_dafsa",
    "//base",
    "//net",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "common_words_util_unittest.cc" ]
  deps = [
    ":common",
    ":common_words_dafsa",
    "//testing/gtest",
  ]
}

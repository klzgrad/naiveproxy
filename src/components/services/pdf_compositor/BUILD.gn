# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//printing/buildflags/buildflags.gni")

static_library("pdf_compositor") {
  sources = [
    "pdf_compositor_impl.cc",
    "pdf_compositor_impl.h",
  ]

  deps = [
    "//base",
    "//components/crash/core/common:crash_key",
    "//components/discardable_memory/client",
    "//components/discardable_memory/public/mojom",
    "//components/services/pdf_compositor/public/cpp",
    "//content/public/utility",
    "//printing/common",
    "//skia",
    "//third_party/blink/public:blink_headers",
  ]

  if (is_win) {
    deps += [ "//content/public/child" ]
  }

  public_deps = [
    "//components/services/pdf_compositor/public/mojom",
  ]
}

if (enable_basic_printing) {
  source_set("unit_tests") {
    testonly = true
    sources = [
      "pdf_compositor_impl_unittest.cc",
    ]

    include_dirs = [
      "//skia/config",
      "//testing/gmock/include",
      "//third_party/skia/include/core",
    ]

    deps = [
      ":pdf_compositor",
      "//base",
      "//base/test:test_support",
      "//cc/paint:paint",
      "//components/crash/core/common:crash_key",
      "//components/services/pdf_compositor/public/cpp",
      "//components/services/pdf_compositor/public/mojom",
      "//skia",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

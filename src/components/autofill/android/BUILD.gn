# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/config/locales.gni")
import("//tools/grit/grit_rule.gni")

java_strings_grd("autofill_strings_grd") {
  grd_file = "java/strings/autofill_strings.grd"
  outputs = [ "values/autofill_strings.xml" ] +
            process_file_template(
                android_bundle_locales_as_resources,
                [ "values-{{source_name_part}}/autofill_strings.xml" ])
}

android_resources("autofill_java_resources") {
  sources = [
    "java/res/layout/autofill_dropdown_footer_item_refresh.xml",
    "java/res/layout/autofill_dropdown_item.xml",
    "java/res/layout/autofill_dropdown_item_refresh.xml",
    "java/res/values/colors.xml",
    "java/res/values/dimens.xml",
    "java/res/values/styles.xml",
  ]
  deps = [
    ":autofill_strings_grd",
    "//ui/android:ui_java_resources",
  ]
}

java_cpp_enum("autofill_core_browser_java_enums") {
  sources = [
    "../core/browser/ui/accessory_sheet_enums.h",
    "../core/browser/ui/popup_item_ids.h",
  ]
}

android_library("full_autofill_java") {
  deps = [
    ":autofill_java_resources",
    ":payments_autofill_java",
    "//base:base_java",
    "//content/public/android:content_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_appcompat_appcompat_resources_java",
    "//third_party/androidx:androidx_core_core_java",
    "//ui/android:ui_no_recycler_view_java",
  ]
  sources = [
    "java/src/org/chromium/components/autofill/AutofillDelegate.java",
    "java/src/org/chromium/components/autofill/AutofillDropdownAdapter.java",
    "java/src/org/chromium/components/autofill/AutofillDropdownFooter.java",
    "java/src/org/chromium/components/autofill/AutofillPopup.java",
    "java/src/org/chromium/components/autofill/AutofillSuggestion.java",
  ]
  srcjar_deps = [ ":autofill_core_browser_java_enums" ]
  resources_package = "org.chromium.components.autofill"
}

# A library containing the minimal deps for payments, so that ui_java_resources
# doesn't have to be pulled in.
android_library("payments_autofill_java") {
  sources = [
    "java/src/org/chromium/components/autofill/Completable.java",
    "java/src/org/chromium/components/autofill/EditableOption.java",
  ]
  deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]
}

java_group("autofill_java") {
  deps = [
    ":full_autofill_java",
    ":payments_autofill_java",
  ]
}

# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/nacl/features.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

assert(enable_nacl)

static_library("browser") {
  sources = [
    "bad_message.cc",
    "bad_message.h",
    "nacl_broker_host_win.cc",
    "nacl_broker_host_win.h",
    "nacl_broker_service_win.cc",
    "nacl_broker_service_win.h",
    "nacl_browser.cc",
    "nacl_browser.h",
    "nacl_file_host.cc",
    "nacl_file_host.h",
    "nacl_host_message_filter.cc",
    "nacl_host_message_filter.h",
    "nacl_process_host.cc",
    "nacl_process_host.h",
    "nacl_validation_cache.cc",
    "nacl_validation_cache.h",
    "pnacl_host.cc",
    "pnacl_host.h",
    "pnacl_translation_cache.cc",
    "pnacl_translation_cache.h",
  ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//components/nacl/common",
    "//components/nacl/common:buildflags",
    "//components/nacl/common:debug_exception_handler",
    "//components/url_formatter",
    "//content/public/browser",
    "//content/public/common",
    "//native_client/src/trusted/service_runtime:sel_main_chrome",
    "//net",
    "//ppapi/host",
    "//ppapi/proxy:ipc",
    "//ppapi/shared_impl",
  ]

  public_deps = [
    "//ipc",
    "//services/service_manager/public/mojom",
  ]

  data_deps = []

  if (is_linux || is_chromeos) {
    sources += [
      "../zygote/nacl_fork_delegate_linux.cc",
      "../zygote/nacl_fork_delegate_linux.h",
    ]

    deps += [
      "//components/nacl/loader:nacl_helper_integration",
      "//sandbox/linux:sandbox_services",
      "//sandbox/linux:suid_sandbox_client",
    ]

    if (enable_nacl_nonsfi) {
      data_deps += [ "//components/nacl/loader:helper_nonsfi" ]
    }
  }

  if (is_win && current_cpu == "x86") {
    data_deps += [ "//components/nacl/broker:nacl64" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "nacl_file_host_unittest.cc",
    "nacl_process_host_unittest.cc",
    "nacl_validation_cache_unittest.cc",
    "pnacl_host_unittest.cc",
    "pnacl_translation_cache_unittest.cc",
    "test_nacl_browser_delegate.cc",
    "test_nacl_browser_delegate.h",
  ]

  deps = [
    ":browser",
    "//base",
    "//components/nacl/common",
    "//content/test:test_support",
    "//net:test_support",
  ]

  if (is_linux || is_chromeos) {
    sources += [ "../zygote/nacl_fork_delegate_linux_unittest.cc" ]
  }
}

# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

static_library("core") {
  sources = [
    "confirm_infobar_delegate.cc",
    "confirm_infobar_delegate.h",
    "infobar.cc",
    "infobar.h",
    "infobar_container.cc",
    "infobar_container.h",
    "infobar_delegate.cc",
    "infobar_delegate.h",
    "infobar_manager.cc",
    "infobar_manager.h",
    "simple_alert_infobar_delegate.cc",
    "simple_alert_infobar_delegate.h",
  ]

  public_deps = [
    ":feature_flags",
    "//skia",
  ]

  deps = [
    "//base",
    "//ui/base",
    "//ui/gfx",
    "//ui/gfx/animation",
    "//ui/strings",
    "//url",
  ]

  if (use_aura || (!is_ios && !is_android)) {
    deps += [ "//ui/native_theme" ]
  }
}

source_set("feature_flags") {
  sources = [
    "infobar_feature.cc",
    "infobar_feature.h",
  ]

  public_deps = [ "//base" ]
}

if (is_android) {
  java_cpp_enum("infobar_generated_enums") {
    sources = [ "infobar_delegate.h" ]
  }

  android_library("infobar_enums_java") {
    deps = [ "//third_party/android_deps:androidx_annotation_annotation_java" ]

    srcjar_deps = [ ":infobar_generated_enums" ]
  }
}

# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

assert(is_chromeos, "Non-Chrome-OS builds must not depend on //chromeos")

# Used by targets that compile into the implementation.
config("chromeos_implementation") {
  defines = [ "CHROMEOS_IMPLEMENTATION" ]
}

mojom("mojom") {
  sources = [ "userspace_swap.mojom" ]

  cpp_only = true
  disable_variants = true

  deps = [ "//mojo/public/mojom/base" ]
}

component("userspace_swap") {
  configs += [
    ":chromeos_implementation",
    "//build/config/linux/nss:system_nss_no_ssl_config",
  ]

  public_deps = [ "//chromeos/constants" ]
  deps = [
    ":mojom",
    "//base",
    "//base/util/memory_pressure",
    "//chromeos:chromeos_export",
    "//chromeos/dbus",
    "//chromeos/dbus/constants",
    "//chromeos/memory",
    "//crypto",
    "//crypto:platform",
    "//services/resource_coordinator/public/cpp/memory_instrumentation",
    "//third_party/zlib/google:compression_utils",
  ]
  sources = [
    "region.h",
    "swap_storage.cc",
    "swap_storage.h",
    "userfaultfd.cc",
    "userfaultfd.h",
    "userspace_swap.cc",
    "userspace_swap.h",
  ]
}

source_set("unit_tests") {
  testonly = true
  configs += [ "//build/config/linux/nss:system_nss_no_ssl_config" ]
  deps = [
    ":mojom",
    ":userspace_swap",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//mojo/core/embedder",
    "//services/resource_coordinator/public/cpp/memory_instrumentation",
    "//testing/gmock",
    "//testing/gtest",
  ]
  sources = [
    "swap_storage_unittest.cc",
    "userfaultfd_unittest.cc",
    "userspace_swap_unittest.cc",
  ]
}

# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//chromeos/components/web_applications/system_apps.gni")
import("//third_party/closure_compiler/compile_js.gni")

assert(is_chromeos, "Telemetry Extension is Chrome OS only")
assert(!is_official_build,
       "Telemetry Extension is only built for unofficial builds")

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "telemetry_extension_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [ ":browser_test_support" ]
}

source_set("browser_test_support") {
  testonly = true
  sources = [
    "telemetry_extension_ui_browsertest.cc",
    "telemetry_extension_ui_browsertest.h",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "../:telemetry_extension_ui",
    "//chrome/browser/chromeos/wilco_dtc_supportd:mojo_utils",
    "//chrome/test:test_support_ui",
    "//chromeos/components/web_applications/test:test_support",
    "//chromeos/constants",
    "//chromeos/dbus/cros_healthd:cros_healthd",
  ]

  data = [
    "trusted_test_requester.js",
    "untrusted_app_resources/",
    "untrusted_browsertest.js",
    "untrusted_test_handlers.js",
  ]
}

group("closure_compile") {
  testonly = true
  deps = [
    ":trusted_browsertest_closure_compile",
    ":untrusted_browsertest_closure_compile",
    "untrusted_app_resources:closure_compile",
  ]
}

js_type_check("trusted_browsertest_closure_compile") {
  testonly = true
  closure_flags = system_app_closure_flags
  deps = [ ":trusted_browsertest" ]
}

js_type_check("untrusted_browsertest_closure_compile") {
  testonly = true
  closure_flags = system_app_closure_flags
  deps = [ ":untrusted_browsertest" ]
}

js_library("trusted_browsertest") {
  testonly = true
  sources = [
    "telemetry_extension_ui_browsertest.js",
    "trusted_test_requester.js",
  ]
  deps = [ "//chromeos/components/telemetry_extension_ui/resources:trusted" ]
  externs_list =
      [ "//chromeos/components/web_applications/js2gtest_support.externs.js" ]
}

js_library("untrusted_browsertest") {
  testonly = true
  sources = [
    "untrusted_browsertest.js",
    "untrusted_test_handlers.js",
  ]
  deps = [ "//chromeos/components/telemetry_extension_ui/resources:untrusted" ]
  externs_list =
      [ "//chromeos/components/web_applications/js2gtest_support.externs.js" ]
}

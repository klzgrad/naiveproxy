# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//chromeos/components/web_applications/system_apps.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//third_party/closure_compiler/compile_js.gni")

assert(is_chromeos, "Media App is Chrome OS only")

static_library("media_app_ui") {
  sources = [
    "media_app_guest_ui.cc",
    "media_app_guest_ui.h",
    "media_app_page_handler.cc",
    "media_app_page_handler.h",
    "media_app_ui.cc",
    "media_app_ui.h",
    "media_app_ui_delegate.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    ":mojo_bindings",
    ":mojo_bindings_js",
    "//chromeos/components/web_applications",
    "//chromeos/constants",
    "//chromeos/resources:media_app_bundle_resources",
    "//chromeos/resources:media_app_resources",
    "//chromeos/strings",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/platform",
    "//ui/webui",
  ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/media_app_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":browser_test_support",
    "//chromeos/constants",
  ]
}

source_set("browser_test_support") {
  testonly = true
  sources = [
    "test/media_app_ui_browsertest.cc",
    "test/media_app_ui_browsertest.h",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":media_app_ui",
    "//chrome/test:test_support_ui",
    "//chromeos/components/web_applications/test:test_support",
  ]

  data = [
    "test/driver.js",
    "test/driver_api.js",
    "test/dom_testing_helpers.js",
    "test/guest_query_receiver.js",
    "test/media_app_guest_ui_browsertest.js",
  ]
}

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_browsertests",
    ":closure_compile_test_lib",
    "resources/js:closure_compile",
    "resources/mock/js:closure_compile",
  ]
}

mojom("mojo_bindings") {
  sources = [ "media_app_ui.mojom" ]
}

media_test_lib_closure_flags = system_app_closure_flags_strict + [ "hide_warnings_for=chromeos/components/media_app_ui/media_app_ui.mojom-lite-for-compile.js" ]

# Use relaxed flags for the browsertest files themselves. This removes null
# checks and "could not determine type" errors which don't add a lot of value.
media_browsertest_closure_flags = system_app_closure_flags + [ "hide_warnings_for=chromeos/components/media_app_ui/media_app_ui.mojom-lite-for-compile.js" ]

js_type_check("closure_compile_test_lib") {
  testonly = true
  closure_flags = media_test_lib_closure_flags
  deps = [
    ":test_driver_api_js",
    ":test_driver_js",
    ":test_guest_query_receiver_js",
  ]
}

js_type_check("closure_compile_browsertests") {
  testonly = true
  closure_flags = media_browsertest_closure_flags
  deps = [
    ":test_media_app_guest_ui_browsertest_js",
    ":test_media_app_ui_browsertest_js",
  ]
}

js_library("test_driver_api_js") {
  testonly = true
  externs_list = [
    "//chromeos/components/web_applications/externs/file_handling.externs.js",
  ]
  sources = [ "test/driver_api.js" ]
}

js_library("test_dom_testing_helpers_js") {
  testonly = true
  sources = [ "test/dom_testing_helpers.js" ]
}

js_library("test_guest_query_receiver_js") {
  testonly = true
  sources = [ "test/guest_query_receiver.js" ]
  deps = [
    ":test_dom_testing_helpers_js",
    ":test_driver_api_js",
    "//chromeos/components/media_app_ui/resources/js:receiver",
    "//chromeos/components/system_apps/public/js:message_pipe",
  ]
}

js_library("test_driver_js") {
  testonly = true
  sources = [ "test/driver.js" ]
  externs_list = [ "//third_party/chaijs/externs/chai-3.5.js" ]
  deps = [
    ":test_driver_api_js",
    "//chromeos/components/media_app_ui/resources/js:launch",
    "//chromeos/components/system_apps/public/js:message_pipe",
  ]
}

js_library("test_media_app_ui_browsertest_js") {
  testonly = true
  sources = [ "test/media_app_ui_browsertest.js" ]
  externs_list =
      [ "//chromeos/components/web_applications/js2gtest_support.externs.js" ]
  deps = [
    ":test_driver_js",
    "//chromeos/components/media_app_ui/resources/js:launch",
    "//chromeos/components/system_apps/public/js:message_pipe",
  ]
}

js_library("test_media_app_guest_ui_browsertest_js") {
  testonly = true
  sources = [ "test/media_app_guest_ui_browsertest.js" ]
  externs_list =
      [ "//chromeos/components/web_applications/js2gtest_support.externs.js" ]
  deps = [
    ":test_driver_js",
    "//chromeos/components/system_apps/public/js:message_pipe",
  ]
}

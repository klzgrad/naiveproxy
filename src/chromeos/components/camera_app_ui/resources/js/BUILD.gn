# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/components/camera_app_ui/resources/images/images.gni")
import("//chromeos/components/camera_app_ui/resources/js/js.gni")
import("//third_party/closure_compiler/compile_js.gni")

js_type_check("closure_compile") {
  closure_flags =
      default_closure_args + [
        "language_in=ECMASCRIPT_2019",
        "jscomp_error=strictCheckTypes",
        "conformance_configs=" +
            rebase_path("externs/conformance_config.textproto", root_build_dir),
        "jscomp_error=conformanceViolations",
        "hide_warnings_for=mojo/public/js/",
        "hide_warnings_for=gen/",
        "hide_warnings_for=js/lib/",

        # For all js module in source tree.
        "js_module_root=" + rebase_path(".", root_build_dir),

        # For all dynamic generated js module.
        "js_module_root=" + rebase_path(target_gen_dir, root_build_dir),
      ]
  deps = [ ":compile_resources" ]
}

action("gen_preload_images_js") {
  script = "../utils/gen_preload_images_js.py"
  inputs = []
  foreach(image, in_app_images) {
    inputs += [ "../images/$image" ]
  }

  outputs = [ "$target_gen_dir/preload_images.js" ]
  response_file_contents = inputs
  args = [
    "--output_file",
    rebase_path(outputs[0], root_build_dir),
    "--images_list_file={{response_file_name}}",
  ]
}

js_library("preload_images") {
  extra_deps = [ ":gen_preload_images_js" ]
  sources = [ "$target_gen_dir/preload_images.js" ]
  deps = []
}

js_library("compile_resources") {
  sources = compile_js_files

  deps = [
    ":preload_images",
    "//chromeos/components/camera_app_ui:mojo_bindings_js_library_for_compile",
    "//components/arc/mojom:camera_intent_js_library_for_compile",
    "//media/capture/mojom:image_capture_js_library_for_compile",
    "//media/capture/video/chromeos/mojom:cros_camera_js_library_for_compile",
    "//third_party/blink/public/mojom:mojom_platform_js_library_for_compile",
  ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/file_manager_private.js",
    "$externs_path/file_system_provider.js",
    "$externs_path/metrics_private.js",

    # TODO(crbug.com/980846): Remove it once it is upstreamed to the closure
    # compiler.
    "//chromeos/components/web_applications/externs/file_handling.externs.js",
  ]
  externs_list += externs_files
}

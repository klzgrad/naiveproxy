# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/components/camera_app_ui/resources/css/css.gni")
import("//chromeos/components/camera_app_ui/resources/images/images.gni")
import("//chromeos/components/camera_app_ui/resources/js/js.gni")
import("//chromeos/components/camera_app_ui/resources/sounds/sounds.gni")
import("//chromeos/components/camera_app_ui/resources/views/views.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos, "Camera App is Chrome OS only")

static_library("camera_app_ui") {
  sources = [
    "camera_app_helper_impl.cc",
    "camera_app_helper_impl.h",
    "camera_app_ui.cc",
    "camera_app_ui.h",
    "camera_app_ui_delegate.h",
    "camera_app_window_manager.cc",
    "camera_app_window_manager.h",
    "camera_app_window_state_controller.cc",
    "camera_app_window_state_controller.h",
    "resources.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  public_deps = [
    "resources/strings",
    "//chromeos/resources:camera_app_resources",
  ]

  deps = [
    ":mojo_bindings",
    ":mojo_bindings_js",
    "//ash/public/cpp",
    "//chromeos/components/web_applications",
    "//chromeos/strings",
    "//chromeos/system",
    "//components/arc",
    "//content/public/browser",
    "//content/public/common",
    "//media/capture:capture_lib",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/platform",
    "//mojo/public/js:resources_grit",
    "//net",
    "//services/network/public/mojom",
    "//ui/views",
    "//ui/webui",
  ]
}

group("closure_compile") {
  testonly = true
  deps = [ "resources/js:closure_compile" ]
}

mojom("mojo_bindings") {
  sources = [ "camera_app_helper.mojom" ]

  deps = [ "//components/arc/mojom:camera_intent" ]
}

cca_grd_prefix = "chromeos_camera_app"
resources_grd_file = "$target_gen_dir/${cca_grd_prefix}_resources.grd"

mojo_grdp_file = "$target_gen_dir/mojo.grdp"
generated_grdp_file = "$target_gen_dir/generated.grdp"

generate_grd("build_grd") {
  deps = [
    ":build_generated_grdp",
    ":build_mojo_grdp",
  ]

  grd_prefix = cca_grd_prefix
  out_grd = resources_grd_file
  grdp_files = [
    mojo_grdp_file,
    generated_grdp_file,
  ]
  input_files_base_dir = rebase_path("resources", "//")
  input_files = []

  # CSS Files.
  foreach(css, css_files) {
    input_files += [ "css/$css" ]
  }

  # View Files.
  foreach(view, view_files) {
    input_files += [ "views/$view" ]
  }

  # Image Files.
  foreach(image, in_app_images) {
    input_files += [ "images/$image" ]
  }
  foreach(image, icon_images) {
    input_files += [ "images/$image" ]
  }

  # JS Files.
  foreach(js, compile_js_files) {
    input_files += [ "js/$js" ]
  }
  foreach(js, no_compile_js_files) {
    input_files += [ "js/$js" ]
  }
  foreach(wasm, wasm_files) {
    input_files += [ "js/$wasm" ]
  }

  # Sound Files.
  foreach(sound, sound_files) {
    input_files += [ "sounds/$sound" ]
  }

  # Other Files.
  input_files += [
    # We put test.html outside of views/ directory to avoid test page and the
    # main window sharing the same shelf icon.
    "test/test.html",
  ]
}

generate_grd("build_mojo_grdp") {
  deps = [
    "//chromeos/components/camera_app_ui:mojo_bindings_js",
    "//components/arc/mojom:camera_intent_js",
    "//media/capture/mojom:image_capture_js",
    "//media/capture/video/chromeos/mojom:cros_camera_js",
    "//mojo/public/js:bindings_lite",
    "//mojo/public/mojom/base:base_js",
    "//third_party/blink/public/mojom:mojom_platform_js",
    "//ui/gfx/geometry/mojom:mojom_js",
    "//ui/gfx/range/mojom:mojom_js",
  ]

  grd_prefix = "chromeos_camera_app_js_mojo"
  input_files_base_dir = rebase_path("$root_gen_dir/", root_build_dir)

  input_mappings = [
    {
      path = "chromeos/components/camera_app_ui/camera_app_helper.mojom-lite.js"
      rewrite_path = "js/mojo/camera_app_helper.mojom-lite.js"
    },
    {
      path = "components/arc/mojom/camera_intent.mojom-lite.js"
      rewrite_path = "js/mojo/camera_intent.mojom-lite.js"
    },
    {
      path = "media/capture/mojom/image_capture.mojom-lite.js"
      rewrite_path = "js/mojo/image_capture.mojom-lite.js"
    },
    {
      path = "media/capture/video/chromeos/mojom/camera_app.mojom-lite.js"
      rewrite_path = "js/mojo/camera_app.mojom-lite.js"
    },
    {
      path = "media/capture/video/chromeos/mojom/camera_common.mojom-lite.js"
      rewrite_path = "js/mojo/camera_common.mojom-lite.js"
    },
    {
      path = "media/capture/video/chromeos/mojom/camera_metadata.mojom-lite.js"
      rewrite_path = "js/mojo/camera_metadata.mojom-lite.js"
    },
    {
      path = "media/capture/video/chromeos/mojom/camera_metadata_tags.mojom-lite.js"
      rewrite_path = "js/mojo/camera_metadata_tags.mojom-lite.js"
    },
    {
      path = "mojo/public/js/mojo_bindings_lite.js"
      rewrite_path = "js/mojo/mojo_bindings_lite.js"
    },
    {
      path = "mojo/public/mojom/base/time.mojom-lite.js"
      rewrite_path = "js/mojo/time.mojom-lite.js"
    },
    {
      path = "ui/gfx/geometry/mojom/geometry.mojom-lite.js"
      rewrite_path = "js/mojo/geometry.mojom-lite.js"
    },
    {
      path = "ui/gfx/range/mojom/range.mojom-lite.js"
      rewrite_path = "js/mojo/range.mojom-lite.js"
    },
    {
      path = "third_party/blink/public/mojom/idle/idle_manager.mojom-lite.js"
      rewrite_path = "js/mojo/idle_manager.mojom-lite.js"
    },
  ]

  input_files = []
  resource_path_rewrites = []
  foreach(input, input_mappings) {
    input_files += [ "${input.path}" ]
    resource_path_rewrites += [ "${input.path}|${input.rewrite_path}" ]
  }

  out_grd = mojo_grdp_file
}

generate_grd("build_generated_grdp") {
  deps = [
    "//chromeos/components/camera_app_ui/resources/js:gen_preload_images_js",
  ]
  grd_prefix = "chromeos_camera_app_js"
  input_files_base_dir =
      rebase_path("$root_gen_dir/chromeos/components/camera_app_ui/resources",
                  root_build_dir)
  input_files = [ "js/preload_images.js" ]
  out_grd = generated_grdp_file
}

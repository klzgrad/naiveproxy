# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "secure_channel.mojom" ]

  public_deps = [
    "//chromeos/components/multidevice/mojom",
    "//mojo/public/mojom/base",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chromeos.secure_channel.mojom.ConnectionMedium"
          cpp = "::chromeos::secure_channel::ConnectionMedium"
        },
        {
          mojom = "chromeos.secure_channel.mojom.ConnectionPriority"
          cpp = "::chromeos::secure_channel::ConnectionPriority"
        },
      ]

      traits_headers = [ "secure_channel_mojom_traits.h" ]
      traits_sources = [ "secure_channel_mojom_traits.cc" ]
      traits_public_deps =
          [ "//chromeos/services/secure_channel/public/cpp/shared" ]
    },
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "secure_channel_mojom_traits_unittest.cc" ]

  deps = [
    ":mojom",
    "//base",
    "//base/test:test_support",
    "//chromeos/services/secure_channel/public/cpp/shared",
    "//mojo/public/cpp/test_support:test_utils",
    "//testing/gtest",
  ]
}

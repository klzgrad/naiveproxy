# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [
    "account_manager.mojom",
    "bitmap.mojom",
    "crosapi.mojom",
    "feedback.mojom",
    "keystore_service.mojom",
    "message_center.mojom",
    "notification.mojom",
    "screen_manager.mojom",
    "select_file.mojom",
  ]
  disable_variants = true

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "crosapi.mojom.Bitmap"
          cpp = "crosapi::Bitmap"
          move_only = true
        },
      ]
      traits_headers = [ "//chromeos/crosapi/mojom/bitmap_mojom_traits.h" ]
      traits_public_deps = [
        ":mojom_traits",
        "//chromeos/crosapi/cpp",
      ]
    },
  ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//services/device/public/mojom:mojom",
    "//ui/gfx/image/mojom",
    "//url/mojom:url_mojom_gurl",
  ]
}

component("mojom_traits") {
  output_name = "crosapi_mojom_traits"

  sources = [
    "bitmap_mojom_traits.cc",
    "bitmap_mojom_traits.h",
  ]

  defines = [ "IS_CROSAPI_MOJOM_TRAITS_IMPL" ]

  public_deps = [
    ":mojom_shared",
    "//chromeos/crosapi/cpp",
    "//mojo/public/cpp/base:shared_typemap_traits",
  ]
}

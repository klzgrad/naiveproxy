# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

assert(is_chromeos, "Non-Chrome-OS builds cannot depend on //chromeos")

component("lorgnette_manager") {
  defines = [ "IS_LORGNETTE_MANAGER_IMPL" ]

  public_deps = [ ":lorgnette_proto" ]

  deps = [
    "//base",
    "//chromeos/dbus:common",
    "//dbus",
  ]

  sources = [
    "fake_lorgnette_manager_client.cc",
    "fake_lorgnette_manager_client.h",
    "lorgnette_manager_client.cc",
    "lorgnette_manager_client.h",
  ]
}

source_set("test_support") {
  testonly = true
  public_deps = [ ":lorgnette_manager" ]
  deps = [
    ":lorgnette_proto",
    "//base",
    "//base/test:test_support",
    "//dbus:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
  sources = [ "lorgnette_manager_client_unittest.cc" ]
}

proto_library("lorgnette_proto") {
  sources =
      [ "//third_party/cros_system_api/dbus/lorgnette/lorgnette_service.proto" ]

  proto_out_dir = "chromeos/dbus/lorgnette"
}

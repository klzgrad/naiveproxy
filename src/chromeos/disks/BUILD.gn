# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

assert(is_chromeos, "Non-Chrome-OS builds must not depend on //chromeos")

component("disks") {
  defines = [ "IS_CHROMEOS_DISKS_IMPL" ]
  deps = [
    "//base",
    "//chromeos/constants",
    "//chromeos/dbus",
    "//chromeos/dbus/power",
    "//chromeos/dbus/power:power_manager_proto",
  ]
  sources = [
    "disk.cc",
    "disk.h",
    "disk_mount_manager.cc",
    "disk_mount_manager.h",
    "suspend_unmount_manager.cc",
    "suspend_unmount_manager.h",
  ]
}

source_set("test_support") {
  testonly = true
  public_deps = [
    ":disks",
  ]
  deps = [
    "//base",
    "//base/test:test_support",
    "//chromeos/dbus:test_support",
    "//chromeos/dbus/power:power_manager_proto",
    "//testing/gmock",
    "//testing/gtest",
  ]
  sources = [
    "mock_disk_mount_manager.cc",
    "mock_disk_mount_manager.h",
  ]
}

source_set("unit_tests") {
  testonly = true
  deps = [
    ":disks",
    ":test_support",
    "//base/test:test_support",
    "//chromeos/constants",
    "//chromeos/dbus:test_support",
    "//chromeos/dbus/power",
    "//chromeos/dbus/power:power_manager_proto",
    "//testing/gmock",
    "//testing/gtest",
  ]
  sources = [
    "disk_mount_manager_unittest.cc",
    "disk_unittest.cc",
    "suspend_unmount_manager_unittest.cc",
  ]
}

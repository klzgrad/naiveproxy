# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

assert(is_win)

config("delay_load_mf") {
  ldflags = [
    "/DELAYLOAD:mf.dll",
    "/DELAYLOAD:mfplat.dll",
    "/DELAYLOAD:mfreadwrite.dll",
  ]
}

component("media_foundation_util") {
  defines = [ "MF_INITIALIZER_IMPLEMENTATION" ]
  set_sources_assignment_filter([])
  sources = [
    "mf_helpers.cc",
    "mf_helpers.h",
    "mf_initializer.cc",
    "mf_initializer.h",
    "mf_initializer_export.h",
  ]
  set_sources_assignment_filter(deprecated_default_sources_assignment_filter)
  configs += [
    # TODO(crbug.com/167187): Fix size_t to int truncations.
    "//build/config/compiler:no_size_t_to_int_warning",
    "//media:media_config",
  ]
  deps = [
    "//base",
    "//media:shared_memory_support",
  ]
  libs = [
    "mf.lib",
    "mfplat.lib",
    "mfreadwrite.lib",
  ]

  # MediaFoundation is not available on Windows N, so must be delay loaded.
  all_dependent_configs = [ ":delay_load_mf" ]
}

source_set("hresult_status_helper") {
  sources = [
    "hresult_status_helper.cc",
    "hresult_status_helper.h",
  ]
  deps = [ "//media" ]
}

source_set("mf_cdm_proxy") {
  sources = [ "mf_cdm_proxy.h" ]
  deps = [ "//base" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "d3d11_mocks.cc",
    "d3d11_mocks.h",
    "mf_mocks.cc",
    "mf_mocks.h",
    "test_utils.h",
  ]
  deps = [
    "//base",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

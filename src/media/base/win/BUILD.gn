# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")

assert(is_win)

config("delay_load_mf") {
  ldflags = [
    "/DELAYLOAD:mf.dll",
    "/DELAYLOAD:mfplat.dll",
    "/DELAYLOAD:mfreadwrite.dll",
  ]
}

jumbo_component("media_foundation_util") {
  defines = [ "MF_INITIALIZER_IMPLEMENTATION" ]
  set_sources_assignment_filter([])
  sources = [
    "mf_helpers.cc",
    "mf_helpers.h",
    "mf_initializer.cc",
    "mf_initializer.h",
    "mf_initializer_export.h",
  ]
  set_sources_assignment_filter(sources_assignment_filter)
  configs += [
    # TODO(crbug.com/167187): Fix size_t to int truncations.
    "//build/config/compiler:no_size_t_to_int_warning",
    "//media:media_config",
  ]
  deps = [
    "//base",
    "//media:shared_memory_support",
  ]
  libs = [
    "mf.lib",
    "mfplat.lib",
    "mfreadwrite.lib",
  ]

  # MediaFoundation is not available on Windows N, so must be delay loaded.
  all_dependent_configs = [ ":delay_load_mf" ]
}

source_set("d3d11") {
  sources = [ "d3d11_create_device_cb.h" ]
  deps = [ "//base" ]
}

source_set("d3d11_test_support") {
  testonly = true
  sources = [
    "d3d11_mocks.cc",
    "d3d11_mocks.h",
  ]
  deps = [
    ":d3d11",
    "//base",
    "//testing/gmock",
  ]
}

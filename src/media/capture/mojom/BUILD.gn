# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("video_capture") {
  generate_java = true
  sources = [
    "video_capture.mojom",
    "video_capture_types.mojom",
  ]

  public_deps = [
    "//gpu/ipc/common:interfaces",
    "//mojo/public/mojom/base",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]

  export_class_attribute_blink = "BLINK_PLATFORM_EXPORT"
  export_define_blink = "BLINK_PLATFORM_IMPLEMENTATION=1"
  export_header_blink = "third_party/blink/public/platform/web_common.h"
}

component("video_capture_mojom_support") {
  sources = [
    "video_capture_types_mojom_traits.cc",
    "video_capture_types_mojom_traits.h",
  ]
  public_deps = [
    ":video_capture_shared_cpp_sources",
    "//media/base/ipc",
    "//media/capture:capture_base",
  ]
  deps = [
    "//media",
    "//media/mojo/mojom",
    "//ui/gfx/geometry/mojom:mojom_traits",
  ]
  defines = [ "IS_MEDIA_CAPTURE_MOJOM_TRAITS_IMPL" ]
}

mojom("image_capture") {
  sources = [ "image_capture.mojom" ]

  # ImageCapture Mojom interfaces are exposed publicly to layout tests which use
  # prepackaged redistributable JS bindings. It is therefore not desirable to
  # scramble these messages.
  scramble_message_ids = false
}

source_set("image_capture_types") {
  sources = [
    "image_capture_types.cc",
    "image_capture_types.h",
  ]

  deps = [ ":image_capture" ]
}

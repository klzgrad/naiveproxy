# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ppapi/buildflags/buildflags.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

# See //content/BUILD.gn for how this works.
group("child") {
  if (is_component_build) {
    public_deps = [ "//content" ]
  } else {
    public_deps = [ ":child_sources" ]
    if (!is_android) {
      public_deps += [ "//content/gpu" ]
    }
    if (enable_plugins) {
      public_deps += [ "//content/ppapi_plugin" ]
    }
  }
}

source_set("child_sources") {
  # External code should depend in via ":child" above.
  visibility = [ "//content/*" ]

  sources = [
    "child_thread.h",
    "dwrite_font_proxy_init_win.h",
    "image_decoder_utils.h",
  ]

  configs += [
    "//build/config/compiler:wexit_time_destructors",
    "//content:content_implementation",
  ]

  deps = [
    "//content/child",  # Must not be public_deps!
    "//content/public/common:common_sources",
    "//gin",
    "//ppapi/c",
  ]

  allow_circular_includes_from = [
    # This target is a pair with content/child. They always go together and
    # include headers from each other.
    "//content/child",
  ]
}

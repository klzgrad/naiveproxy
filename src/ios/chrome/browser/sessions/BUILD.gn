# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("sessions") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "ios_chrome_session_tab_helper.h",
    "ios_chrome_session_tab_helper.mm",
    "ios_chrome_tab_restore_service_client.h",
    "ios_chrome_tab_restore_service_client.mm",
    "ios_chrome_tab_restore_service_factory.cc",
    "ios_chrome_tab_restore_service_factory.h",
    "tab_restore_service_delegate_impl_ios.h",
    "tab_restore_service_delegate_impl_ios.mm",
    "tab_restore_service_delegate_impl_ios_factory.h",
    "tab_restore_service_delegate_impl_ios_factory.mm",
  ]
  deps = [
    ":serialisation",
    "//base",
    "//components/keyed_service/core",
    "//components/keyed_service/ios",
    "//components/sessions",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/web_state_list",
    "//ios/public/provider/chrome/browser",
    "//ios/web",
    "//net",
    "//url",
  ]
  libs = [ "UIKit.framework" ]
}

source_set("serialisation") {
  sources = [
    "NSCoder+Compatibility.h",
    "NSCoder+Compatibility.mm",
    "session_ios.h",
    "session_ios.mm",
    "session_service_ios.h",
    "session_service_ios.mm",
    "session_util.h",
    "session_util.mm",
    "session_window_ios.h",
    "session_window_ios.mm",
    "session_window_restoring.h",
  ]
  deps = [
    "//base",
    "//components/sessions",
    "//ios/chrome/browser/browser_state",
    "//ios/web",
    "//ios/web/public/session",
  ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("test_support") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "test_session_service.h",
    "test_session_service.mm",
  ]
  deps = [
    ":serialisation",
    "//base",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "session_service_ios_unittest.mm",
    "session_window_ios_unittest.mm",
  ]
  deps = [
    ":resources_unit_tests",
    ":serialisation",
    ":sessions",
    "//base",
    "//base/test:test_support",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/web",
    "//ios/web/public/session",
    "//testing/gtest",
    "//third_party/ocmock",
  ]
  libs = [ "Foundation.framework" ]
}

bundle_data("resources_unit_tests") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [
    "//ios/chrome/test/data/sessions/corrupted.plist",
    "//ios/chrome/test/data/sessions/session_m57.plist",
    "//ios/chrome/test/data/sessions/session_m58.plist",
  ]
  outputs = [
    "{{bundle_resources_dir}}/ios/chrome/test/data/sessions/{{source_file_part}}",
  ]
}

# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("fullscreen") {
  sources = [
    "animated_scoped_fullscreen_disabler.h",
    "animated_scoped_fullscreen_disabler.mm",
    "fullscreen_controller.h",
    "fullscreen_controller_observer.h",
    "fullscreen_ui_updater.h",
    "scoped_fullscreen_disabler.h",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  allow_circular_includes_from = [ ":internal" ]

  deps = [
    ":internal",
    ":ui",
    "//base",
    "//components/keyed_service/core",
    "//components/keyed_service/ios",
    "//ios/chrome/browser/ui/fullscreen:feature_flags",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/common",
  ]
}

source_set("feature_flags") {
  sources = [
    "fullscreen_features.h",
    "fullscreen_features.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    "//base",
    "//components/flags_ui",
  ]
}

source_set("coordinators") {
  sources = [
    "chrome_coordinator+fullscreen_disabling.h",
    "chrome_coordinator+fullscreen_disabling.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":fullscreen",
    "//base",
    "//ios/chrome/browser/ui/coordinators:chrome_coordinators",
    "//ios/chrome/browser/ui/fullscreen:feature_flags",
  ]
}

source_set("internal") {
  sources = [
    "fullscreen_content_adjustment_util.h",
    "fullscreen_content_adjustment_util.mm",
    "fullscreen_controller_impl.h",
    "fullscreen_controller_impl.mm",
    "fullscreen_mediator.h",
    "fullscreen_mediator.mm",
    "fullscreen_model.h",
    "fullscreen_model.mm",
    "fullscreen_model_observer.h",
    "fullscreen_system_notification_observer.h",
    "fullscreen_system_notification_observer.mm",
    "fullscreen_ui_updater.mm",
    "fullscreen_web_state_list_observer.h",
    "fullscreen_web_state_list_observer.mm",
    "fullscreen_web_state_observer.h",
    "fullscreen_web_state_observer.mm",
    "fullscreen_web_view_proxy_observer.h",
    "fullscreen_web_view_proxy_observer.mm",
    "fullscreen_web_view_resizer.h",
    "fullscreen_web_view_resizer.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":feature_flags",
    ":ui",
    "//base",
    "//components/keyed_service/ios",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/ui/broadcaster",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/common:timing",
    "//ios/public/provider/chrome/browser",
    "//ios/public/provider/chrome/browser/ui",
    "//ios/web",
    "//ios/web/common",
    "//ios/web/public/security",
    "//ui/gfx/geometry",
  ]
}

source_set("ui") {
  sources = [
    "fullscreen_animator.h",
    "fullscreen_animator.mm",
    "fullscreen_ui_element.h",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    "//base",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/common:timing",
    "//ui/gfx/geometry",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "fullscreen_mediator_unittest.mm",
    "fullscreen_model_unittest.mm",
    "fullscreen_ui_updater_unittest.mm",
    "fullscreen_web_state_list_observer_unittest.mm",
    "fullscreen_web_state_observer_unittest.mm",
    "fullscreen_web_view_resizer_unittest.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":fullscreen",
    ":internal",
    ":ui",
    "//base/test:test_support",
    "//ios/chrome/browser/ui:feature_flags",
    "//ios/chrome/browser/ui/broadcaster",
    "//ios/chrome/browser/ui/fullscreen/test",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/browser/web_state_list:test_support",
    "//ios/web/common",
    "//ios/web/public",
    "//ios/web/public/security",
    "//ios/web/public/test/fakes",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/ocmock",
  ]
}

source_set("eg_tests") {
  defines = [ "CHROME_EARL_GREY_1" ]
  testonly = true
  sources = [ "fullscreen_egtest.mm" ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":fullscreen",
    "//base",
    "//base/test:test_support",
    "//ios/chrome/browser/ui/fullscreen/test:eg_test_support",
    "//ios/chrome/browser/ui/side_swipe",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/test:eg_test_support",
    "//ios/chrome/test/app:test_support",
    "//ios/chrome/test/earl_grey:test_support",
    "//ios/testing/earl_grey:earl_grey_support",
    "//ios/web:earl_grey_test_support",
    "//ios/web/common",
    "//ios/web/public",
    "//ios/web/public/test",
    "//ios/web/public/test/http_server",
  ]
  libs = [ "XCTest.framework" ]
}

source_set("eg2_tests") {
  defines = [ "CHROME_EARL_GREY_2" ]
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true
  sources = [ "fullscreen_egtest.mm" ]
  deps = [
    "//base",
    "//base/test:test_support",
    "//ios/chrome/browser/ui/fullscreen/test:eg_test_support+eg2",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/test:eg_test_support+eg2",
    "//ios/chrome/test/earl_grey:eg_test_support+eg2",
    "//ios/testing/earl_grey:eg_test_support+eg2",
    "//ios/third_party/earl_grey2:test_lib",
    "//ios/web/common",
    "//ios/web/public/test/http_server",
    "//url",
  ]
  libs = [ "UIKit.framework" ]
}

# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/chrome_build.gni")

source_set("scene_testing") {
  sources = [ "scene_controller_testing.h" ]
  deps = [ "//ios/chrome/browser:utils" ]
  frameworks = [ "UIKit.framework" ]
}

source_set("scene_state_observer") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [ "scene_state_observer.h" ]

  frameworks = [ "UIKit.framework" ]
}

source_set("observing_scene_agent") {
  configs += [ "//build/config/compiler:enable_arc" ]

  sources = [
    "observing_scene_state_agent.h",
    "observing_scene_state_agent.mm",
  ]

  frameworks = [ "UIKit.framework" ]

  public_deps = [
    ":scene_state_header",
    ":scene_state_observer",
  ]
}

source_set("scene_state_header") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "scene_state.h",
    "scene_state_browser_agent.h",
  ]
  public_deps = [
    ":scene_state_observer",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/ui/scoped_ui_blocker",
    "//ios/chrome/browser/window_activities",
  ]
}

source_set("incognito_blocker_scene_agent") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "incognito_blocker_scene_agent.h",
    "incognito_blocker_scene_agent.mm",
  ]
  deps = [
    ":scene_state_header",
    "//base",
    "//ios/chrome/browser/ui/util:multiwindow_util",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("scene") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "connection_information.h",
    "default_browser_scene_agent.h",
    "default_browser_scene_agent.mm",
    "scene_controller.h",
    "scene_controller.mm",
    "scene_delegate.h",
    "scene_delegate.mm",
    "scene_state.mm",
    "ui_blocker_scene_agent.h",
    "ui_blocker_scene_agent.mm",
  ]

  deps = [
    ":incognito_blocker_scene_agent",
    ":main",
    ":observing_scene_agent",
    ":scene_testing",
    "//base",
    "//components/infobars/core",
    "//components/previous_session_info",
    "//components/signin/public/identity_manager",
    "//components/url_formatter",
    "//components/web_resource",
    "//ios/chrome/app:app",
    "//ios/chrome/app:blocking_scene_commands",
    "//ios/chrome/app:mode",
    "//ios/chrome/app:tests_hook",
    "//ios/chrome/app/application_delegate:app_state_header",
    "//ios/chrome/app/application_delegate:tab_opening",
    "//ios/chrome/app/application_delegate:url_opener_params",
    "//ios/chrome/app/strings:ios_strings_grit",
    "//ios/chrome/browser",
    "//ios/chrome/browser:chrome_url_constants",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browsing_data",
    "//ios/chrome/browser/crash_report",
    "//ios/chrome/browser/crash_report:crash_report_internal",
    "//ios/chrome/browser/crash_report/breadcrumbs",
    "//ios/chrome/browser/crash_report/breadcrumbs:feature_flags",
    "//ios/chrome/browser/first_run",
    "//ios/chrome/browser/geolocation:geolocation_internal",
    "//ios/chrome/browser/infobars",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/ntp:features",
    "//ios/chrome/browser/policy",
    "//ios/chrome/browser/screenshot",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/snapshots",
    "//ios/chrome/browser/ui:feature_flags",
    "//ios/chrome/browser/ui/appearance",
    "//ios/chrome/browser/ui/authentication",
    "//ios/chrome/browser/ui/authentication/signin",
    "//ios/chrome/browser/ui/blocking_overlay",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/browser/ui/commands:commands",
    "//ios/chrome/browser/ui/first_run",
    "//ios/chrome/browser/ui/first_run:field_trial",
    "//ios/chrome/browser/ui/history",
    "//ios/chrome/browser/ui/incognito_reauth:incognito_reauth_scene_agent",
    "//ios/chrome/browser/ui/settings:settings_root",
    "//ios/chrome/browser/ui/settings/sync",
    "//ios/chrome/browser/ui/tab_switcher/tab_grid",
    "//ios/chrome/browser/ui/thumb_strip:feature_flags",
    "//ios/chrome/browser/ui/toolbar/public",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/browser/ui/util:multiwindow_util",
    "//ios/chrome/browser/ui/whats_new:utils",
    "//ios/chrome/browser/url_loading",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/browser/web_state_list:agents",
    "//ios/chrome/browser/web_state_list:session_metrics",
    "//ios/chrome/browser/window_activities",
    "//ios/chrome/common/ui/reauthentication",
    "//ios/chrome/common/ui/util",
    "//ios/public/provider/chrome/browser",
    "//ios/public/provider/chrome/browser/mailto",
    "//ios/public/provider/chrome/browser/signin",
    "//ios/public/provider/chrome/browser/user_feedback",
    "//ui/base:base",
  ]
  public_deps = [
    ":scene_state_header",
    ":scene_state_observer",
  ]
  allow_circular_includes_from = [
    ":main",

    # TODO(crbug.com/1103553): remove circular dependency.
    "//ios/chrome/browser/ui/settings/sync",
  ]

  frameworks = [ "UIKit.framework" ]
}

source_set("browser_interface_provider") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [ "browser_interface_provider.h" ]
  deps = [ "//base" ]
  frameworks = [ "UIKit.framework" ]
}

source_set("main") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "browser_view_wrangler.h",
    "browser_view_wrangler.mm",
    "bvc_container_view_controller.h",
    "bvc_container_view_controller.mm",
    "scene_state_browser_agent.mm",
  ]
  deps = [
    "//base",
    "//components/translate/core/browser",
    "//ios/chrome/app:mode",
    "//ios/chrome/app/application_delegate:app_state_header",
    "//ios/chrome/app/resources:launchscreen_xib",
    "//ios/chrome/browser",
    "//ios/chrome/browser/app_launcher",
    "//ios/chrome/browser/autofill:autofill_internal",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browsing_data",
    "//ios/chrome/browser/crash_report:crash_report_internal",
    "//ios/chrome/browser/device_sharing",
    "//ios/chrome/browser/download",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/reading_list",
    "//ios/chrome/browser/sessions",
    "//ios/chrome/browser/sessions:restoration_agent",
    "//ios/chrome/browser/sessions:serialisation",
    "//ios/chrome/browser/sessions:session_service",
    "//ios/chrome/browser/snapshots",
    "//ios/chrome/browser/store_kit",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/tabs:tabs_internal",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/chrome/browser/ui/autofill",
    "//ios/chrome/browser/ui/browser_container",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/browser/ui/commands",
    "//ios/chrome/browser/ui/coordinators:chrome_coordinators",
    "//ios/chrome/browser/ui/download",
    "//ios/chrome/browser/ui/incognito_reauth:incognito_reauth_scene_agent",
    "//ios/chrome/browser/ui/page_info:coordinator",
    "//ios/chrome/browser/ui/print",
    "//ios/chrome/browser/ui/qr_scanner:coordinator",
    "//ios/chrome/browser/ui/reading_list",
    "//ios/chrome/browser/ui/recent_tabs",
    "//ios/chrome/browser/ui/settings/sync",
    "//ios/chrome/browser/ui/snackbar",
    "//ios/chrome/browser/ui/thumb_strip:feature_flags",
    "//ios/chrome/browser/ui/translate:legacy_translate",
    "//ios/chrome/browser/ui/util:multiwindow_util",
    "//ios/chrome/browser/url_loading:url_loading_params_header",
    "//ios/chrome/browser/web",
    "//ios/chrome/browser/web:tab_helper_delegates",
    "//ios/chrome/browser/web:web_internal",
    "//ios/chrome/browser/web_state_list",
    "//ios/public/provider/chrome/browser",
    "//net",
  ]

  if (ios_enable_multi_window || ios_enable_scene_startup) {
    deps += [ "//ios/chrome/app/resources:base_scene_storyboard" ]
  }

  public_deps = [
    ":browser_interface_provider",
    ":scene_state_header",
  ]

  # TODO(crbug.com/1103553): remove circular dependency.
  allow_circular_includes_from = [ "//ios/chrome/browser/ui/settings/sync" ]

  frameworks = [ "UIKit.framework" ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "browser_view_wrangler_unittest.mm",
    "incognito_blocker_scene_agent_unittest.mm",
    "scene_controller_unittest.mm",
    "scene_state_browser_agent_unittest.mm",
  ]
  deps = [
    ":incognito_blocker_scene_agent",
    ":main",
    ":scene",
    "//base",
    "//base/test:test_support",
    "//components/bookmarks/test",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app/startup",
    "//ios/chrome/browser/bookmarks",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/main:test_support",
    "//ios/chrome/browser/sessions:restoration_agent",
    "//ios/chrome/browser/sessions:test_support",
    "//ios/chrome/browser/sync",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/test:block_cleanup_test",
    "//ios/testing:block_swizzler",
    "//ios/web/public/test",
    "//testing/gtest",
    "//third_party/ocmock",
  ]
}

# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("dialogs") {
  deps = [
    "//base",
    "//ios/web",
  ]

  sources = [
    "java_script_dialog_blocking_state.h",
    "java_script_dialog_blocking_state.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("feature_flags") {
  sources = [
    "dialog_features.h",
    "dialog_features.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    "//base",
    "//components/flags_ui",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "java_script_dialog_blocking_state_unittest.mm" ]

  deps = [
    ":dialogs",
    "//ios/web",
    "//ios/web/public/test/fakes",
    "//testing/gtest",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("constants") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "dialog_constants.h",
    "dialog_constants.mm",
  ]
}

source_set("dialogs_internal") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "dialog_presenter.h",
    "dialog_presenter.mm",
    "java_script_dialog_metrics.cc",
    "java_script_dialog_metrics.h",
    "java_script_dialog_presenter_impl.h",
    "java_script_dialog_presenter_impl.mm",
    "nsurl_protection_space_util.h",
    "nsurl_protection_space_util.mm",
    "overlay_java_script_dialog_presenter.h",
    "overlay_java_script_dialog_presenter.mm",
  ]
  deps = [
    ":completion_block_util",
    ":constants",
    ":dialogs",
    "//base",
    "//components/strings",
    "//components/url_formatter",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/overlays",
    "//ios/chrome/browser/overlays/public/web_content_area",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/chrome/browser/ui/util",
    "//ios/web",
    "//ui/base",
    "//url",
  ]
  libs = [ "UIKit.framework" ]
}

source_set("completion_block_util") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "completion_block_util.h",
    "completion_block_util.mm",
  ]
  deps = [ "//base" ]
}

source_set("unit_tests_internal") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "completion_block_util_unittest.mm",
    "dialog_presenter_unittest.mm",
    "java_script_dialog_presenter_impl_unittest.mm",
    "nsurl_protection_space_util_unittest.mm",
    "overlay_java_script_dialog_presenter_unittest.mm",
  ]
  deps = [
    ":completion_block_util",
    ":dialogs_internal",
    "//base",
    "//components/strings",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/overlays",
    "//ios/chrome/browser/overlays/public/web_content_area",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/web",
    "//ios/web/public/test/fakes",
    "//testing/gtest",
    "//ui/base",
    "//url",
  ]
}

source_set("eg_tests") {
  defines = [ "CHROME_EARL_GREY_1" ]
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [ "javascript_dialog_egtest.mm" ]
  deps = [
    ":constants",
    ":feature_flags",
    "//base",
    "//base/test:test_support",
    "//components/strings",
    "//components/url_formatter",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/test:eg_test_support",
    "//ios/chrome/test/app:test_support",
    "//ios/chrome/test/earl_grey:test_support",
    "//ios/testing/earl_grey:earl_grey_support",
    "//ios/third_party/earl_grey:earl_grey+link",
    "//ios/web",
    "//ios/web:earl_grey_test_support",
    "//ios/web/public/test",
    "//ios/web/public/test:element_selector",
    "//ios/web/public/test/http_server",
    "//net:test_support",
    "//ui/base",
    "//url",
  ]
  libs = [
    "UIKit.framework",
    "XCTest.framework",
  ]
}

source_set("eg2_tests") {
  defines = [ "CHROME_EARL_GREY_2" ]
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true
  sources = [ "javascript_dialog_egtest.mm" ]
  deps = [
    ":constants",
    ":feature_flags",
    "//base",
    "//base/test:test_support",
    "//components/strings",
    "//components/url_formatter",
    "//ios/chrome/app/strings",
    "//ios/chrome/test:eg_test_support+eg2",
    "//ios/chrome/test/earl_grey:eg_test_support+eg2",
    "//ios/testing/earl_grey:eg_test_support+eg2",
    "//ios/third_party/earl_grey2:test_lib",
    "//ios/web/public/test:element_selector",
    "//ios/web/public/test/http_server",
    "//net:test_support",
    "//ui/base",
    "//url",
  ]
  libs = [ "UIKit.framework" ]
}

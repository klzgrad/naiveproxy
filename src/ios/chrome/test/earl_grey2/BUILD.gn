# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ios/ios_sdk.gni")
import("//build/config/ios/rules.gni")
import("//ios/chrome/test/earl_grey2/chrome_ios_eg2_test.gni")

group("all_tests") {
  testonly = true
  deps = [
    ":ios_chrome_eg2tests",
    ":ios_chrome_smoke_eg2tests_module",
    ":ios_chrome_ui_eg2tests_module",
    ":ios_chrome_web_eg2tests_module",
  ]
}

chrome_ios_eg2_test_app_host("ios_chrome_eg2tests") {
}

chrome_ios_eg2_test("ios_chrome_smoke_eg2tests_module") {
  xcode_test_application_name = "ios_chrome_eg2tests"

  deps = [
    "//ios/chrome/test/earl_grey2:eg2_tests",
  ]
}

chrome_ios_eg2_test("ios_chrome_web_eg2tests_module") {
  xcode_test_application_name = "ios_chrome_eg2tests"

  deps = [
    "//ios/chrome/browser/web:eg2_tests",
  ]
}

chrome_ios_eg2_test("ios_chrome_ui_eg2tests_module") {
  xcode_test_application_name = "ios_chrome_eg2tests"

  deps = [
    "//ios/chrome/browser/ui/activity_services:eg2_tests",
    "//ios/chrome/browser/ui/download:eg2_tests",
    "//ios/chrome/browser/ui/omnibox/popup:eg2_tests",
    "//ios/chrome/browser/ui/open_in:eg2_tests",
    "//ios/chrome/browser/ui/toolbar:eg2_tests",
  ]
}

source_set("shared_helper_headers") {
  testonly = true
  sources = [
    "//ios/chrome/test/earl_grey2/chrome_earl_grey_edo.h",
  ]
}

source_set("eg_app_support+eg2") {
  testonly = true
  configs += [ "//build/config/compiler:enable_arc" ]

  sources = [
    "chrome_earl_grey_edo.mm",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//ios/chrome/test/app:test_support",
    "//ios/testing/earl_grey:eg_app_support+eg2",
    "//ios/third_party/earl_grey2:app_framework+link",
  ]

  public_deps = [
    ":shared_helper_headers",
  ]
}

source_set("eg2_tests") {
  defines = [ "CHROME_EARL_GREY_2" ]
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true

  sources = [
    "smoke_egtest.mm",
  ]

  deps = [
    ":shared_helper_headers",
    "//ios/chrome/app/strings",
    "//ios/chrome/test/earl_grey:eg_test_support+eg2",
    "//ios/testing/earl_grey:eg_test_support+eg2",
    "//ios/third_party/earl_grey2:test_lib",
    "//ui/base",
  ]

  libs = [ "UIKit.framework" ]
}

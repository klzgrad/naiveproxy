# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ios/rules.gni")
import("//build/config/locales.gni")
import("//build/config/mac/base_rules.gni")
import("//build/mac/tweak_info_plist.gni")
import("//ios/build/chrome_build.gni")
import("//ios/build/config.gni")
import("//ios/chrome/extension_repack.gni")
import("//ios/chrome/tools/strings/generate_localizable_strings.gni")
import("//ios/public/provider/chrome/browser/build_config.gni")

group("resources") {
  deps = [ "//ios/chrome/credential_provider_extension/strings" ]
}

extension_repack_all_locales("packed_resources") {
  extension = "credential_provider_extension"
  input_locales = ios_packed_locales
  output_locales = ios_packed_locales_as_mac_outputs
}

tweak_info_plist("tweak_info_plist") {
  info_plist = "Info.plist"
}

compile_entitlements("entitlements") {
  substitutions = [ "IOS_BUNDLE_ID_PREFIX=$ios_app_bundle_id_prefix" ]
  output_name =
      "$target_gen_dir/credential_provider_extension.appex.entitlements"
  entitlements_templates = [
    "entitlements/external/credential_provider_extension.appex.entitlements",
  ]
  if (ios_chrome_cpe_entitlements_additions != []) {
    entitlements_templates += ios_chrome_cpe_entitlements_additions
  }
}

ios_appex_bundle("credential_provider_extension") {
  sources = [
    "credential_provider_view_controller.h",
    "credential_provider_view_controller.mm",
    "reauthentication_handler.h",
    "reauthentication_handler.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":system_strings",
    "//base",
    "//ios/chrome/common/app_group",
    "//ios/chrome/common/app_group:command",
    "//ios/chrome/common/ui/colors",
    "//ios/chrome/common/ui/reauthentication",
    "//ios/chrome/credential_provider_extension/resources",
    "//ios/chrome/credential_provider_extension/ui",
    ios_account_verification_provider_target,
  ]
  bundle_deps_filter = [ "//third_party/icu:icudata" ]

  assert_no_deps = ios_extension_assert_no_deps

  libs = [
    "AuthenticationServices.framework",
    "Foundation.framework",
    "UIKit.framework",
  ]

  extra_substitutions = [
    "CHROME_CHANNEL_SCHEME=$url_channel_scheme",
    "CHROMIUM_SHORT_NAME=$chromium_short_name",
    "CREDENTIAL_PROVIDER_EXTENSION_BUNDLE_ID=$chromium_bundle_id.CredentialProviderExtension",
  ]

  entitlements_target = ":entitlements"
  info_plist_target = ":tweak_info_plist"
}

generate_localizable_strings("system_strings") {
  visibility = [
    ":*",
    "//ios/showcase:*",
  ]
  _packed_resources_target = ":packed_resources"
  config_file = "credential_provider_extension_localize_strings_config.plist"
  datapack_dir = get_label_info(_packed_resources_target, "target_gen_dir")
  packed_locales = ios_packed_locales_as_mac_outputs
  output_filenames = [ "Localizable.strings" ]
  deps = [ _packed_resources_target ]
}

source_set("credential_provider_extension_providers") {
  sources = [ "account_verification_provider.h" ]

  assert_no_deps = [ "//ios/chrome/browser" ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("account_verification_provider") {
  sources = [ "account_verification_provider.mm" ]

  deps = [
    ":credential_provider_extension_providers",
    "//base",
  ]

  assert_no_deps = [ "//ios/chrome/browser" ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

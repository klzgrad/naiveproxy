# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//ios/build/chrome_build.gni")

buildflag_header("buildflags") {
  header = "showcase_model_buildflags.h"
  flags = [
    "SHOWCASE_CREDENTIAL_PROVIDER_ENABLED=ios_enable_credential_provider_extension",
    "SHOWCASE_CONTENT_WIDGET_ENABLED=$ios_enable_content_widget_extension",
    "SHOWCASE_SEARCH_WIDGET_ENABLED=$ios_enable_search_widget_extension",
  ]
}

source_set("core") {
  sources = [
    "app_delegate.h",
    "app_delegate.mm",
    "showcase_model.h",
    "showcase_model.mm",
    "showcase_view_controller.h",
    "showcase_view_controller.mm",
  ]
  deps = [
    ":buildflags",
    "//base",
    "//base:i18n",
    "//ios/chrome/browser/ui/main",
    "//ios/chrome/browser/ui/util",
    "//ios/showcase/common",
    "//ios/third_party/material_components_ios:material_components_ios+bundle",
    "//ios/third_party/material_components_ios:material_components_ios+link",
    "//ui/base",
  ]
  frameworks = [ "UIKit.framework" ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("main_eg2") {
  testonly = true
  sources = [ "main.mm" ]
  deps = [
    ":core",
    "//base",
    "//ios/chrome/browser/ui/omnibox/keyboard_assist",
    "//ios/chrome/browser/ui/settings",

    # Needed to disable the tests hooks.
    "//ios/chrome/test/earl_grey:hooks",

    # Needed for including ios/chrome/browser/ui.
    "//ios/chrome/browser/tabs:tabs_internal",
  ]
  frameworks = [ "UIKit.framework" ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("main") {
  sources = [ "main.mm" ]
  deps = [
    ":core",
    "//base",

    # Needed to disable the tests hooks.
    "//ios/chrome/app:tests_fake_hook",
    "//ios/chrome/browser/ui/omnibox/keyboard_assist",
    "//ios/chrome/browser/ui/settings",

    # Needed for including ios/chrome/browser/ui.
    "//ios/chrome/browser/tabs:tabs_internal",
  ]
  frameworks = [ "UIKit.framework" ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("eg2_tests") {
  defines = [ "CHROME_EARL_GREY_2" ]
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true
  sources = [ "showcase_egtest.mm" ]
  deps = [
    "//ios/showcase/test:eg2_test",
    "//ios/testing/earl_grey:eg_test_support+eg2",
    "//ios/third_party/earl_grey2:test_lib",
  ]
  frameworks = [ "UIKit.framework" ]
}

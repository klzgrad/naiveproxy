# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("service_manager") {
  testonly = true
  sources = [
    "service_manager_listener_unittest.cc",
    "service_manager_unittest.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//base/test:test_config",
    "//base/test:test_support",
    "//mojo/public/cpp/platform",
    "//mojo/public/cpp/system",
    "//services/service_manager",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/test:test_support",
    "//services/service_manager/public/mojom",
    "//services/service_manager/runner/common",
    "//services/service_manager/tests:catalog_source",
    "//testing/gtest",
  ]

  data_deps = [
    ":service_manager_unittest_embedder",
    ":service_manager_unittest_target",
  ]
}

mojom("interfaces") {
  sources = [
    "service_manager_unittest.mojom",
  ]

  deps = [
    "//services/service_manager/public/mojom",
  ]
}

service_manifest("manifest") {
  name = "service_manager_unittest"
  source = "service_manager_unittest_manifest.json"
}

executable("service_manager_unittest_target") {
  testonly = true

  sources = [
    "target.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//build/win:default_exe_manifest",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/standalone_service:main",
  ]
}

service_manifest("target_manifest") {
  name = "service_manager_unittest_target"
  source = "target_manifest.json"
}

service_manifest("embedder_manifest") {
  name = "service_manager_unittest_embedder"
  source = "embedder_manifest.json"
  packaged_services = [
    ":shared_instance_across_users_manifest",
    ":regular_manifest",
    ":singleton_manifest",
  ]
}

service_manifest("shared_instance_across_users_manifest") {
  name = "service_manager_unittest_shared_instance_across_users"
  source = "shared_instance_across_users_manifest.json"
}

service_manifest("singleton_manifest") {
  name = "service_manager_unittest_singleton"
  source = "singleton_manifest.json"
}

service_manifest("regular_manifest") {
  name = "service_manager_unittest_regular"
  source = "regular_manifest.json"
}

service("service_manager_unittest_embedder") {
  testonly = true

  sources = [
    "embedder.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//build/win:default_exe_manifest",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/standalone_service:main",
    "//services/service_manager/public/mojom",
    "//services/service_manager/runner:init",
    "//services/service_manager/runner/common",
  ]
}

catalog("catalog") {
  embedded_services = [ ":manifest" ]

  standalone_services = [
    ":embedder_manifest",
    ":target_manifest",
  ]
}

# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

source_set("cpp") {
  sources = [
    "safe_json_parser.cc",
    "safe_json_parser.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//base",
  ]

  public_deps = [
    "//services/data_decoder/public/mojom",
  ]

  if (!is_ios) {
    sources += [
      "decode_image.cc",
      "decode_image.h",
      "json_sanitizer.h",
      "safe_json_parser_impl.cc",
      "safe_json_parser_impl.h",
      "safe_xml_parser.cc",
      "safe_xml_parser.h",
    ]
    public_deps += [ "//services/service_manager/public/cpp" ]
    if (is_android) {
      sources += [
        "json_sanitizer_android.cc",
        "safe_json_parser_android.cc",
        "safe_json_parser_android.h",
      ]
      deps += [ "android:safe_json_jni_headers" ]
    } else {
      sources += [ "json_sanitizer.cc" ]
    }
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "testing_json_parser.cc",
    "testing_json_parser.h",
  ]

  deps = [
    ":cpp",
    "//base",
    "//services/service_manager/public/cpp/test:test_support",
  ]

  if (!is_ios) {
    sources += [
      "test_data_decoder_service.cc",
      "test_data_decoder_service.h",
    ]

    deps += [
      "//services/data_decoder:lib",
      "//services/service_manager/public/cpp/test:test_support",
    ]
  }
}

source_set("manifest") {
  if (!is_ios) {
    sources = [
      "manifest.cc",
      "manifest.h",
    ]

    deps = [
      "//base",
      "//services/data_decoder/public/mojom",
      "//services/service_manager/public/cpp",
    ]
  }
}

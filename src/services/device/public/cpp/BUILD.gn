# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

component("device_features") {
  public = [
    "device_features.h",
  ]
  sources = [
    "device_features.cc",
    "device_features_export.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
  public_deps = [
    "//base",
  ]

  defines = [ "DEVICE_FEATURES_IMPLEMENTATION" ]
}

source_set("manifest") {
  sources = [
    "manifest.cc",
    "manifest.h",
  ]

  deps = [
    "//base",
    "//services/device/public/mojom",
    "//services/device/public/mojom:generic_sensor",
    "//services/device/public/mojom:usb",
    "//services/device/public/mojom:usb_test",
    "//services/service_manager/public/cpp",
  ]

  if (is_chromeos && use_dbus) {
    defines = [ "USE_DBUS=1" ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "test/fake_sensor_and_provider.cc",
    "test/fake_sensor_and_provider.h",
    "test/fake_serial_port_manager.cc",
    "test/fake_serial_port_manager.h",
    "test/fake_usb_device.cc",
    "test/fake_usb_device.h",
    "test/fake_usb_device_info.cc",
    "test/fake_usb_device_info.h",
    "test/fake_usb_device_manager.cc",
    "test/fake_usb_device_manager.h",
    "test/mock_usb_mojo_device.cc",
    "test/mock_usb_mojo_device.h",
    "test/scoped_geolocation_overrider.cc",
    "test/scoped_geolocation_overrider.h",
    "test/test_wake_lock_provider.cc",
    "test/test_wake_lock_provider.h",
  ]

  public_deps = [
    "//base",
    "//services/device/public/cpp/generic_sensor",
    "//services/device/public/cpp/geolocation",
    "//services/device/public/cpp/usb",
    "//services/device/public/mojom",
    "//services/device/public/mojom:usb",
    "//services/service_manager/public/cpp",
  ]

  deps = [
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}

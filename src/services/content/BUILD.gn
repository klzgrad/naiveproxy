# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

source_set("impl") {
  visibility = [
    ":tests",
    "//content/browser",
  ]

  public = [
    "navigable_contents_delegate.h",
    "service.h",
    "service_delegate.h",
  ]

  sources = [
    "navigable_contents_factory_impl.cc",
    "navigable_contents_factory_impl.h",
    "navigable_contents_impl.cc",
    "navigable_contents_impl.h",
    "service.cc",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    "//base",
    "//services/service_manager/public/cpp",
    "//ui/gfx",
  ]

  deps = [
    "//mojo/public/cpp/bindings",
    "//services/content/public/cpp",
    "//services/content/public/mojom",
  ]

  if (toolkit_views && !is_chromecast) {
    deps += [
      "//ui/base",
      "//ui/views",
    ]

    if (use_aura) {
      deps += [ "//ui/aura" ]
    }
  }
}

source_set("tests") {
  testonly = true

  sources = [
    "service_unittest.cc",
  ]

  deps = [
    ":impl",
    "//base",
    "//base/test:test_support",
    "//services/content/public/cpp",
    "//services/content/public/cpp/test:tests",
    "//services/content/public/mojom",
    "//services/service_manager/public/cpp/test:test_support",
    "//testing/gtest",
    "//url",
  ]
}

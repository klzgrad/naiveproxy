# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/content/public/features.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_REMOTE_NAVIGABLE_CONTENTS_VIEW=$enable_remote_navigable_contents_view" ]
}

component("cpp") {
  output_name = "content_service_cpp"

  public = [
    "navigable_contents.h",
    "navigable_contents_observer.h",
    "navigable_contents_view.h",
  ]

  sources = [
    "navigable_contents.cc",
    "navigable_contents_view.cc",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  defines = [ "IS_CONTENT_SERVICE_CPP_IMPL" ]

  public_deps = [
    ":buildflags",
    "//base",
    "//net",
    "//services/content/public/mojom",
    "//ui/gfx:native_widget_types",
    "//ui/gfx/geometry",
    "//url",
  ]

  deps = []
  if (toolkit_views) {
    deps += [
      "//ui/base",
      "//ui/views",
    ]

    if (enable_remote_navigable_contents_view) {
      deps += [
        "//services/ws/public/mojom",
        "//ui/views/mus/remote_view:remote_view_host",
      ]
    }
  }

  if (use_aura) {
    deps += [ "//ui/aura" ]
  }
}

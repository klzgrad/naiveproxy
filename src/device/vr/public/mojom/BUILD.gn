# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//device/vr/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom_component("vr_service") {
  output_prefix = "device_vr_service_mojo_bindings"
  macro_prefix = "DEVICE_VR_SERVICE_MOJO_BINDINGS"

  sources = [ "vr_service.mojom" ]

  public_deps = [
    ":test_mojom",
    "//device/gamepad/public/mojom",
    "//gpu/ipc/common:interfaces",
    "//mojo/public/mojom/base",
    "//skia/public/mojom",
    "//ui/display/mojom:mojom",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]

  enabled_features = []
  if (enable_openxr) {
    enabled_features += [ "enable_openxr" ]
  }

  shared_cpp_typemap = {
    types = [
      {
        mojom = "device.mojom.RgbaTupleF16"
        cpp = "::device::RgbaTupleF16"
      },
      {
        mojom = "device.mojom.RgbTupleF32"
        cpp = "::device::RgbTupleF32"
      },
      {
        mojom = "device.mojom.Pose"
        cpp = "::device::Pose"
      },
    ]
    traits_headers = [ "//device/vr/public/mojom/vr_service_mojom_traits.h" ]
    traits_public_deps = [ ":vr_public_typemaps" ]
  }

  cpp_typemaps = [ shared_cpp_typemap ]
  blink_cpp_typemaps = [ shared_cpp_typemap ]
}

# Separating the public mojom interface into vr_service and isolated_xr_service
# components allows us to add dependencies to isolated_xr_service without having
# a blink variant. The blink variant tends to cause problematic, unresolved externals.
mojom_component("isolated_xr_service") {
  output_prefix = "device_vr_isolated_xr_service_mojo_bindings"
  macro_prefix = "DEVICE_VR_ISOLATED_XR_SERVICE_MOJO_BINDINGS"

  sources = [ "isolated_xr_service.mojom" ]

  disable_variants = true

  public_deps = [
    ":test_mojom",
    ":vr_service",
    "//gpu/ipc/common:interfaces",
    "//mojo/public/mojom/base",
    "//skia/public/mojom",
    "//ui/gfx/geometry/mojom",
  ]

  deps = [ "//services/viz/public/mojom" ]
}

mojom_component("test_mojom") {
  output_prefix = "device_vr_test_mojo_bindings"
  macro_prefix = "DEVICE_VR_TEST_MOJO_BINDINGS"

  sources = [ "browser_test_interfaces.mojom" ]

  public_deps = [
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]
}

component("vr_public_typemaps") {
  output_name = "device_vr_public_typemaps"

  defines = [ "IS_VR_PUBLIC_TYPEMAPS_IMPL" ]

  sources = [
    "pose.cc",
    "pose.h",
  ]

  deps = [
    "//base:base",
    "//skia",
    "//ui/gfx:geometry_skia",
    "//ui/gfx/geometry:geometry",
  ]
}

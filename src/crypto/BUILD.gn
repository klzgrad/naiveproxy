# Copyright 2013 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//crypto/features.gni")

if (is_ios) {
  import("//build/config/apple/mobile_config.gni")  # For `target_platform`
}

buildflag_header("buildflags") {
  header = "crypto_buildflags.h"
  flags = [ "USE_NSS_CERTS=$use_nss_certs" ]
}

component("crypto") {
  output_name = "crcrypto"  # Avoid colliding with OpenSSL's libcrypto.
  sources = [
    "aead.cc",
    "aead.h",
    "aes_cbc.cc",
    "aes_cbc.h",
    "aes_ctr.cc",
    "aes_ctr.h",
    "cose.cc",
    "cose.h",
    "crypto_export.h",
    "ecdsa_utils.cc",
    "ecdsa_utils.h",
    "evp.cc",
    "evp.h",
    "features.cc",
    "features.h",
    "hash.cc",
    "hash.h",
    "hkdf.cc",
    "hkdf.h",
    "hmac.cc",
    "hmac.h",
    "kdf.cc",
    "kdf.h",
    "kex.cc",
    "kex.h",
    "keypair.cc",
    "keypair.h",
    "obsolete/md5.cc",
    "obsolete/md5.h",
    "obsolete/sha1.cc",
    "obsolete/sha1.h",
    "openssl_util.cc",
    "openssl_util.h",
    "pem.cc",
    "pem.h",
    "process_bound_string.cc",
    "process_bound_string.h",
    "random.cc",
    "random.h",
    "scoped_nss_types.h",
    "secure_hash.cc",
    "secure_hash.h",
    "secure_util.cc",
    "secure_util.h",
    "sha2.cc",
    "sha2.h",
    "sign.cc",
    "sign.h",
    "signature_verifier.cc",
    "signature_verifier.h",
    "subtle_passkey.cc",
    "subtle_passkey.h",
    "unexportable_key.cc",
    "unexportable_key.h",
    "unexportable_key_metrics.cc",
    "unexportable_key_metrics.h",
    "unexportable_key_software_unsecure.cc",
    "user_verifying_key.cc",
    "user_verifying_key.h",
  ]

  deps = [ "//components/cbor" ]

  public_deps = [
    ":buildflags",
    "//base",
    "//third_party/boringssl",
  ]

  if (is_apple) {
    sources += [
      "apple/keychain.cc",
      "apple/keychain.h",
      "apple/keychain_secitem.h",
      "apple/keychain_secitem.mm",
      "apple/keychain_util.h",
      "apple/keychain_util.mm",
      "apple/keychain_v2.h",
      "apple/keychain_v2.mm",
    ]

    if (is_mac) {
      sources += [
        "apple/scoped_lacontext.h",
        "apple/scoped_lacontext.mm",
        "apple/security_framework_lock.cc",
        "apple/security_framework_lock.h",
        "apple/unexportable_key_mac.h",
        "apple/unexportable_key_mac.mm",
        "user_verifying_key_mac.mm",
      ]
    }

    frameworks = [
      "CoreFoundation.framework",
      "CryptoTokenKit.framework",
      "Foundation.framework",
      "Security.framework",
    ]

    if (is_mac || (is_ios && target_platform != "tvos")) {
      frameworks += [ "LocalAuthentication.framework" ]
    }
  }

  if (is_win) {
    sources += [
      "scoped_capi_types.h",
      "scoped_cng_types.h",
      "unexportable_key_win.cc",
      "unexportable_key_win.h",
      "user_verifying_key_win.cc",
    ]
    libs = [
      "crypt32.lib",
      "ncrypt.lib",
    ]
  }

  # Some files are built when NSS is used for the platform certificate library.
  if (use_nss_certs) {
    sources += [
      "nss_crypto_module_delegate.h",
      "nss_key_util.cc",
      "nss_key_util.h",
      "nss_util.cc",
      "nss_util.h",
      "nss_util_internal.h",
    ]
    configs += [ "//build/config/linux/nss" ]
  }

  if (is_chromeos) {
    sources += [
      "chaps_support.cc",
      "chaps_support.h",
      "nss_util_chromeos.cc",
    ]
  }

  defines = [ "CRYPTO_IMPLEMENTATION" ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

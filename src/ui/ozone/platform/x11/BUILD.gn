# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//gpu/vulkan/features.gni")

visibility = [ "//ui/ozone/*" ]

source_set("x11") {
  sources = [
    "client_native_pixmap_factory_x11.cc",
    "client_native_pixmap_factory_x11.h",
    "gl_ozone_glx.cc",
    "gl_ozone_glx.h",
    "gl_surface_egl_ozone_x11.cc",
    "gl_surface_egl_ozone_x11.h",
    "gl_surface_egl_readback_x11.cc",
    "gl_surface_egl_readback_x11.h",
    "gl_surface_glx_ozone.cc",
    "gl_surface_glx_ozone.h",
    "ozone_platform_x11.cc",
    "ozone_platform_x11.h",
    "x11_canvas_surface.cc",
    "x11_canvas_surface.h",
    "x11_clipboard_ozone.cc",
    "x11_clipboard_ozone.h",
    "x11_cursor_factory_ozone.cc",
    "x11_cursor_factory_ozone.h",
    "x11_cursor_ozone.cc",
    "x11_cursor_ozone.h",
    "x11_screen_ozone.cc",
    "x11_screen_ozone.h",
    "x11_surface_factory.cc",
    "x11_surface_factory.h",
    "x11_window_ozone.cc",
    "x11_window_ozone.h",
  ]

  deps = [
    "//base",
    "//gpu/vulkan:buildflags",
    "//skia",
    "//ui/base",
    "//ui/base/clipboard:clipboard_types",
    "//ui/base/ime",
    "//ui/base/x",
    "//ui/display/fake",
    "//ui/events",
    "//ui/events/devices",
    "//ui/events/ozone",
    "//ui/events/ozone/layout",
    "//ui/events/platform",
    "//ui/events/platform/x11",
    "//ui/events/x",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/x",
    "//ui/gl",
    "//ui/ozone:ozone_base",
    "//ui/ozone/common",
    "//ui/platform_window",
    "//ui/platform_window/platform_window_handler",
    "//ui/platform_window/x11",
  ]

  if (is_chromeos) {
    deps += [ "//ui/base/ime/chromeos" ]
  } else {
    deps += [ "//ui/base/ime/linux" ]
  }

  if (enable_vulkan) {
    deps += [ "//gpu/vulkan/x" ]
  }

  configs += [
    "//build/config/linux:x11",
    "//build/config/linux:xrandr",
  ]

  public_configs = [ "//third_party/khronos:khronos_headers" ]
}

source_set("x11_unittests") {
  testonly = true
  sources = [
    "x11_cursor_factory_ozone_unittest.cc",
    "x11_screen_ozone_unittest.cc",
    "x11_window_ozone_unittest.cc",
  ]

  deps = [
    ":x11",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base/x",
    "//ui/events:test_support",
    "//ui/events/devices/x11",
    "//ui/events/platform/x11",
    "//ui/ozone:platform",
    "//ui/ozone:test_support",
    "//ui/ozone/common",
    "//ui/platform_window/x11:x11",
  ]
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia)

import("//gpu/vulkan/features.gni")

visibility = [ "//ui/ozone/*" ]

source_set("scenic") {
  sources = [
    "client_native_pixmap_factory_scenic.cc",
    "client_native_pixmap_factory_scenic.h",
    "ozone_platform_scenic.cc",
    "ozone_platform_scenic.h",
    "scenic_gpu_host.cc",
    "scenic_gpu_host.h",
    "scenic_gpu_service.cc",
    "scenic_gpu_service.h",
    "scenic_screen.cc",
    "scenic_screen.h",
    "scenic_surface.cc",
    "scenic_surface.h",
    "scenic_surface_factory.cc",
    "scenic_surface_factory.h",
    "scenic_window.cc",
    "scenic_window.h",
    "scenic_window_canvas.cc",
    "scenic_window_canvas.h",
    "scenic_window_manager.cc",
    "scenic_window_manager.h",
    "sysmem_buffer_collection.cc",
    "sysmem_buffer_collection.h",
    "sysmem_buffer_manager.cc",
    "sysmem_buffer_manager.h",
  ]

  defines = [
    "OZONE_IMPLEMENTATION",
    "VK_USE_PLATFORM_FUCHSIA",
  ]

  deps = [
    "//base",
    "//mojo/public/cpp/system",
    "//services/service_manager/public/cpp",
    "//skia",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.images",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.mem",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.sysmem",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.ui.gfx",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.ui.scenic",
    "//third_party/fuchsia-sdk/sdk/pkg/scenic_cpp",
    "//third_party/fuchsia-sdk/sdk/pkg/sys_cpp",
    "//ui/base",
    "//ui/base/ime/fuchsia",
    "//ui/display/fake",
    "//ui/events:dom_keycode_converter",
    "//ui/events/ozone/layout",
    "//ui/gfx/geometry",
    "//ui/ozone:ozone_base",
    "//ui/ozone/common",
    "//ui/ozone/public/mojom",
    "//ui/platform_window",
  ]

  data_deps = []
  if (enable_vulkan) {
    sources += [
      "vulkan_implementation_scenic.cc",
      "vulkan_implementation_scenic.h",
    ]
    defines += [ "VK_USE_PLATFORM_MAGMA_KHR" ]
    deps += [ "//gpu/ipc/common:vulkan_ycbcr_info" ]
    data_deps += [
      "//third_party/angle:libEGL",
      "//third_party/angle:libGLESv2",
    ]
  }
}

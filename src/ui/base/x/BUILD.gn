# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")
import("//build/config/ui.gni")
import("//ui/ozone/ozone.gni")

assert(use_x11 || ozone_platform_x11)

jumbo_component("x") {
  output_name = "ui_base_x"

  sources = [
    "x11_display_manager.cc",
    "x11_display_manager.h",
    "x11_display_util.cc",
    "x11_display_util.h",
    "x11_menu_list.cc",
    "x11_menu_list.h",
    "x11_pointer_grab.cc",
    "x11_pointer_grab.h",
    "x11_shm_image_pool_base.cc",
    "x11_shm_image_pool_base.h",
    "x11_software_bitmap_presenter.cc",
    "x11_software_bitmap_presenter.h",
    "x11_util.cc",
    "x11_util.h",
    "x11_util_internal.h",
    "x11_window.cc",
    "x11_window.h",
  ]

  if (use_x11) {
    sources += [
      "x11_shm_image_pool.cc",
      "x11_shm_image_pool.h",
    ]
  } else {
    sources += [
      "x11_shm_image_pool_ozone.cc",
      "x11_shm_image_pool_ozone.h",
    ]
  }

  configs += [
    "//build/config/linux:x11",
    "//build/config/linux:xrandr",
  ]

  defines = [ "IS_UI_BASE_X_IMPL" ]

  deps = [
    "//base",
    "//base:i18n",
    "//skia",
    "//ui/base:hit_test",
    "//ui/display/util",
    "//ui/events",
    "//ui/events/devices/x11",
    "//ui/events/keycodes:x11",
    "//ui/events/platform/x11",
    "//ui/events/x:x",
    "//ui/gfx",
    "//ui/gfx/x",
    "//ui/platform_window/common",
  ]
}

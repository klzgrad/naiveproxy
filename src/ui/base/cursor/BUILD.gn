# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

component("cursor") {
  output_name = "ui_base_cursor"

  sources = [
    "cursor.cc",
    "cursor.h",
    "cursor_size.h",
  ]

  defines = [ "IS_UI_BASE_CURSOR_IMPL" ]

  public_deps = [
    "//base",
    "//skia",
    "//ui/base/mojom:cursor_type",
    "//ui/gfx/geometry",
  ]

  deps = [ "//ui/gfx:geometry_skia" ]

  if (use_x11 && use_aura) {
    sources += [ "cursor_x11.cc" ]

    deps += [ "//ui/base/x" ]
  }

  if (use_ozone) {
    sources += [ "cursor_ozone.cc" ]

    deps += [ "//ui/ozone:ozone_base" ]
  }

  if (is_win) {
    sources += [ "cursor_win.cc" ]
  }
}

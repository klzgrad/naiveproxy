# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

assert(is_chromeos, "SMB Shares is Chrome OS only.")

# TODO(crbug.com/1031947): Fully convert this component to Polymer 3 once
# chrome://os-settings has been migrated.

js_type_check("closure_compile") {
  deps = [
    ":add_smb_share_dialog",
    ":smb_browser_proxy",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":add_smb_share_dialog.m",
    ":smb_browser_proxy.m",
  ]
}

js_library("smb_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr" ]
}

js_library("add_smb_share_dialog") {
  deps = [
    ":smb_browser_proxy",
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_checkbox:cr_checkbox",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog",
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:i18n_behavior",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("add_smb_share_dialog.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/smb_shares/add_smb_share_dialog.m.js" ]
  deps = [
    ":smb_browser_proxy.m",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_checkbox:cr_checkbox.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":add_smb_share_dialog_module" ]
}

js_modulizer("smb_browser_proxy_module") {
  input_files = [ "smb_browser_proxy.js" ]
  namespace_rewrites = [ "smb_shares.SmbBrowserProxy|SmbBrowserProxy" ]
}

js_library("smb_browser_proxy.m") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/smb_shares/smb_browser_proxy.m.js" ]
  extra_deps = [ ":smb_browser_proxy_module" ]
}

polymer_modulizer("add_smb_share_dialog") {
  js_file = "add_smb_share_dialog.js"
  html_file = "add_smb_share_dialog.html"
  html_type = "dom-module"
  namespace_rewrites = [ "smb_shares.SmbBrowserProxy|SmbBrowserProxy" ]
  auto_imports = [ "ui/webui/resources/cr_components/chromeos/smb_shares/smb_browser_proxy.html|SmbAuthMethod,SmbBrowserProxy,SmbBrowserProxyImpl,SmbMountResult" ]
}

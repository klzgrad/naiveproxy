# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../os_cr_components.gni")

js_type_check("closure_compile") {
  uses_legacy_modules = true
  deps = [
    ":pin_keyboard",
    ":setup_pin_keyboard",
  ]
}

js_library("pin_keyboard") {
  deps = [
    ":lock_screen_constants",
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
    "//ui/webui/resources/js:i18n_behavior",
  ]
}

js_library("lock_screen_constants") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_profile_avatar_selector:cr_profile_avatar_selector",
    "//ui/webui/resources/js:cr",
  ]
}

js_library("setup_pin_keyboard") {
  deps = [
    ":lock_screen_constants",
    ":pin_keyboard",
    "//ui/webui/resources/cr_components/chromeos/quick_unlock:lock_screen_constants",
    "//ui/webui/resources/js:i18n_behavior",
  ]
  externs_list = [ "$externs_path/quick_unlock_private.js" ]
  extra_sources = [ "$interfaces_path/quick_unlock_private_interface.js" ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":lock_screen_constants.m",
    ":pin_keyboard.m",
    ":setup_pin_keyboard.m",
  ]
}

js_library("lock_screen_constants.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/quick_unlock/lock_screen_constants.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_profile_avatar_selector:cr_profile_avatar_selector.m",
    "//ui/webui/resources/js:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("pin_keyboard.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/quick_unlock/pin_keyboard.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  extra_deps = [ ":pin_keyboard_module" ]
}

js_library("setup_pin_keyboard.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/quick_unlock/setup_pin_keyboard.m.js" ]
  deps = [
    ":lock_screen_constants.m",
    ":pin_keyboard.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_components/chromeos/quick_unlock:lock_screen_constants.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  externs_list = [ "$externs_path/quick_unlock_private.js" ]
  extra_sources = [ "$interfaces_path/quick_unlock_private_interface.js" ]
  extra_deps = [ ":setup_pin_keyboard_module" ]
}

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":pin_keyboard_icon_module",
    ":pin_keyboard_module",
    ":setup_pin_keyboard_module",
  ]
}

polymer_modulizer("pin_keyboard") {
  js_file = "pin_keyboard.js"
  html_file = "pin_keyboard.html"
  html_type = "dom-module"
  namespace_rewrites = cr_components_chromeos_namespace_rewrites
  auto_imports = cr_components_chromeos_auto_imports
}

polymer_modulizer("setup_pin_keyboard") {
  js_file = "setup_pin_keyboard.js"
  html_file = "setup_pin_keyboard.html"
  html_type = "dom-module"
  namespace_rewrites = cr_components_chromeos_namespace_rewrites
  auto_imports = cr_components_chromeos_auto_imports
}

js_modulizer("modulize") {
  input_files = [ "lock_screen_constants.js" ]
  namespace_rewrites = cr_components_chromeos_namespace_rewrites
}

polymer_modulizer("pin_keyboard_icon") {
  js_file = "pin_keyboard_icon.m.js"
  html_file = "pin_keyboard_icon.html"
  html_type = "iron-iconset"
}

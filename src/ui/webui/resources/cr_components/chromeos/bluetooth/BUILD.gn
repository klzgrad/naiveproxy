# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

assert(is_chromeos, "Bluetooth dialog is Chrome OS only.")

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [ ":bluetooth_dialog.m" ]
}

js_library("bluetooth_dialog.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/bluetooth/bluetooth_dialog.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  externs_list = chrome_extension_public_externs + [
                   "$externs_path/bluetooth.js",
                   "$externs_path/bluetooth_private.js",
                 ]
  extra_sources = [
    "$interfaces_path/bluetooth_interface.js",
    "$interfaces_path/bluetooth_private_interface.js",
  ]
  extra_deps = [ ":bluetooth_dialog_module" ]
}

polymer_modulizer("bluetooth_dialog") {
  js_file = "bluetooth_dialog.js"
  html_file = "bluetooth_dialog.html"
  html_type = "dom-module"
  auto_imports = [ "ui/webui/resources/html/assert.html|assert" ]
}

group("polymer3_elements") {
  public_deps = [ ":bluetooth_dialog_module" ]
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("../../../tools/js_modulizer.gni")
import("../../cr.gni")

preprocess_folder = "$root_gen_dir/ui/webui/resources/preprocessed/js/cr/ui"
preprocess_src_manifest = "preprocessed_src_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

generate_grd("build_grdp") {
  grd_prefix = "webui_js_cr_ui"
  out_grd = "$target_gen_dir/resources.grdp"
  deps = [ ":preprocess" ]
  manifest_files = [
    "$target_gen_dir/$preprocess_gen_manifest",
    "$target_gen_dir/$preprocess_src_manifest",
  ]
  resource_path_prefix = "js/cr/ui"
}

group("preprocess") {
  public_deps = [
    ":preprocess_generated",
    ":preprocess_src",
  ]
}

preprocess_if_expr("preprocess_src") {
  in_folder = "./"
  out_folder = "$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_src_manifest"
  in_files = [
    "array_data_model.js",
    "command.js",
    "context_menu_handler.js",
    "dialogs.js",
    "drag_wrapper.js",
    "focus_grid.js",
    "focus_manager.js",
    "focus_outline_manager.js",
    "focus_row.js",
    "keyboard_shortcut_list.js",
    "list_item.js",
    "list.js",
    "list_selection_controller.js",
    "list_selection_model.js",
    "list_single_selection_model.js",
    "menu_button.js",
    "menu_item.js",
    "menu.js",
    "position_util.js",
    "splitter.js",
    "store_client.js",
    "store.js",
    "tabs.js",
    "tree.js",
  ]

  if (!is_android) {
    in_files += [
      "focus_row_behavior.js",
      "focus_without_ink.js",
    ]
  }

  if (is_chromeos_ash) {
    in_files += [ "grid.js" ]
  }
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":modulize" ]
  in_folder = target_gen_dir
  out_folder = "$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "array_data_model.m.js",
    "drag_wrapper.m.js",
    "focus_grid.m.js",
    "focus_outline_manager.m.js",
    "focus_row.m.js",
    "keyboard_shortcut_list.m.js",
    "list_item.m.js",
    "list.m.js",
    "list_selection_controller.m.js",
    "list_selection_model.m.js",
    "store_client.m.js",
    "store.m.js",
    "tabs.m.js",
    "tree.m.js",
  ]

  if (!is_android) {
    in_files += [
      "command.m.js",
      "context_menu_handler.m.js",
      "focus_row_behavior.m.js",
      "focus_without_ink.m.js",
      "menu_button.m.js",
      "menu_item.m.js",
      "menu.m.js",
      "position_util.m.js",
    ]
  }

  if (is_chromeos_ash) {
    in_files += [
      "dialogs.m.js",
      "grid.m.js",
      "list_single_selection_model.m.js",
      "splitter.m.js",
    ]
  }
}

group("closure_compile") {
  deps = [
    ":ui_resources",
    ":ui_resources_modules",
  ]
}

js_type_check("ui_resources") {
  uses_legacy_modules = true
  deps = [
    ":array_data_model",
    ":command",
    ":context_menu_handler",
    ":dialogs",
    ":drag_wrapper",
    ":focus_grid",
    ":focus_manager",
    ":focus_outline_manager",
    ":focus_row",
    ":focus_row_behavior",
    ":focus_without_ink",
    ":keyboard_shortcut_list",
    ":list",
    ":list_item",
    ":list_selection_controller",
    ":list_selection_model",
    ":list_single_selection_model",
    ":menu",
    ":menu_button",
    ":menu_item",
    ":position_util",
    ":splitter",
    ":store",
    ":store_client",
    ":tabs",
    ":tree",
  ]

  if (is_chromeos_ash) {
    deps += [ ":grid" ]
  }
}

js_library("array_data_model") {
  deps = [
    "..:event_target",
    "../..:cr",
  ]
}

js_library("command") {
  deps = [
    ":keyboard_shortcut_list",
    "..:ui",
    "../..:cr",
  ]
}

js_library("context_menu_handler") {
  deps = [
    ":menu",
    ":menu_button",
    ":position_util",
    "..:event_target",
    "..:ui",
    "../..:cr",
  ]
}

js_library("dialogs") {
  deps = [ "../..:cr" ]
}

js_library("drag_wrapper") {
  deps = [ "../..:cr" ]
}

js_library("focus_grid") {
  deps = [
    ":focus_row",
    "../..:assert",
    "../..:cr",
  ]
}

js_library("focus_manager") {
  deps = [ "../..:cr" ]
}

js_library("focus_outline_manager") {
  deps = [ "../..:cr" ]
}

js_library("focus_row_behavior") {
  deps = [
    ":focus_row",
    ":focus_without_ink",
  ]
}

js_library("focus_row") {
  deps = [
    "../..:assert",
    "../..:cr",
    "../..:event_tracker",
    "../..:util",
  ]
}

js_library("focus_without_ink") {
  deps = [
    "..:ui",
    "../..:cr",
  ]
}

js_library("grid") {
  deps = [ ":list" ]
}

js_library("keyboard_shortcut_list") {
  deps = [ "../..:cr" ]
}

js_library("list") {
  deps = [
    ":array_data_model",
    ":list_item",
    ":list_selection_controller",
    ":list_selection_model",
  ]
}

js_library("list_item") {
  deps = [ "../..:cr" ]
}

js_library("list_selection_controller") {
  deps = [
    ":list_selection_model",
    "../..:cr",
  ]
}

js_library("list_selection_model") {
  deps = [
    "..:event_target",
    "../..:cr",
  ]
}

js_library("list_single_selection_model") {
  deps = [
    "..:event_target",
    "../..:cr",
  ]
}

js_library("menu_button") {
  deps = [
    ":menu",
    ":menu_item",
    ":position_util",
    "..:ui",
    "../..:assert",
    "../..:cr",
    "../..:event_tracker",
  ]
}

js_library("menu_item") {
  deps = [
    ":command",
    "..:ui",
    "../..:cr",
    "../..:load_time_data",
  ]
}

js_library("menu") {
  deps = [
    ":menu_item",
    "..:ui",
    "../..:assert",
    "../..:cr",
  ]
}

js_library("position_util") {
  deps = [ "../..:cr" ]
}

js_library("splitter") {
  deps = [
    "..:ui",
    "../..:cr",
  ]
}

js_library("store") {
  deps = [ "../..:cr" ]
}

js_library("store_client") {
  deps = [
    ":store",
    "../..:cr",
  ]
}

js_library("tabs") {
  deps = [
    ":focus_outline_manager",
    "..:ui",
    "../..:cr",
    "../..:util",
  ]
}

js_library("tree") {
  deps = [
    "..:ui",
    "../..:cr",
    "../..:util",
  ]
}

js_modulizer("modulize") {
  input_files = [
    "array_data_model.js",
    "command.js",
    "context_menu_handler.js",
    "dialogs.js",
    "drag_wrapper.js",
    "focus_grid.js",
    "focus_outline_manager.js",
    "focus_row.js",
    "focus_row_behavior.js",
    "focus_without_ink.js",
    "grid.js",
    "keyboard_shortcut_list.js",
    "list.js",
    "list_item.js",
    "list_selection_controller.js",
    "list_selection_model.js",
    "list_single_selection_model.js",
    "menu.js",
    "menu_button.js",
    "menu_item.js",
    "position_util.js",
    "splitter.js",
    "store.js",
    "store_client.js",
    "tabs.js",
    "tree.js",
  ]
  namespace_rewrites = cr_namespace_rewrites
}

js_type_check("ui_resources_modules") {
  is_polymer3 = true
  deps = [
    ":array_data_model.m",
    ":command.m",
    ":context_menu_handler.m",
    ":dialogs.m",
    ":drag_wrapper.m",
    ":focus_grid.m",
    ":focus_outline_manager.m",
    ":focus_row.m",
    ":focus_row_behavior.m",
    ":focus_without_ink.m",
    ":grid.m",
    ":keyboard_shortcut_list.m",
    ":list.m",
    ":list_item.m",
    ":list_selection_controller.m",
    ":list_selection_model.m",
    ":list_single_selection_model.m",
    ":menu.m",
    ":menu_button.m",
    ":menu_item.m",
    ":position_util.m",
    ":splitter.m",
    ":store.m",
    ":store_client.m",
    ":tabs.m",
    ":tree.m",
  ]
}

js_library("array_data_model.m") {
  sources =
      [ "$root_gen_dir/ui/webui/resources/js/cr/ui/array_data_model.m.js" ]
  deps = [
    "..:event_target.m",
    "../..:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("command.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/command.m.js" ]
  deps = [
    ":keyboard_shortcut_list.m",
    "../:ui.m",
    "../..:assert.m",
    "../..:cr.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("context_menu_handler.m") {
  sources =
      [ "$root_gen_dir/ui/webui/resources/js/cr/ui/context_menu_handler.m.js" ]
  deps = [
    ":menu.m",
    ":menu_button.m",
    ":menu_item.m",
    ":position_util.m",
    "..:event_target.m",
    "../:ui.m",
    "../..:assert.m",
    "../..:cr.m",
    "../..:event_tracker.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("dialogs.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/dialogs.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("drag_wrapper.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/drag_wrapper.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("focus_grid.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/focus_grid.m.js" ]
  deps = [
    ":focus_row.m",
    "../..:assert.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("focus_outline_manager.m") {
  sources =
      [ "$root_gen_dir/ui/webui/resources/js/cr/ui/focus_outline_manager.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("focus_row.m") {
  deps = [
    "../..:assert.m",
    "../..:event_tracker.m",
    "../..:util.m",
  ]
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/focus_row.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("focus_row_behavior.m") {
  deps = [
    ":focus_row.m",
    ":focus_without_ink.m",
    "../..:assert.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  sources =
      [ "$root_gen_dir/ui/webui/resources/js/cr/ui/focus_row_behavior.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("focus_without_ink.m") {
  deps = [
    "../..:assert.m",
    "../..:cr.m",
  ]
  sources =
      [ "$root_gen_dir/ui/webui/resources/js/cr/ui/focus_without_ink.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("grid.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/grid.m.js" ]
  deps = [
    ":list.m",
    ":list_item.m",
    ":list_selection_controller.m",
    ":list_selection_model.m",
    "../:ui.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("keyboard_shortcut_list.m") {
  sources = [
    "$root_gen_dir/ui/webui/resources/js/cr/ui/keyboard_shortcut_list.m.js",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("list.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/list.m.js" ]
  deps = [
    ":array_data_model.m",
    ":list_item.m",
    ":list_selection_controller.m",
    ":list_selection_model.m",
    "../:ui.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("list_item.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/list_item.m.js" ]
  deps = [ "../..:cr.m" ]
  extra_deps = [ ":modulize" ]
}

js_library("list_selection_controller.m") {
  sources = [
    "$root_gen_dir/ui/webui/resources/js/cr/ui/list_selection_controller.m.js",
  ]
  deps = [
    ":list_selection_model.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("list_selection_model.m") {
  sources =
      [ "$root_gen_dir/ui/webui/resources/js/cr/ui/list_selection_model.m.js" ]
  deps = [
    "..:event_target.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("list_single_selection_model.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/list_single_selection_model.m.js" ]
  deps = [
    "..:event_target.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("menu.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/menu.m.js" ]
  deps = [
    ":menu_item.m",
    "../:ui.m",
    "../..:assert.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("menu_button.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/menu_button.m.js" ]
  deps = [
    ":menu.m",
    ":menu_item.m",
    ":position_util.m",
    "../:ui.m",
    "../..:assert.m",
    "../..:cr.m",
    "../..:event_tracker.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("menu_item.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/menu_item.m.js" ]
  deps = [
    ":command.m",
    "../:ui.m",
    "../..:assert.m",
    "../..:cr.m",
    "../..:load_time_data.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("position_util.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/position_util.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("splitter.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/splitter.m.js" ]
  deps = [
    "..:ui.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("store.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/store.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("store_client.m") {
  deps = [
    ":store.m",
    "../..:assert.m",
  ]
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/store_client.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("tabs.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/tabs.m.js" ]
  deps = [
    ":focus_outline_manager.m",
    "..:ui.m",
    "../..:cr.m",
    "../..:util.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("tree.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/js/cr/ui/tree.m.js" ]
  deps = [
    "../:ui.m",
    "../..:assert.m",
    "../..:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

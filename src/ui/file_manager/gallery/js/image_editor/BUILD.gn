# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

js_type_check("closure_compile_jsmodules") {
  deps = [
    ":exif_encoder.m",
    ":image_encoder.m",
    ":image_util.m",
    ":test_util.m",
  ]
}

js_type_check("closure_compile_module") {
  uses_legacy_modules = true
  deps = [
    ":commands",
    ":exif_encoder",
    ":filter",
    ":image_adjust",
    ":image_buffer",
    ":image_editor",
    ":image_editor_mode",
    ":image_editor_prompt",
    ":image_editor_toolbar",
    ":image_encoder",
    ":image_loader",
    ":image_resize",
    ":image_transform",
    ":image_util",
    ":image_view",
    ":test_util",
    ":viewport",
  ]
}

js_library("commands") {
  deps = [
    ":filter",
    ":image_editor_prompt",
    ":image_util",
    ":image_view",
    ":viewport",
    "//ui/file_manager/file_manager/foreground/elements:files_toast",
  ]
}

js_library("exif_encoder") {
  deps = [
    ":image_encoder",
    "//ui/file_manager/file_manager/foreground/js/metadata:exif_constants",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_item",
  ]
  externs_list = [ "//ui/file_manager/externs/exif_entry.js" ]
}

js_library("exif_encoder.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/gallery/js/image_editor/exif_encoder.m.js",
  ]
  deps = [
    ":image_encoder.m",
    "//ui/file_manager/externs:exif_entry.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:exif_constants.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_item.m",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_unittest("exif_encoder_unittest.m") {
  deps = [
    ":exif_encoder.m",
    ":image_encoder.m",
    ":test_util.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/externs:metadata_worker_window.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:byte_reader.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:exif_parser.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_item.m",
  ]
}

js_library("filter") {
  deps = [ ":image_util" ]
}

js_library("image_adjust") {
  deps = [
    ":commands",
    ":image_editor",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("image_buffer") {
}

js_library("image_editor") {
  deps = [
    ":image_buffer",
    ":image_editor_mode",
    ":image_editor_toolbar",
    ":image_resize",
    ":image_util",
    ":image_view",
    ":viewport",
    "//ui/webui/resources/js/cr:event_target",
  ]
}

js_library("image_editor_mode") {
  deps = [
    ":commands",
    ":image_buffer",
    ":image_editor_toolbar",
    ":image_view",
    ":viewport",
  ]
}

js_library("image_editor_prompt") {
  deps = [ "//ui/webui/resources/js:assert" ]
}

js_library("image_editor_toolbar") {
  deps = [
    ":image_util",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/elements:files_tooltip",
    "//ui/file_manager/gallery/js/:gallery_util",
  ]
  externs_list = [ "//ui/file_manager/externs/paper_elements.js" ]
}

js_library("image_encoder") {
  deps = [
    ":image_util",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_item",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("image_encoder.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/gallery/js/image_editor/image_encoder.m.js",
  ]
  deps = [
    ":image_util.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_item.m",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_unittest("image_encoder_unittest.m") {
  deps = [
    ":image_encoder.m",
    ":test_util.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/base/js:test_error_reporting.m",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_item.m",
  ]
  externs_list = [ "//ui/file_manager/externs/metadata_worker_window.js" ]
}

js_library("image_loader") {
  deps = [
    ":image_util",
    "//ui/file_manager/file_manager/common/js:file_type",
    "//ui/file_manager/file_manager/common/js:metrics_base",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_model",
    "//ui/file_manager/gallery/js/:gallery_item",
    "//ui/file_manager/image_loader:image_loader_client",
  ]
}

js_library("image_resize") {
  deps = [
    ":image_editor_mode",
    "//ui/file_manager/file_manager/foreground/js/ui:files_alert_dialog",
  ]
}

js_library("image_transform") {
  deps = [
    ":commands",
    ":image_buffer",
    ":image_editor_mode",
    ":image_editor_toolbar",
    ":image_util",
    ":viewport",
  ]
}

js_library("image_util") {
  deps = [ "//ui/webui/resources/js:assert" ]
}

js_library("image_util.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/gallery/js/image_editor/image_util.m.js",
  ]
  deps = [ "//ui/webui/resources/js:assert.m" ]

  extra_deps = [ ":modulize" ]
}

js_library("image_view") {
  deps = [
    ":image_buffer",
    ":image_loader",
    ":image_util",
    ":viewport",
    "//ui/file_manager/file_manager/common/js:metrics_base",
    "//ui/file_manager/file_manager/foreground/js:thumbnail_loader",
    "//ui/file_manager/gallery/js/:gallery_item",
    "//ui/webui/resources/js:assert",
  ]
}

js_unittest("image_view_unittest") {
  deps = [
    ":image_view",
    "//ui/file_manager/gallery/js/:mock_gallery_item",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("test_util") {
}

js_library("test_util.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/gallery/js/image_editor/test_util.m.js" ]
  deps = [ "//ui/webui/resources/js:assert.m" ]

  extra_deps = [ ":modulize" ]
}

js_library("viewport") {
  deps = [
    ":image_util",
    "//ui/webui/resources/js/cr:event_target",
  ]
}

js_test_gen_html("js_test_gen_html_modules") {
  deps = [
    ":exif_encoder_unittest.m",
    ":image_encoder_unittest.m",
  ]
  js_module = true

  closure_flags =
      strict_error_checking_closure_args + [
        "js_module_root=./gen/ui",
        "js_module_root=../../ui",
        "browser_resolver_prefix_replacements=\"chrome://test/=./\"",
      ]
}

js_test_gen_html("js_test_gen_html") {
  deps = [ ":image_view_unittest" ]
  mocks = [ "//ui/file_manager/file_manager/foreground/js/metadata/metadata_dispatcher_mock_deps.js" ]
}

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_jsmodules",
    ":closure_compile_module",
    ":js_test_gen_html_modules_type_check_auto",
    ":js_test_gen_html_type_check_auto",
  ]
}

js_modulizer("modulize") {
  input_files = [
    "exif_encoder.js",
    "image_encoder.js",
    "image_util.js",
    "test_util.js",
  ]
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_jsmodules",
    ":closure_compile_module",
    ":js_test_gen_html_modules_type_check_auto",
  ]
}

js_type_check("closure_compile_jsmodules") {
  is_polymer3 = true
  deps = [
    ":background.m",
    ":error_util.m",
    ":main.m",
    ":test_util.m",
    ":video_player.m",
    ":video_player_metrics.m",
    ":video_player_native_controls.m",
  ]
}

js_type_check("closure_compile_module") {
  uses_legacy_modules = true
  deps = [
    ":background",
    ":closure_compile_externs",
    ":error_util",
    ":video_player",
    ":video_player_metrics",
    ":video_player_native_controls",
  ]
}

js_library("closure_compile_externs") {
  sources = []
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/media_player_private.js",
    "$externs_path/metrics_private.js",
    "$externs_path/picture_in_picture.js",
    "//ui/file_manager/externs/chrome_cast.js",
    "//ui/file_manager/externs/platform.js",
  ]
}

js_library("background") {
  deps = [
    ":error_util",
    "//ui/file_manager/file_manager/background/js:app_window_wrapper",
    "//ui/file_manager/file_manager/background/js:background_base",
  ]
}

js_library("background.m") {
  sources = [ "$root_gen_dir/ui/file_manager/video_player/js/background.m.js" ]
  deps = [
    "//ui/file_manager/externs/background:background_base.m",
    "//ui/file_manager/file_manager/background/js:app_window_wrapper.m",
    "//ui/file_manager/file_manager/background/js:background_base.m",
    "//ui/file_manager/file_manager/common/js:util.m",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("error_util") {
}

js_library("error_util.m") {
  sources = [ "$root_gen_dir/ui/file_manager/video_player/js/error_util.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("test_util") {
}

js_library("test_util.m") {
  sources = [ "$root_gen_dir/ui/file_manager/video_player/js/test_util.m.js" ]
  deps = [
    ":background.m",
    "//ui/file_manager/file_manager/background/js:test_util_base.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("video_player_native_controls") {
  deps = [
    "//ui/file_manager/base/js:app_util",
    "//ui/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:util",
  ]
  externs_list = [ "//ui/file_manager/externs/platform.js" ]
}

js_library("video_player_native_controls.m") {
  sources = [ "$root_gen_dir/ui/file_manager/video_player/js/video_player_native_controls.m.js" ]
  deps = [
    "//ui/file_manager/base/js:app_util.m",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]

  externs_list = [ "//ui/file_manager/externs/platform.js" ]

  extra_deps = [ ":modulize" ]
}

js_unittest("video_player_native_controls_unittest.m") {
  deps = [
    ":video_player_native_controls.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/base/js:mock_chrome.m",
  ]
}

js_library("video_player") {
  deps = [
    ":error_util",
    ":video_player_metrics",
    ":video_player_native_controls",
    "//ui/file_manager/base/js:app_util",
    "//ui/file_manager/base/js:filtered_volume_manager",
    "//ui/file_manager/file_manager/common/js:metrics_base",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/webui/resources/js/cr/ui:menu",
    "//ui/webui/resources/js/cr/ui:menu_item",
  ]
}

js_library("video_player.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/video_player/js/video_player.m.js" ]
  deps = [
    ":error_util.m",
    ":video_player_metrics.m",
    ":video_player_native_controls.m",
    "//ui/file_manager/base/js:app_util.m",
    "//ui/file_manager/base/js:filtered_volume_manager.m",
    "//ui/file_manager/base/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/common/js:util.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("video_player_metrics") {
  deps = [ "//ui/file_manager/file_manager/common/js:metrics_base" ]
}

js_library("video_player_metrics.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/video_player/js/video_player_metrics.m.js",
  ]
  deps = [ "//ui/file_manager/file_manager/common/js:metrics_base.m" ]

  extra_deps = [ ":modulize" ]
}

js_test_gen_html("js_test_gen_html_modules") {
  deps = [ ":video_player_native_controls_unittest.m" ]
  is_polymer3 = true

  closure_flags =
      strict_error_checking_closure_args + [
        "generate_exports=false",
        "js_module_root=./gen/ui",
        "js_module_root=../../ui",
        "browser_resolver_prefix_replacements=\"chrome://test/=./\"",
        "hide_warnings_for=third_party/",
      ]
}

js_library("main.m") {
  deps = [ ":video_player.m" ]
}

js_library("main_background.m") {
  deps = [
    ":background.m",
    ":error_util.m",
    ":test_util.m",
    ":video_player_metrics.m",
  ]
}

js_modulizer("modulize") {
  input_files = [
    "video_player_native_controls.js",
    "video_player_metrics.js",
    "error_util.js",
    "test_util.js",
    "background.js",
    "video_player.js",
  ]
}

preprocess_folder =
    rebase_path("$target_gen_dir/../../preprocessed/video_player/js",
                root_build_dir)

optimize_webui("build") {
  host = "video_player"

  input = preprocess_folder
  js_module_in_files = [ "main.m.js" ]

  js_out_files = [ "main.m.rollup.js" ]

  deps = [
    ":main.m",
    "//ui/file_manager:preprocess_generated",
    "//ui/file_manager:preprocess_static",
    "//ui/webui/resources:preprocess",
  ]
}

optimize_webui("build_background") {
  host = "video_player"

  input = preprocess_folder
  js_module_in_files = [ "main_background.m.js" ]

  js_out_files = [ "main_background.m.rollup.js" ]

  deps = [
    ":main_background.m",
    "//ui/file_manager:preprocess_generated",
    "//ui/file_manager:preprocess_static",
    "//ui/webui/resources:preprocess",
  ]
}

# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")

visibility = [ "//ui/file_manager/file_manager/foreground/*" ]

group("closure_compile") {
  testonly = true
  visibility += [ "//ui/file_manager:closure_compile" ]
  deps = [
    ":closure_compile_module",
    ":js_test_gen_html_type_check_auto",
  ]
}

js_type_check("closure_compile_module") {
  deps = [
    ":action_model_ui",
    ":actions_submenu",
    ":banners",
    ":closure_compile_externs",
    ":combobutton",
    ":commandbutton",
    ":default_task_dialog",
    ":dialog_footer",
    ":directory_tree",
    ":drag_selector",
    ":empty_folder",
    ":file_grid",
    ":file_list_selection_model",
    ":file_manager_dialog_base",
    ":file_manager_ui",
    ":file_metadata_formatter",
    ":file_table",
    ":file_table_list",
    ":file_tap_handler",
    ":files_alert_dialog",
    ":files_confirm_dialog",
    ":files_menu",
    ":gear_menu",
    ":import_crostini_image_dialog",
    ":install_linux_package_dialog",
    ":list_container",
    ":location_line",
    ":multi_menu",
    ":progress_center_panel",
    ":providers_menu",
    ":search_box",
    ":suggest_apps_dialog",
    "table:table",
    "table:table_column",
    "table:table_column_model",
    "table:table_header",
    "table:table_list",
    "table:table_splitter",
  ]
}

js_library("closure_compile_externs") {
  sources = []
  externs_list = [
    "$externs_path/metrics_private.js",
    "//ui/file_manager/externs/background/crostini.js",
    "//ui/file_manager/externs/background/drive_sync_handler.js",
    "//ui/file_manager/externs/background/file_browser_background.js",
    "//ui/file_manager/externs/background/file_operation_manager.js",
    "//ui/file_manager/externs/background/import_history.js",
    "//ui/file_manager/externs/background_window.js",
    "//ui/file_manager/externs/chrome_echo_private.js",
    "//ui/file_manager/externs/chrome_webstore_widget_private.js",
    "//ui/file_manager/externs/css_rule.js",
    "//ui/file_manager/externs/drag_target.js",
    "//ui/file_manager/externs/entries_changed_event.js",
    "//ui/file_manager/externs/gallery_foreground.js",
    "//ui/file_manager/externs/menu_item_update_event.js",
    "//ui/file_manager/externs/paper_elements.js",
    "//ui/file_manager/externs/platform.js",
    "//ui/file_manager/externs/search_item.js",
  ]
}

js_library("a11y_announce") {
}

js_library("action_model_ui") {
  deps = [
    ":files_alert_dialog",
    ":list_container",
  ]
}

js_library("actions_submenu") {
  deps = [
    "//ui/file_manager/file_manager/foreground/js:actions_model",
    "//ui/webui/resources/js/cr/ui:command",
    "//ui/webui/resources/js/cr/ui:menu",
    "//ui/webui/resources/js/cr/ui:menu_item",
  ]
}

js_unittest("actions_submenu_unittest") {
  deps = [
    ":actions_submenu",
    "//ui/file_manager/file_manager/foreground/js:mock_actions_model",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("banners") {
  deps = [
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js:directory_model",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js/cr:event_target",
  ]
  externs_list = [ "//ui/file_manager/externs/chrome_echo_private.js" ]
}

js_library("combobutton") {
  deps = [
    ":files_menu",
    "//ui/file_manager/file_manager/foreground/elements:files_toggle_ripple",
    "//ui/webui/resources/js/cr/ui:menu_button",
  ]
  externs_list = [ "//ui/file_manager/externs/paper_elements.js" ]
}

js_library("commandbutton") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js/cr:ui",
    "//ui/webui/resources/js/cr/ui:command",
  ]
}

js_library("default_task_dialog") {
  deps = [
    ":file_manager_dialog_base",
    "//ui/webui/resources/js/cr/ui:array_data_model",
    "//ui/webui/resources/js/cr/ui:list",
    "//ui/webui/resources/js/cr/ui:list_single_selection_model",
  ]
}

js_library("dialog_footer") {
  deps = [
    "//ui/file_manager/file_manager/common/js:file_type",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js:dialog_type",
    "//ui/file_manager/file_manager/foreground/js:file_list_model",
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
  ]
}

js_library("directory_tree") {
  deps = [
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js:directory_model",
    "//ui/file_manager/file_manager/foreground/js:navigation_list_model",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_model",
    "//ui/webui/resources/js/cr/ui:context_menu_handler",
    "//ui/webui/resources/js/cr/ui:menu",
    "//ui/webui/resources/js/cr/ui:tree",
  ]
}

js_unittest("directory_tree_unittest") {
  deps = [
    ":directory_tree",
    "//ui/file_manager/base/js:mock_chrome",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/file_manager/file_manager/background/js:mock_volume_manager",
    "//ui/file_manager/file_manager/foreground/js:fake_android_app_list_model",
    "//ui/file_manager/file_manager/foreground/js:mock_directory_model",
    "//ui/file_manager/file_manager/foreground/js:mock_folder_shortcut_data_model",
    "//ui/file_manager/file_manager/foreground/js:mock_navigation_list_model",
  ]
}

js_library("drag_selector") {
  deps = [
    "//ui/webui/resources/js/cr:ui",
    "//ui/webui/resources/js/cr/ui:list",
  ]
  externs_list = [ "//ui/file_manager/externs/drag_target.js" ]
}

js_library("empty_folder") {
  deps = [
    "//ui/webui/resources/js:util",
  ]
}

js_library("file_grid") {
  deps = [
    ":drag_selector",
    ":file_table_list",
    ":file_tap_handler",
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/file_manager/file_manager/common/js:file_type",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js:list_thumbnail_loader",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_model",
    "//ui/webui/resources/js/cr/ui:grid",
  ]
  externs_list = [ "//ui/file_manager/externs/background/import_history.js" ]
}

js_library("file_list_selection_model") {
  deps = [
    "//ui/webui/resources/js/cr/ui:list_selection_model",
    "//ui/webui/resources/js/cr/ui:list_single_selection_model",
  ]
}

js_unittest("file_list_selection_model_unittest") {
  deps = [
    ":file_list_selection_model",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("file_manager_dialog_base") {
  deps = [
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js/cr:ui",
    "//ui/webui/resources/js/cr/ui:dialogs",
  ]
  externs_list = [ "$externs_path/chrome_extensions.js" ]
}

js_unittest("file_manager_dialog_base_unittest") {
  deps = [
    ":file_manager_dialog_base",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("file_manager_ui") {
  deps = [
    ":a11y_announce",
    ":action_model_ui",
    ":actions_submenu",
    ":banners",
    ":combobutton",
    ":default_task_dialog",
    ":dialog_footer",
    ":directory_tree",
    ":empty_folder",
    ":file_grid",
    ":file_table",
    ":files_alert_dialog",
    ":files_confirm_dialog",
    ":gear_menu",
    ":import_crostini_image_dialog",
    ":install_linux_package_dialog",
    ":list_container",
    ":location_line",
    ":multi_menu",
    ":progress_center_panel",
    ":providers_menu",
    ":search_box",
    ":suggest_apps_dialog",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/elements:files_message",
    "//ui/file_manager/file_manager/foreground/elements:files_toast",
    "//ui/file_manager/file_manager/foreground/elements:files_toggle_ripple",
    "//ui/file_manager/file_manager/foreground/elements:files_tooltip",
    "//ui/file_manager/file_manager/foreground/elements:xf_activity_complete",
    "//ui/file_manager/file_manager/foreground/elements:xf_button",
    "//ui/file_manager/file_manager/foreground/elements:xf_circular_progress",
    "//ui/file_manager/file_manager/foreground/elements:xf_display_panel",
    "//ui/file_manager/file_manager/foreground/elements:xf_panel_item",
    "//ui/file_manager/file_manager/foreground/js:launch_param",
    "//ui/file_manager/file_manager/foreground/js:providers_model",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr/ui:dialogs",
    "//ui/webui/resources/js/cr/ui:menu",
  ]
}

js_library("file_metadata_formatter") {
  deps = [
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/webui/resources/js/cr:event_target",
  ]
}

js_library("file_table") {
  deps = [
    ":a11y_announce",
    ":drag_selector",
    ":file_metadata_formatter",
    ":file_table_list",
    "table:table",
    "//ui/file_manager/file_manager/foreground/js:file_list_model",
    "//ui/file_manager/file_manager/foreground/js:list_thumbnail_loader",
    "//ui/webui/resources/js:cr",
  ]
  externs_list = [ "//ui/file_manager/externs/background/import_history.js" ]
}

js_unittest("file_table_unittest") {
  deps = [
    ":file_table",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("file_table_list") {
  deps = [
    ":file_list_selection_model",
    ":file_tap_handler",
    "table:table_list",
    "//ui/file_manager/file_manager/common/js:file_type",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_model",
    "//ui/webui/resources/js/cr:ui",
    "//ui/webui/resources/js/cr/ui:list_selection_controller",
  ]
}

js_unittest("file_table_list_unittest") {
  deps = [
    ":file_table",
    ":file_table_list",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/file_manager/file_manager/background/js:mock_volume_manager",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js/metadata:mock_metadata",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("file_tap_handler") {
  deps = [
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_unittest("file_tap_handler_unittest") {
  deps = [
    ":file_tap_handler",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

# TODO(tapted): Move this into //ui/file_manager/base.
js_library("files_alert_dialog") {
  visibility += [ "//ui/file_manager/gallery/*" ]
  deps = [
    "//ui/webui/resources/js/cr/ui:dialogs",
  ]
}

# TODO(tapted): Move this into //ui/file_manager/base.
js_library("files_confirm_dialog") {
  visibility += [ "//ui/file_manager/gallery/*" ]
  deps = [
    "//ui/webui/resources/js/cr/ui:dialogs",
  ]
}

js_library("files_menu") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js/cr/ui:menu",
    "//ui/webui/resources/js/cr/ui:menu_item",
  ]
  externs_list = [ "//ui/file_manager/externs/paper_elements.js" ]
}

js_library("install_linux_package_dialog") {
  deps = [
    ":file_manager_dialog_base",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_unittest("install_linux_package_dialog_unittest") {
  deps = [
    ":install_linux_package_dialog",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("import_crostini_image_dialog") {
  deps = [
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js/cr:ui",
    "//ui/webui/resources/js/cr/ui:dialogs",
  ]
}

js_library("gear_menu") {
  deps = [
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_library("list_container") {
  deps = [
    ":file_grid",
    ":file_table",
    "//ui/webui/resources/js/cr/ui:list_item",
  ]
}

js_library("location_line") {
  deps = [
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/file_manager/externs:volume_manager",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types",
    "//ui/file_manager/file_manager/common/js:metrics",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_library("multi_menu") {
  deps = [
    "//ui/webui/resources/js:event_tracker",
    "//ui/webui/resources/js/cr/ui:menu",
    "//ui/webui/resources/js/cr/ui:menu_button",
    "//ui/webui/resources/js/cr/ui:menu_item",
    "//ui/webui/resources/js/cr/ui:position_util",
  ]
}

js_unittest("multi_menu_unittest") {
  deps = [
    ":multi_menu",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("progress_center_panel") {
  # The progress_center on the background page maintains a list of panels.
  visibility += [ "//ui/file_manager/file_manager/background/*" ]

  deps = [
    "//ui/file_manager/file_manager/common/js:progress_center_common",
    "//ui/file_manager/file_manager/foreground/js:progress_center_item_group",
  ]
  externs_list = [ "//ui/file_manager/externs/progress_center_panel.js" ]
}

js_library("providers_menu") {
  deps = [
    ":directory_tree",
    ":files_menu",
    "//ui/file_manager/file_manager/foreground/js:providers_model",
    "//ui/webui/resources/js/cr/ui:menu",
    "//ui/webui/resources/js/cr/ui:position_util",
  ]
  externs_list = [ "//ui/file_manager/externs/menu_item_update_event.js" ]
}

js_library("search_box") {
  deps = [
    ":autocomplete_list",
    "//ui/file_manager/file_manager/common/js:file_type",
    "//ui/file_manager/file_manager/common/js:metrics",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/elements:files_toggle_ripple",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr:event_target",
    "//ui/webui/resources/js/cr:ui",
  ]
  externs_list = [ "//ui/file_manager/externs/search_item.js" ]
}

js_library("autocomplete_list") {
  deps = [
    "//ui/webui/resources/js/cr/ui:list",
    "//ui/webui/resources/js/cr/ui:list_single_selection_model",
    "//ui/webui/resources/js/cr/ui:position_util",
  ]
}

js_library("suggest_apps_dialog") {
  deps = [
    ":file_manager_dialog_base",
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/file_manager/file_manager/common/js:metrics",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/cws_widget:cws_widget_container",
    "//ui/file_manager/file_manager/foreground/js:constants",
    "//ui/file_manager/file_manager/foreground/js:launch_param",
    "//ui/file_manager/file_manager/foreground/js:providers_model",
    "//ui/file_manager/file_manager/foreground/js:web_store_utils",
  ]
  externs_list =
      [ "//ui/file_manager/externs/chrome_webstore_widget_private.js" ]
}

js_test_gen_html("js_test_gen_html") {
  deps = [
    ":actions_submenu_unittest",
    ":directory_tree_unittest",
    ":file_list_selection_model_unittest",
    ":file_manager_dialog_base_unittest",
    ":file_table_list_unittest",
    ":file_table_unittest",
    ":file_tap_handler_unittest",
    ":install_linux_package_dialog_unittest",
    ":multi_menu_unittest",
  ]
}

# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/util/version.gni")
import("//remoting/remoting_options.gni")

template("remoting_apk_tmpl") {
  android_apk(target_name) {
    forward_variables_from(invoker, "*")

    if (!is_java_debug) {
      proguard_enabled = true
      enable_multidex = false
      disable_checkdiscard = true
      if (!defined(proguard_configs)) {
        proguard_configs = []
      }
      proguard_configs += [
        "//remoting/android/proguard.flags",
        "//base/android/proguard/chromium_apk.flags",
        "//base/android/proguard/chromium_code.flags",
      ]
    }

    android_manifest = "$root_gen_dir/remoting/android/AndroidManifest.xml"
    android_manifest_dep = "//remoting/android:remoting_apk_manifest"
    target_sdk_version = 28
    shared_libraries = [ "//remoting/client/jni:remoting_client_jni" ]
    version_name = chrome_version_name
    version_code = chrome_version_code
  }
}

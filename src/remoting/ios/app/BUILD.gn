# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/build/config/remoting_build.gni")
import("//remoting/ios/app/remoting_ios_tmpl.gni")

group("all") {
  testonly = true

  deps = [
    ":ios_remoting_app",
  ]
}

# source set to be used by both external and internal app.
source_set("common_source_set") {
  sources = [
    "app_delegate.h",
    "app_delegate.mm",
    "app_initializer.h",
    "app_view_controller.h",
    "client_connection_view_controller.h",
    "client_connection_view_controller.mm",
    "first_launch_view_controller.h",
    "first_launch_view_controller.mm",
    "first_launch_view_presenter.h",
    "first_launch_view_presenter.mm",
    "help_and_feedback.h",
    "help_and_feedback.mm",
    "help_view_controller.h",
    "help_view_controller.mm",
    "host_collection_header_view.h",
    "host_collection_header_view.mm",
    "host_collection_view_cell.h",
    "host_collection_view_cell.mm",
    "host_collection_view_controller.h",
    "host_collection_view_controller.mm",
    "host_fetching_error_view_controller.h",
    "host_fetching_error_view_controller.mm",
    "host_fetching_view_controller.h",
    "host_fetching_view_controller.mm",
    "host_setup_footer_view.h",
    "host_setup_footer_view.mm",
    "host_setup_header_view.h",
    "host_setup_header_view.mm",
    "host_setup_view_cell.h",
    "host_setup_view_cell.mm",
    "host_setup_view_controller.h",
    "host_setup_view_controller.mm",
    "host_view_controller.h",
    "host_view_controller.mm",
    "main.mm",
    "physical_keyboard_detector.h",
    "physical_keyboard_detector.mm",
    "pin_entry_view.h",
    "pin_entry_view.mm",
    "refresh_control_provider.h",
    "refresh_control_provider.mm",
    "remoting_theme.h",
    "remoting_theme.mm",
    "remoting_view_controller.h",
    "remoting_view_controller.mm",
    "session_reconnect_view.h",
    "session_reconnect_view.mm",
    "side_menu_items.h",
    "side_menu_items.mm",
    "user_status_presenter.h",
    "user_status_presenter.mm",
    "view_utils.h",
    "view_utils.mm",
    "web_view_controller.h",
    "web_view_controller.mm",
  ]

  deps = [
    "//base",
    "//google_apis",
    "//remoting/base",
    "//remoting/client",
    "//remoting/client/input",
    "//remoting/ios:ios_core",
    "//remoting/ios/app/resources:assets",
    "//remoting/ios/app/settings",
    "//remoting/ios/audio",
    "//remoting/ios/display",
    "//remoting/ios/domain",
    "//remoting/ios/mdc",
    "//remoting/ios/persistence",
    "//remoting/protocol",
    "//remoting/resources",
    "//ui/base",
    "//ui/gfx",
    "//ui/resources",
  ]

  foreach(locale, remoting_locales_with_underscores) {
    deps += [ "//remoting/ios/app/resources:locale_${locale}_bundle_data" ]
  }

  public_deps = [
    "//ios/third_party/material_components_ios",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("app_source_set") {
  sources = [
    "app_initializer_chromium.mm",
    "app_view_controller_chromium.mm",
    "refresh_control_provider_chromium.h",
    "refresh_control_provider_chromium.mm",
    "remoting_menu_view_controller.h",
    "remoting_menu_view_controller.mm",
  ]
  deps = [
    ":common_source_set",
    "//base",
  ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

ios_remoting_app_tmpl("ios_remoting_app") {
  output_name = "chromoting"
  info_plist_path = "resources/Info.plist"
  entitlements_path = "resources/Remoting.entitlements"
  deps = [
    ":app_source_set",
    "//remoting/ios/app/resources:launchscreen_assets",
    "//remoting/ios/app/resources:remoting_icons",
  ]
}

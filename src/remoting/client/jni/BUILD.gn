# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//remoting/android/client_java_tmpl.gni")
import("//remoting/android/remoting_apk_tmpl.gni")
import("//remoting/remoting_options.gni")

group("jni") {
  testonly = true

  deps = [
    ":remoting_client_jni",
  ]
}

shared_library("remoting_client_jni") {
  deps = [
    "//remoting/android:jni_headers",
    "//remoting/base",
    "//remoting/client",
    "//remoting/client/display",
    "//remoting/client/notification",
    "//remoting/proto/remoting/v1:directory_grpc_library",
    "//remoting/protocol",
    "//ui/gfx",
  ]
  sources = [
    "egl_thread_context.cc",
    "egl_thread_context.h",
    "jni_client.cc",
    "jni_client.h",
    "jni_directory_service.cc",
    "jni_directory_service.h",
    "jni_gl_display_handler.cc",
    "jni_gl_display_handler.h",
    "jni_notification_presenter.cc",
    "jni_notification_presenter.h",
    "jni_oauth_token_getter.cc",
    "jni_oauth_token_getter.h",
    "jni_runtime_delegate.cc",
    "jni_runtime_delegate.h",
    "jni_touch_event_data.cc",
    "jni_touch_event_data.h",
    "remoting_jni_onload.cc",
  ]
  libs = [
    "android",
    "EGL",
  ]
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [
    "//build/config/android:hide_all_but_jni",
    "//remoting/build/config:enable_webrtc_remoting_client",
  ]
  assert_no_deps = [ "//third_party/ffmpeg:*" ]
}

java_cpp_enum("jni_enums") {
  sources = [
    "jni_directory_service.h",
    "jni_oauth_token_getter.h",
  ]
}

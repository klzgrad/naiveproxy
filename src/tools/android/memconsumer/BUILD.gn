# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

android_resources("memconsumer_apk_resources") {
  sources = [
    "java/res/drawable/icon.png",
    "java/res/drawable/notification_icon.png",
  ]
  android_manifest = "java/AndroidManifest.xml"
}

android_apk("memconsumer_apk") {
  apk_name = "MemConsumer"
  android_manifest = "java/AndroidManifest.xml"
  min_sdk_version = 16
  target_sdk_version = 16
  sources = [
    "java/src/org/chromium/memconsumer/MemConsumer.java",
    "java/src/org/chromium/memconsumer/ResidentService.java",
  ]
  shared_libraries = [ ":libmemconsumer" ]

  deps = [
    ":memconsumer_apk_resources",
    "//base:base_java",
  ]
}

shared_library("libmemconsumer") {
  output_name = "memconsumer"
  sources = [ "memconsumer_hook.cc" ]
  libs = [ "log" ]
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]

  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]
}

# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win)

config("delay_load_mf") {
  ldflags = [
    "/DELAYLOAD:mf.dll",
    "/DELAYLOAD:mfplat.dll",
    "/DELAYLOAD:mfreadwrite.dll",
  ]
}

component("win") {
  output_name = "media_win_util"
  defines = [ "MF_INITIALIZER_IMPLEMENTATION" ]
  set_sources_assignment_filter([])
  sources = [
    "mf_helpers.cc",
    "mf_helpers.h",
    "mf_initializer.cc",
    "mf_initializer.h",
    "mf_initializer_export.h",
  ]
  set_sources_assignment_filter(sources_assignment_filter)
  configs += [
    # TODO(crbug.com/167187): Fix size_t to int truncations.
    "//build/config/compiler:no_size_t_to_int_warning",
    "//media:media_config",
  ]
  deps = [
    "//base",
    "//media:shared_memory_support",
  ]
  libs = [
    "mf.lib",
    "mfplat.lib",
    "mfreadwrite.lib",
  ]

  # MediaFoundation is not available on Windows N, so must be delay loaded.
  all_dependent_configs = [ ":delay_load_mf" ]
}

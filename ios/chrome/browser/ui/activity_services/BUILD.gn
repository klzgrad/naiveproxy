# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("activity_services") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "activity_service_controller.h",
    "activity_service_controller.mm",
    "activity_type_util.h",
    "activity_type_util.mm",
    "appex_constants.h",
    "appex_constants.mm",
    "chrome_activity_item_source.h",
    "chrome_activity_item_source.mm",
    "chrome_activity_item_thumbnail_generator.h",
    "chrome_activity_item_thumbnail_generator.mm",
    "print_activity.h",
    "print_activity.mm",
    "reading_list_activity.h",
    "reading_list_activity.mm",
    "share_protocol.h",
    "share_to_data.h",
    "share_to_data.mm",
    "share_to_data_builder.h",
    "share_to_data_builder.mm",
  ]
  deps = [
    "resources:activity_services_print",
    "resources:activity_services_reading_list",
    "//base",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/passwords",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/ui",
    "//ios/chrome/browser/ui/activity_services/requirements",
    "//ios/chrome/browser/ui/commands",
    "//net",
    "//ui/base",
    "//url",
  ]
  public_deps = [
    "//ios/third_party/material_components_ios",
  ]
  libs = [
    "MobileCoreServices.framework",
    "UIKit.framework",
  ]
}

source_set("coordinator") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "activity_service_legacy_coordinator.h",
    "activity_service_legacy_coordinator.mm",
  ]
  deps = [
    ":activity_services",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/passwords",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/ui/activity_services/requirements",
    "//ios/chrome/browser/ui/commands",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "activity_service_controller_unittest.mm",
    "activity_type_util_unittest.mm",
    "chrome_activity_item_thumbnail_generator_unittest.mm",
    "share_to_data_builder_unittest.mm",
  ]
  deps = [
    ":activity_services",
    "//base",
    "//base/test:test_support",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/passwords",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/ui/activity_services/requirements",
    "//ios/chrome/browser/ui/commands",
    "//ios/testing:ocmock_support",
    "//ios/web",
    "//ios/web/public/test",
    "//testing/gtest",
    "//third_party/ocmock",
    "//ui/base",
    "//ui/base:test_support",
  ]
}

source_set("eg_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "activity_service_controller_egtest.mm",
  ]
  deps = [
    "//base",
    "//components/strings",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/ui",
    "//ios/chrome/browser/ui:ui_internal",
    "//ios/chrome/test/app:test_support",
    "//ios/chrome/test/earl_grey:test_support",
    "//ios/third_party/material_components_ios",
    "//ios/web:earl_grey_test_support",
    "//ios/web/public/test",
    "//ios/web/public/test/http_server",
    "//ui/base",
  ]
  libs = [ "XCTest.framework" ]
}
